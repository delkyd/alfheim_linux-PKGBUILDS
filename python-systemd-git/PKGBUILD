# Maintainer: Alexander GÃ¶rtz <aur@nyloc.de>

pkgname=python-systemd-git
pkgver=231.r3.g87a9d64
pkgrel=1
pkgdesc="Systemd python bindings"
arch=('any')
url="https://github.com/systemd/python-systemd"
depends=('python' 'systemd')
makedepends=('python-setuptools')
provides=('python-systemd')
conflicts=('python-systemd')
license=('GPL3')
arch=('any')
source=("${pkgname}"::"git+https://github.com/systemd/python-systemd.git")
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  ( set -o pipefail
    git describe --long --tags 2>/dev/null | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

build() {
    cd "${srcdir}/${pkgname}"
    make build
}

package() {
    cd "${srcdir}/${pkgname}"
    make DESTDIR="$pkgdir" install 
}
