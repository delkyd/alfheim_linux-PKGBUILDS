# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=python-socketio
pkgname=('python-socketio' 'python2-socketio')
pkgver=1.8.1
pkgrel=1
pkgdesc='Python implementation of the Socket.IO realtime server'
url='https://github.com/miguelgrinberg/python-socketio'
arch=('any')
license=('MIT')
makedepends=('python-setuptools' 'python-sphinx' 'python-six' 'python-engineio'
             'python2-setuptools' 'python2-sphinx' 'python2-six' 'python2-engineio')
checkdepends=('python-pytest' 'python-mock' 'python2-pytest' 'python2-mock')
options=('!makeflags')
source=(${pkgbase}-${pkgver}.tar.gz::https://github.com/miguelgrinberg/python-socketio/archive/v${pkgver}.tar.gz)
sha256sums=('de1bdd8f83b610c13156e0d1ca9b86d84bb33cb5ec403a7f3a2de096803d3b70')
sha512sums=('071c52d7783d8d42ce2c168b925dc14aa4c89be00180fa0d963943c9c6d97f827092033d4cad5ebf9f803911cad7e217260548373e8b654fcb0aa29eebc20d5a')

prepare() {
  cp -ra ${pkgbase}-${pkgver}{,-py2}
}

build() {
  (cd ${pkgbase}-${pkgver}
    python setup.py build
    make -C docs man text SPHINXBUILD=sphinx-build
  )
  (cd ${pkgbase}-${pkgver}-py2
    python2 setup.py build
    make -C docs man text SPHINXBUILD=sphinx-build2
  )
}

check() {
  (cd ${pkgbase}-${pkgver}
    py.test
  )
  (cd ${pkgbase}-${pkgver}-py2
    py.test2
  )
}

package_python-socketio() {
  depends=('python-six' 'python-engineio')

  cd ${pkgbase}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 README.rst "${pkgdir}/usr/share/doc/${pkgname}/README"
  install -Dm 644 docs/_build/text/index.txt "${pkgdir}/usr/share/doc/${pkgname}/DOCUMENTATION"
  install -Dm 644 docs/_build/man/socketio.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

package_python2-socketio() {
  depends=('python2-six' 'python2-engineio')

  cd ${pkgbase}-${pkgver}-py2
  python2 setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 README.rst "${pkgdir}/usr/share/doc/${pkgname}/README"
  install -Dm 644 docs/_build/text/index.txt "${pkgdir}/usr/share/doc/${pkgname}/DOCUMENTATION"
  install -Dm 644 docs/_build/man/socketio.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

# vim: ts=2 sw=2 et:
