# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=sagemath-doc
pkgname=(sagemath-doc sagemath-src)
pkgver=8.0
pkgrel=1
arch=(any)
url="http://www.sagemath.org"
license=(GPL)
makedepends=(sagemath sage-notebook python2-pyzmq python2-pillow python2-docutils tachyon
             python2-jupyter_client python2-pkgconfig cython2 thebe python2-sphinx python2-ipywidgets)
source=("$pkgbase-$pkgver.tar.gz::https://github.com/sagemath/sage/archive/$pkgver.tar.gz"
	package.patch fes02.patch docbuild_main.patch r-no-readline.patch latte-count.patch sagemath-ecl-no-sigfpe.patch
        sagemath-sphinx-1.6.patch)
sha256sums=('91df3e303add71616e24b38c04789f66d57ada7b1017e5369e850c42d5e8753d'
            '4a2297e4d9d28f0b3a1f58e1b463e332affcb109eafde44837b1657e309c8212'
            'a39da083c038ada797ffc5bedc9ba47455a3f77057d42f86484ae877ef9172ea'
            '187b75d3ce202126012ff20b6316277b0a003ffdd7f15b820d3b88459d2753c8'
            'ef9f401fa84fe1772af9efee6816643534f2896da4c23b809937b19771bdfbbf'
            'c6836783251d94c00f0229c1e671de86c58c6c6fb0f6959725317817abc64ca8'
            'c31809f887bf9acc45c5bd9dd30bb93e73601d3efbf3016594c3c1d241731c8a'
            '5358b4604dfab126a02bba6c9e436a5c41c66d5bc241ae3299e76ac1e9ae593d')

prepare() {
  cd sage-$pkgver

# Arch-specific patches
# assume all optional packages are installed
  patch -p0 -i ../package.patch
# fix freezes in R interface with readline 7 (Debian)
  patch -p1 -i ../r-no-readline.patch
# use correct latte-count binary name
  patch -p1 -i ../latte-count.patch
# fix Cremona database detection
  sed -e "s|is_package_installed('database_cremona_ellcurve')|os.path.exists('/usr/share/cremona/cremona.db')|" \
   -i src/sage/databases/cremona.py

# Upstream patches
# fix build against libfes 0.2 http://trac.sagemath.org/ticket/15209
  patch -p1 -i ../fes02.patch
# disable SIGFPE for ecl https://trac.sagemath.org/ticket/22191
  patch -p1 -i ../sagemath-ecl-no-sigfpe.patch
# fix doc build with Sphinx 1.6 https://trac.sagemath.org/ticket/23023
  patch -p1 -i ../sagemath-sphinx-1.6.patch

# fix relative imports
  patch -p0 -i ../docbuild_main.patch

# use python2
  sed -e 's|cython %s %s|cython2 %s %s|' -e 's|python setup.py|python2 setup.py|' -i src/sage/misc/cython.py

  mkdir -p local-python
  ln -sr src/sage_setup local-python
}

build() {
  cd sage-$pkgver/src

  export SAGE_LOCAL="/usr"
  export SAGE_SRC="$PWD"
  export SAGE_DOC_SRC="$SAGE_SRC"/doc
  export SAGE_DOC="$SAGE_SRC"/doc
  export SAGE_DOC_MATHJAX=yes
  export PYTHONPATH="/usr/lib/sage/site-packages:$srcdir/sage-$pkgver/local-python"
  python2 sage_setup/docbuild --no-pdf-links -k all html
}

package_sagemath-doc() {
  pkgdesc="HTML documentation and inline help for SageMath"
  depends=(sage-notebook python2-sphinx gap-doc thebe)
  conflicts=(sage-mathematics-doc)
  replaces=(sage-mathematics-doc)

  cd sage-$pkgver/src/doc
 
  mkdir -p "$pkgdir"/usr/share/doc/sage/en
  cp -r en/introspect "$pkgdir"/usr/share/doc/sage/en
  cp -r common "$pkgdir"/usr/share/doc/sage
  cp -r html "$pkgdir"/usr/share/doc/sage

# Replace duplicated files by symlinks (Gentoo)
  cd "$pkgdir"/usr/share/doc/sage
  mv html/en/_static{,.tmp}
  for _dir in `find -name _static` ; do
        rm -r $_dir
        ln -s /usr/share/doc/sage/html/en/_static $_dir
  done
  mv html/en/_static{.tmp,}
}

package_sagemath-src() {
  pkgdesc="Source files for SageMath"
  conflicts=(sage-mathematics-src)
  replaces=(sage-mathematics-src)
  
  mkdir -p "$pkgdir"/usr/share/sage/source
  cp -r sage-$pkgver/src/{sage,sage_setup} "$pkgdir"/usr/share/sage/source
}
