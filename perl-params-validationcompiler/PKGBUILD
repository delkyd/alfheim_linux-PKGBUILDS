# $Id$
# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>

pkgname=perl-params-validationcompiler
_pkgname=Params-ValidationCompiler
pkgver=0.24
pkgrel=3
pkgdesc="Build an optimized subroutine parameter validator once, use it forever"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-role-tiny' 'perl-eval-closure' 'perl-exception-class')
checkdepends=('perl-test-fatal')
url="http://search.cpan.org/dist/Params-ValidationCompiler/"
source=(http://search.cpan.org/CPAN/authors/id/D/DR/DROLSKY/Params-ValidationCompiler-$pkgver.tar.gz)
sha256sums=('a8963747bae618c2b96ec1918ef2b6ea7dd7ff3dbcebc9de6124b2771a4f4287')

build() {
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL="--skipdeps" \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='"$pkgdir"'" \
    PERL_MB_OPT="--installdirs vendor --destdir '"$pkgdir"'" \
    MODULEBUILDRC=/dev/null

  cd "${srcdir}/${_pkgname}-${pkgver}"
  perl Makefile.PL
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make install
}
