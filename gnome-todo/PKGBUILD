# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-todo
pkgver=3.26.1
pkgrel=1
pkgdesc="Task manager for GNOME"
url="https://wiki.gnome.org/Apps/Todo"
arch=(i686 x86_64)
license=(GPL)
depends=(evolution-data-server libpeas)
makedepends=(gobject-introspection appstream-glib git meson)
optdepends=('python3: Some plugins')
groups=(gnome-extra)
_commit=3dfdb6e323e4bbe8d23e51fe1eabf1229d65dd61  # tags/GNOME_TODO_3_26_1^0
source=("git+https://git.gnome.org/browse/gnome-todo#commit=$_commit")
sha256sums=('SKIP')

check() {
  cd build
  meson test
}

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^GNOME_TODO_//;s/_/./g;s/-/+/g'
}

prepare() {
  mkdir build
  cd $pkgname
}

build() {
  cd build
  meson setup --prefix=/usr --buildtype=release ../$pkgname
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}

# vim:set ts=2 sw=2 et:
