# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=libretro-blastem
pkgver=940
pkgrel=1
pkgdesc='Sega Mega Drive core'
arch=('x86_64')
url='https://github.com/libretro/blastem-libretro'
license=('GPL3')
groups=('libretro')
depends=('glibc')
makedepends=('git')
_commit='7b6f4c539dd7727f89e1380db1979785e35ee204'
source=("libretro-blastem::git+https://github.com/libretro/blastem-libretro.git#commit=${_commit}"
        'https://raw.githubusercontent.com/libretro/libretro-super/master/dist/info/blastem_libretro.info')
sha256sums=('SKIP'
            '0314c46e4869b5d90e7b8ae627b7dbfcf5ba29963031312c0f28b4eaf8ea0a05')

pkgver() {
  cd libretro-blastem

  git rev-list --count HEAD
}

build() {
  cd libretro-blastem/libretro

  sed 's/CFLAGS :=/CFLAGS +=/g' -i Makefile

  [[ $CARCH == 'i686' ]] && make WITH_DYNAREC='x86'
  [[ $CARCH == 'x86_64' ]] && make WITH_DYNAREC='x86_64'
}

package() {
  cd libretro-blastem/libretro

  install -dm 755 "${pkgdir}"/usr/{lib/libretro,share/libretro/info}
  install -m 644 blastem_libretro.so "${pkgdir}"/usr/lib/libretro/
  install -m 644 ../../blastem_libretro.info "${pkgdir}"/usr/share/libretro/info/
}

# vim: ts=2 sw=2 et:
