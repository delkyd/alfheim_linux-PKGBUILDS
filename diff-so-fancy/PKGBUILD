# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: icasdri <icasdri at gmail dot com>

pkgname=diff-so-fancy
pkgver=1.0.0
pkgrel=1
pkgdesc="Good-looking diffs with diff-highlight and more"
arch=('any')
url="https://github.com/so-fancy/diff-so-fancy"
license=('MIT')
depends=('git')
checkdepends=('bash-bats-assert' 'bash-bats-support')
source=("$pkgname-$pkgver.tar.gz::https://github.com/so-fancy/diff-so-fancy/archive/v$pkgver.tar.gz"
        system-diff_highlight.patch)
sha384sums=('00361ed7b36f2b4bb1d79430175e32daadfd5590ff5656e304d7e25c739f599f266efec1664cda7e4caf017a9a56316b'
            'ec4d6df1fc6dbf9e2c2f3fdc4d1c4e57e7cb2b085136f5f84cb32761e853cbc5a3a0878a62b467c05531286b19fe185e')

prepare() {
  cd "$srcdir"/$pkgname-$pkgver
  patch -p0 -i ../system-diff_highlight.patch

  rm -r third_party

  sed -e 's|test_helper/bats-support|/usr/lib/bats-support|' \
      -e 's|test_helper/bats-assert|/usr/lib/bats-assert|' \
      -e "s|load'$|load.bash'|" \
      -i test/*.bats
  rm test/diff-highlight.bats
}

check() {
  cd "$srcdir"/$pkgname-$pkgver
  git init
  bats test/*.bats
}

package() {
  cd "$srcdir"/$pkgname-$pkgver

  install -Dm755 diff-so-fancy "$pkgdir"/usr/bin/diff-so-fancy
  install -Dm644 readme.md "$pkgdir"/usr/share/doc/diff-so-fancy/readme.md
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
