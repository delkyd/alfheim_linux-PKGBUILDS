# $Id$
# Maintainer: Jiachen Yang <farseerfc@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=tuntox
pkgver=0.0.6
_commit=fad23001af27f1cd380f7d05270ddcc04244f507
pkgrel=4
pkgdesc='Tunnel TCP connections over the Tox protocol'
arch=('i686' 'x86_64')
url='https://github.com/gjedeer/tuntox'
license=('GPL3')
depends=('toxcore')
makedepends=('cscope')
source=( # "git+https://github.com/gjedeer/tuntox.git#commit=$_commit"  #tag=$pkgver
        "https://github.com/gjedeer/${pkgname}/archive/${pkgver}.tar.gz"
        "https://github.com/gjedeer/${pkgname}/releases/download/${pkgver}/${pkgver}.tar.gz.asc"
       )

sha512sums=('57893516d2186f9830784f23f6312746ea6c64e0c75c84fbb3f1453da6d7c25f29cb9a848c0160b0d93dc0098d46cd923cf13b5bf2c3142c4cc05b0b14a73872'
            'SKIP')
validpgpkeys=('11C1B15A5D5DD662E469928AEBDA6B974ED3D2B7')  # GDR!_ <gdr@gdr.name>

prepare() {
  cd $pkgname-$pkgver
  sed -i "s|DEPS=libsodium toxcore|DEPS=libsodium libtoxcore|" Makefile
}

build() {
  cd $pkgname-$pkgver

  # Makefile requires these, make it happy
  # pending issue: https://github.com/gjedeer/tuntox/issues/35
  mkdir -p .git
  touch .git/HEAD .git/index
  echo "#define GITVERSION \"${_commit}\"" > gitversion.h
  make tuntox_nostatic  # use dynamic linking
  mv tuntox_nostatic tuntox
}

package() {
  cd $pkgname-$pkgver
  install -Dm755 tuntox "$pkgdir/usr/bin/tuntox"
  install -Dm755 scripts/tokssh "$pkgdir/usr/bin/tokssh"
  install -Dm644 scripts/tuntox.service "$pkgdir/usr/lib/systemd/system/tuntox.service"
}
