# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Kuba Serafinowski <zizzfizzix(at)gmail(dot)com>

_name=owncloudclient
pkgname=owncloud-client
pkgver=2.3.3
pkgrel=1
pkgdesc='ownCloud client based on mirall'
arch=(i686 x86_64)
url='http://owncloud.org/'
license=(GPL2)
depends=(qtkeychain qt5-webkit hicolor-icon-theme xdg-utils)
makedepends=(extra-cmake-modules python-sphinx qt5-tools doxygen kio)
optdepends=('python2-nautilus: integration with Nautilus' 'nemo-python: integration with Nemo' 'python2-caja: integration with Caja')
backup=('etc/ownCloud/sync-exclude.lst')
source=("https://download.owncloud.com/desktop/stable/$_name-$pkgver.tar.xz")
sha256sums=('aac1bbc16629041795ebb67151abfe109b0c220161e1f7a3f431311e2c6dade4')

prepare() {
  mkdir -p build
}

build() {
  cd build

  cmake ../$_name-$pkgver \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_SYSCONFDIR=/etc/$pkgname
  make
  make doc-man
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
