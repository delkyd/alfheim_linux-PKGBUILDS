# Maintainer: Karol Babioch <karol@babioch.de>

pkgname=('yubikey-manager-git')
_gitname='yubikey-manager'
pkgver=0.4.5.r11.e23d0bd
pkgrel=1
pkgdesc='Command line and GUI tool for configuring YubiKeys, over all transports.'
arch=('any')
url='https://github.com/Yubico/yubikey-manager'
license=('BSD')
depends=('python' 'python-pyusb' 'python-pyscard' 'python-click' 'python-cryptography' 'python-pyopenssl' 'libu2f-host' 'yubikey-personalization')
makedepends=('git' 'python-setuptools')
conflicts=('yubikey-manager')
provides=('yubikey-manager')
source=("$_gitname::git+https://github.com/Yubico/yubikey-manager.git"
	"git+https://github.com/Yubico/python-yubicommon.git")
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  git describe --tag --long | sed 's/^yubikey-manager.//;s/\([^-]*-\)g/r\1/;s/-/./g'
}

prepare() {
  cd "$srcdir/$_gitname"
  git submodule init
  git config submodule.vendor/yubicommon.url "$srcdir/python-yubicommon"
  git submodule update
}

package() {
  cd "$srcdir/$_gitname"

  python setup.py install --root="$pkgdir/" --optimize=1

  install -Dm0644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  install -d "$pkgdir/usr/share/bash-completion/completions/"
  _YKMAN_COMPLETE=source "$pkgdir/usr/bin/ykman" > "$pkgdir/usr/share/bash-completion/completions/ykman" || true
}

