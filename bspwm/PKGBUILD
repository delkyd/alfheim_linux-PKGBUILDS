# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Bastien Dejean <nihilhill@gmail.com>

pkgname=bspwm
pkgver=0.9.3
pkgrel=1
pkgdesc='Tiling window manager based on binary space partitioning'
arch=('x86_64' 'i686')
url='https://github.com/baskerville/bspwm'
license=('custom:BSD')
depends=('bash' 'xcb-util' 'xcb-util-wm' 'xcb-util-keysyms')
makedepends=('git')
optdepends=('sxhkd: to define keyboard and pointer bindings'
            'bar-aint-recursive: for the example panel'
            'xtitle: for the example panel'
            'sutils: for the example panel'
            'xdo: for the example panel')
source=("git://github.com/baskerville/$pkgname#tag=$pkgver")
md5sums=('SKIP')

build() {
  make -C "$pkgname" PREFIX=/usr
}

package() {
  cd "$pkgname"

  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# getver: -u 2 raw.githubusercontent.com/baskerville/bspwm/master/doc/bspwm.1
# vim: ts=2 sw=2 et:
