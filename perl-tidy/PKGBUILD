# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Firmicus <francois.archlinux.org>

pkgname=perl-tidy
pkgver=20170521
pkgrel=1
pkgdesc="Parses and beautifies perl source"
arch=('any')
url="http://search.cpan.org/dist/Perl-Tidy"
license=('GPL' 'PerlArtistic')
depends=('perl>=5.10.0')
options=('!emptydirs')
source=(http://www.cpan.org/authors/id/S/SH/SHANCOCK/Perl-Tidy-$pkgver.tar.gz)
sha256sums=('01b3f178c13ac24154a0ee2a62d3ce3f29cfcc6b327b0b3021094e00309ea4da')

build() {
  cd  "$srcdir"/Perl-Tidy-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd  "$srcdir"/Perl-Tidy-$pkgver
  make install DESTDIR="$pkgdir"
  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}
