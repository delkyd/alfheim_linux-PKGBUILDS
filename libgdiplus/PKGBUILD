# $Id$
# Maintainer: Daniel Isenmann <daniel@archlinux.org>

pkgname=libgdiplus
pkgver=4.2
pkgrel=1
pkgdesc="An Open Source Implementation of the GDI+ API"
arch=(i686 x86_64)
license=('MPL' 'LGPL')
url="http://www.mono-project.com"
depends=('libtiff' 'cairo' 'giflib' 'glib2' 'libexif')
source=(http://download.mono-project.com/sources/${pkgname}/${pkgname}-${pkgver}.tar.gz)
md5sums=('3e68075aa0911d5ebc2b3739e85af0ea')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -i -e 's/-L\${libjpeg_prefix}\/lib -ljpeg/-ljpeg/' configure
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --with-cairo=system

  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
