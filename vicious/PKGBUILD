# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: SÃ©bastien Luttringer

pkgname=vicious
pkgver=2.2.0
pkgrel=1
pkgdesc='Widgets for the Awesome window manager'
url='https://github.com/Mic92/vicious'
arch=('any')
license=('GPL2')
depends=('lua')
optdepends=(
  'hddtemp: for the HDD Temp widget type'
  'alsa-utils: for the Volume widget type'
  'wireless_tools: for the Wireless widget type'
  'curl: for widget types accessing network resources'
)
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/Mic92/vicious/archive/v${pkgver}.tar.gz)
sha256sums=('93b45dca464530f8b9de7ed56bc62916bb693d587a796f8d642cd5c195eee190')
sha512sums=('717c57b14870bb72a35c90c5356a807e2431d50f28be6e50abdbf17d39fea33c5b16ee14746726d238a80f9fe30c0ad1a6a98d6c9730e42437495656fed8cfbe')

package() {
  cd ${pkgname}-$pkgver
  install -Dm 644 *.lua -t "${pkgdir}/usr/share/lua/5.3/${pkgname}"
  install -Dm 644 widgets/*.lua -t "${pkgdir}/usr/share/lua/5.3/${pkgname}/widgets"
  install -Dm 644 contrib/*.lua -t "${pkgdir}/usr/share/lua/5.3/${pkgname}/contrib"
  install -Dm 644 README.md Changes.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim:set ts=2 sw=2 et:
