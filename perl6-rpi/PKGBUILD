# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=perl6-rpi
pkgver=0.1.1
pkgrel=3
pkgdesc="An interface to the Raspberry Pi's GPIO and other peripherals"
arch=('any')
depends=('perl6' 'perl6-posix')
checkdepends=('perl')
makedepends=('git')
groups=('perl6')
url="https://github.com/cspencer/perl6-raspberry-pi"
license=('PerlArtistic')
source=($pkgname-$pkgver::git+https://github.com/cspencer/perl6-raspberry-pi)
sha256sums=('SKIP')

check() {
  cd "$srcdir/$pkgname-$pkgver"

  msg2 'Running tests...'
  PERL6LIB=lib prove -r -e perl6
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  msg2 'Installing documentation...'
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"

  msg2 'Installing...'
  export RAKUDO_LOG_PRECOMP=1
  export RAKUDO_RERESOLVE_DEPENDENCIES=0
  perl6-install-dist \
    --to="$pkgdir/usr/share/perl6/vendor" \
    --for=vendor \
    --from=.

  msg2 'Removing test bin examples...'
  rm -rf "$pkgdir/usr/share/perl6/vendor/bin"
}
