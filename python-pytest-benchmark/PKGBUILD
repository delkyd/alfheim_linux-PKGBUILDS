# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pytest-benchmark
pkgname=('python-pytest-benchmark' 'python2-pytest-benchmark')
pkgver=3.0.0
pkgrel=2
pkgdesc='A py.test fixture for benchmarking code'
arch=('any')
license=('BSD')
url='https://github.com/ionelmc/pytest-benchmark'
makedepends=('python-pytest' 'python2-pytest' 'python2-statistics' 'python2-pathlib' 'git')
checkdepends=('python-pytest-cov' 'python2-pytest-cov' 'python-pygal' 'python2-pygal'
              'python-freezegun' 'python2-freezegun' 'mercurial' 'python-aspectlib'
              'python2-aspectlib' 'python-pytest-xdist' 'python2-pytest-xdist')
source=("git+https://github.com/ionelmc/pytest-benchmark.git#tag=v$pkgver")
md5sums=('SKIP')

prepare() {
  cp -a pytest-benchmark{,-py2}
}

build() {
  cd "$srcdir"/pytest-benchmark
  python setup.py build

  cd "$srcdir"/pytest-benchmark-py2
  python2 setup.py build
}

check() {
  # Hack entry points by installing it
  # Test should be related to pytest 2.9

  cd "$srcdir"/pytest-benchmark
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  LC_CTYPE=en_US.UTF-8 PYTHONPATH="$PWD/tmp_install/usr/lib/python3.6/site-packages:$PYTHONPATH" py.test tests/ || warning "Tests failed"

  cd "$srcdir"/pytest-benchmark-py2
  python2 setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python2.7/site-packages:$PYTHONPATH" py.test2 tests/ || warning "Tests failed"
}

package_python-pytest-benchmark() {
  depends=('python-pytest')

  cd "$srcdir"/pytest-benchmark
  python setup.py install --root="$pkgdir"/ --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pytest-benchmark() {
  depends=('python2-pytest' 'python2-statistics' 'python2-pathlib')

  cd "$srcdir"/pytest-benchmark-py2
  python2 setup.py install --root="$pkgdir"/ --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
