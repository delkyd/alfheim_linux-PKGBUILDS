# $Id$
# Maintainer: Anatol Pomozov
# Contributor: Tim Hütz <tim@huetz.biz>
# Contributor: Stéphane Gaudreault <stephane@archlinux.org>
# Contributor: Sebastien Binet <binet@cern.ch>

pkgbase=python-mpi4py
pkgname=(python-mpi4py python2-mpi4py)
pkgver=2.0.0
pkgrel=3
pkgdesc='Python bindings for the Message Passing Interface (MPI) standard'
arch=(x86_64 i686)
url='https://planet.scipy.org/'
license=(BSD)
makedepends=(python-setuptools python2-setuptools openmpi)
checkdepends=(inetutils)
source=(https://bitbucket.org/mpi4py/mpi4py/get/6e9f90c47a53.zip)

# pipy 2.0.0 is not openmpi 2.x compatible
# thus use the trunk per upstream recomendation https://bitbucket.org/mpi4py/mpi4py/issues/50/is-new-release-210-coming#comment-37501124
#https://pypi.python.org/packages/source/m/mpi4py/mpi4py-$pkgver.tar.gz)
sha1sums=('b62a6175e778f2a292109b9ad6339951c67f4e41')

prepare() {
  mv mpi4py-mpi4py-6e9f90c47a53 mpi4py-$pkgver

  # fix for https://bitbucket.org/mpi4py/mpi4py/issues/28/test_dltestdl-test-failure
  sed -e 's/libm.so/libm.so.6/' -i  mpi4py-$pkgver/test/test_dl.py

  cp -a mpi4py-$pkgver{,-py2}
}

build() {
  cd "$srcdir/mpi4py-$pkgver"
  python setup.py build

  cd "$srcdir/mpi4py-$pkgver-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/mpi4py-$pkgver-py2"
  python2 setup.py test

  cd "$srcdir/mpi4py-$pkgver"
  python setup.py test
}

package_python-mpi4py() {
  depends=(python openmpi)

  cd "$srcdir/mpi4py-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.rst "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}

package_python2-mpi4py() {
  depends=(python2 openmpi)

  cd "$srcdir/mpi4py-$pkgver-py2"
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.rst "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}
