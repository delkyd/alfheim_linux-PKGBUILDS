# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>

pkgname=geeqie
pkgver=1.3
pkgrel=3
pkgdesc="A lightweight image browser and viewer"
arch=('i686' 'x86_64')
url="http://www.geeqie.org/"
license=('GPL2')
depends=('exiv2' 'gtk2' 'lcms')
makedepends=('intltool' 'doxygen' 'gnome-doc-utils' 'git')
optdepends=('librsvg: SVG rendering'
	    'fbida: for jpeg rotation'
	    'exiv2: for tiff and png rotation')
validpgpkeys=('85D4CA42952C949B175362B379D0B06F4E20AF1C')
source=(http://www.geeqie.org/${pkgname}-${pkgver}.tar.xz{,.asc}
        0001-Work-around-changed-c-11-std-string-implementation.patch)
sha1sums=('f991b44d8587f2f42d5793700c5f6882bed6abf9'
          'SKIP'
          '45dea6145ab6d74ed69a81a6e5871300f51450eb')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i ../0001-Work-around-changed-c-11-std-string-implementation.patch
  ./autogen.sh
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package(){
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
