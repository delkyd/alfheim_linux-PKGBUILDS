# Maintainer: Alexey D. <lq07829icatm@rambler.ru>
# Contributor: Joker-jar <joker-jar@yandex.ru>

pkgname="psi-plus-git"
pkgver=1.0.0.792
pkgrel=1
pkgdesc="Psi+ is a powerful Jabber client (Qt, C++) designed for the Jabber power users"
url="http://psi-plus.com"
license=('GPL2')
arch=('i686' 'x86_64')
depends=('qt4' 'qca-qt4' 'libidn' 'enchant' 'libxss')
makedepends=('git' 'patch' 'qconf-git')
optdepends=('qca-gnupg: encrypted client-to-client connection')
provides=("psi-plus=$pkgver")
replaces=('psi-plus' 'psi-plus-webkit-git')
conflicts=('psi-plus' 'psi-plus-webkit-git')
source=('git://github.com/psi-im/psi.git'
        'psi-plus::git://github.com/psi-plus/main.git'
        'git://github.com/psi-im/iris.git'
        'git://github.com/psi-im/libpsi.git')
md5sums=('SKIP'
         'SKIP'
         'SKIP'
         'SKIP')

pkgver() {
  cd "$srcdir"/psi-plus

  PSI_PLUS_REVISION="$(git describe --tags --long | cut -d - -f 2)"
  PSI_PLUS_TAG="$(git describe --tags | cut -d - -f 1)"
  PSI_REVISION="$(cd "$srcdir"/psi && git describe --tags --long | cut -d - -f 2)"

  echo "${PSI_PLUS_TAG}.${PSI_PLUS_REVISION}.${PSI_REVISION}"
}

prepare() {
  cd psi

  # makepkg doesn't support --recursive
  # so setup git modules manually
  git submodule init
  git config submodule.iris.url "$srcdir/iris"
  git config submodule.src/libpsi.url "$srcdir/libpsi"
  git submodule update

  # patches from Psi+ project
  for patch in "$srcdir"/psi-plus/patches/*.diff; do
    echo "* Appling ${patch##*/}"
    patch -p1 -i "$patch"
  done

  # additional icon themes
  cp -a "$srcdir"/psi-plus/iconsets .

  # make build date in --version output a bit more readable
  #sed "s/yyyyMMdd/yyyy-MM-dd/" -i qcm/conf.qcm
  echo "$(pkgver) ($(date +"%Y-%m-%d"))" >version
}

build() {
  cd psi

  qconf
  ./configure --prefix=/usr \
              --qtdir="/usr/lib/qt4"
  make
}

package() {
  cd psi

  make INSTALL_ROOT="$pkgdir" install

  install -dm755 "$pkgdir/usr/include/psi-plus/plugins"
  install -m644 src/plugins/include/*.h "$pkgdir/usr/include/psi-plus/plugins"
}

