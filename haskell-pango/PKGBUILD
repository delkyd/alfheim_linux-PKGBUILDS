# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=haskell-pango
pkgver=0.13.3.1
pkgrel=5
pkgdesc="Binding to the pango library for Gtk2Hs."
url="https://hackage.haskell.org/package/pango"
license=('LGPL2.1')
arch=('i686' 'x86_64')
depends=('ghc-libs' 'pango' 'haskell-glib' 'haskell-cairo')
makedepends=('ghc' "haskell-gtk2hs-buildtools" "mesa")
provides=('gtk2hs-pango')
replaces=('gtk2hs-pango')
conflicts=('gtk2hs-pango')
source=("https://github.com/gtk2hs/gtk2hs/archive/843d83548bd076975f1869e6768768b5ed24b268.tar.gz")
sha512sums=('610a080ddf8ad8511176a7fd5ed5457aa26b5ce48539b7eb46563d86db1ffab6501f8b4deac4152f7d9cea84093de34e1bd1bb9467f558f1c202026337e1c708')

prepare() {
    mv gtk2hs-843d83548bd076975f1869e6768768b5ed24b268/pango pango-$pkgver
}

build() {
    cd "${srcdir}/pango-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    sed -i 's|import Foreign.C.Types.*(CULong, CUInt)|import Foreign.C.Types (CULong(..), CUInt(..))|' Graphics/Rendering/Pango/Types.chs
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/pango-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
}
