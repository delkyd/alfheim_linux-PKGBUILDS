# Maintainer: Jelle van der Waa <jelle@vdwaa.nl>
# Contributor: Joost Bremmer <toost dot b at gmail dot com>

pkgname=timew
pkgver=1.0.0
pkgrel=4
pkgdesc="A command line time tracking application"
arch=('x86_64' 'i686')
url="http://taskwarrior.org/docs/timewarrior/"
license=('MIT')
makedepends=('cmake' 'gcc-libs')
optdepends=('task: Taskwarrior integration')
provides=('timew')
conflicts=('timew-git')
install=$pkgname.install
source=("https://taskwarrior.org/download/timew-${pkgver}.tar.gz")
sha256sums=('ac027910e1e8365bdd218a8b42389b26d017d38d3c96516c408db6d5a44e0bb5')

prepare() {
    cd "${pkgname}-${pkgver}"

    # Fix Python shebang, fixed upstream in 1.1.0
    # https://git.tasktools.org/projects/TM/repos/timew/commits/5ddef40d8272be870e05ee3753dcf3bbe35e2bd9
    sed -i 's/env python/env python2/g' doc/holidays/refresh
    sed -i 's/env python/env python2/g' ext/on-modify.timewarrior
    sed -i 's/env python/env python2/g' ext/totals.py
}

build() {
    cd "${pkgname}-${pkgver}"
    cmake -DCMAKE_BUILD_TYPE=release -DCMAKE_INSTALL_PREFIX=/usr .
    make
}


package() {
    cd "${pkgname}-${pkgver}"

    make DESTDIR=${pkgdir} install

    # Install the refresh holiday scripts in /usr/bin/ for user convenience.
    chmod 755 "${pkgdir}/usr/share/doc/timew/doc/holidays/refresh"
    ln -s "/usr/share/doc/timew/doc/holidays/refresh" "${pkgdir}/usr/bin/timew-refresh-holidays"
}
