# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kturtle
pkgver=17.08.1
pkgrel=1
pkgdesc="Educational Programming Environment"
url="https://kde.org/applications/education/kturtle/"
arch=(i686 x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kdeedu)
depends=(knewstuff hicolor-icon-theme)
makedepends=(extra-cmake-modules python kdoctools)
conflicts=(kdeedu-kturtle)
replaces=(kdeedu-kturtle)
source=("https://download.kde.org/stable/applications/$pkgver/src/kturtle-$pkgver.tar.xz"{,.sig})
sha256sums=('b490b6acd3ddd72a54e4b6a09a395888aa90da131ae6a7f9b50d07561bac3a5b'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
