# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=criterion
pkgname=haskell-criterion
pkgver=1.2.2.0
pkgrel=10
pkgdesc="Robust, reliable performance measurement and analysis"
url="http://www.serpentine.com/criterion"
license=("custom:BSD3")
arch=('i686' 'x86_64')
depends=('ghc-libs' "haskell-aeson" "haskell-ansi-wl-pprint" "haskell-cassava" "haskell-code-page"
         "haskell-exceptions" "haskell-glob" "haskell-js-flot" "haskell-js-jquery"
         "haskell-microstache" "haskell-mtl" "haskell-mwc-random" "haskell-optparse-applicative"
         "haskell-parsec" "haskell-statistics" "haskell-text" "haskell-transformers-compat"
         "haskell-vector" "haskell-vector-algorithms")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('b655bba05c4da4e9c2272dba79de95f1f714a655f2d48baff5b8b26fd0eadcfc3528c21e028adfb2c4adcd904260d5ff1b06e30a41c9d5bdf91ca01bea6559e0')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-fast
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
