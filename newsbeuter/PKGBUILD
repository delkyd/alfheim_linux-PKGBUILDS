# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Sven Pfleiderer <pfleidi@roothausen.de>

pkgname=newsbeuter
pkgver=2.9
pkgrel=8
pkgdesc="A RSS feed reader for the text console with special Podcast support"
arch=('i686' 'x86_64')
url="http://www.newsbeuter.org/"
license=('custom: MIT')
depends=('curl' 'json-c' 'libxml2' 'sqlite' 'stfl')
makedepends=('swig' 'gettext')
changelog=$pkgname.changelog
source=($pkgname-r$pkgver.tar.gz::https://github.com/akrennmair/$pkgname/archive/r$pkgver.tar.gz
        $pkgname-$pkgver-ncursesw6.patch
        $pkgname-segfault.patch::https://github.com/akrennmair/newsbeuter/commit/33577f842d9b74c119f3cebda95ef8652304db81.patch
        newsbeuter-CVE-2017-12904.patch
        remote-code-execution-podcast-name.patch)
sha256sums=('489694a57d4af390aa15ab48b66517b0dc34a1cda4410eb5f6f997c6369f51d8'
            '5ae54c463f44d91725da3be655d2b107d598ade6da86ab4a99b10b039b8dba27'
            '60f56cabe47a1773f4a3a960ae0aee418f8a6df7bdd48b9874bf79cdd4c23b84'
            '51c57a5b92704f5659e1283d1bdde521b9df64d315c9584e0fc4e69bb74db930'
            'f5d7b9af66884e9551eb28a36bad9f14f361809664fddde68e89ca540e4a27c9')

prepare() {
  cd "${srcdir}"/$pkgname-r$pkgver

  patch -p1 -i ../$pkgname-$pkgver-ncursesw6.patch
  patch -p1 -i ../$pkgname-segfault.patch
  patch -p1 -i ../$pkgname-CVE-2017-12904.patch
  patch -p1 -i ../remote-code-execution-podcast-name.patch
}

build() {
  cd "${srcdir}"/$pkgname-r$pkgver

  make prefix=/usr
}

package() {
  cd "${srcdir}"/$pkgname-r$pkgver

  make prefix=/usr DESTDIR="${pkgdir}" install
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}
