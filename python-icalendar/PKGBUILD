# Maintainer: Daniel M. Capella <polyzen@archlinux.info>
# Contributor: Sirat18 <aur@sirat18.de>
# Contributor: Jonas Heinrich <onny@project-insanity.org>

pkgname=python-icalendar
pkgver=3.11.7
pkgrel=1
pkgdesc='A parser/generator of iCalendar files (RFC 2445)'
arch=('any')
url=https://github.com/collective/icalendar
license=('GPL')
depends=('python-dateutil' 'python-pytz')
makedepends=('python-setuptools')
source=("icalendar-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('7331e65cea5e4371042cf2e4c547146e99458ea86be39572951f045039ed97fd821296bdb6940a5a8ffdc6273b5b09562557fbd19fa9e5c1839be5396cf16f6a')

build() {
  cd icalendar-$pkgver
  python setup.py build
}

check() {
  cd icalendar-$pkgver
  python -m unittest discover src
}

package() {
  cd icalendar-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

# vim:set ts=2 sw=2 et:
