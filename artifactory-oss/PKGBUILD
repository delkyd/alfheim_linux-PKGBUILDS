# Maintainer: Yegorius <yegorius@domic.us>

pkgname=artifactory-oss
pkgver=5.4.6
pkgrel=1
pkgdesc='Artifactory is an advanced Binary Repository Manager for use by build tools, dependency management tools and build servers'
arch=('any')
url="https://bintray.com/jfrog/product/JFrog-Artifactory-Oss/view"
license=('GPLv3')
depends=('java-runtime-headless' 'net-tools' 'bash')
install="$pkgname.install"
source=("jfrog-artifactory-oss-${pkgver}.zip::https://bintray.com/jfrog/artifactory/download_file?file_path=jfrog-artifactory-oss-${pkgver}.zip"
        'artifactory.service'
        'artifactory.conf'
        'artifactory.default')
sha256sums=('43fb4b14ad5350daa200e0282a36903495505ccb536b5f0ac7d7b5487dcdfca6'
            '8ba1287f4d062f57a5cf9e5426d4affcfcc00ca2680cd603f41c603957a42c20'
            '48bc1cddf9fa64f0d62a519470a490719398d67b6baeef6a3e647b737d6484df'
            '54617cc75624db3a03dbed62898b2f438cb8145983a034d72f12ca259c8218a4')
options=('!strip')
PKGEXT='.pkg.tar'

package() {
  local artDist="/opt/artifactory"
  
  pushd "$pkgname-$pkgver"
  rm -f bin/*.{exe,bat}
  rm -f bin/{install,uninstall}Service.sh
  rm -f bin/artifactoryctl
  rm -f tomcat/bin/*.bat
  rm -f COPYING* *.txt *.html
  popd
  
  install -d "$pkgdir$artDist"
  cp -r "$pkgname-$pkgver"/* "$pkgdir$artDist"
  install -Dm644 "$srcdir/artifactory.conf" "$pkgdir/usr/lib/systemd/sysusers.d/artifactory.conf"
  install -Dm644 "$srcdir/artifactory.default" "$pkgdir$artDist/bin"
  install -Dm644 "$srcdir/artifactory.service" "$pkgdir/usr/lib/systemd/system/artifactory.service"
  install -d "$pkgdir$artDist/run"
}
