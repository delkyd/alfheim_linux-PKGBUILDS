# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-ukpostcodeparser
pkgname=('python-ukpostcodeparser' 'python2-ukpostcodeparser')
pkgver=1.1.1
_commit=dae25cd340ec69b4e9e97ed0b979883ea5c51cf6
pkgrel=1
pkgdesc="UK Postcode parser"
arch=('any')
license=('MIT')
url="https://github.com/hamstah/ukpostcodeparser"
makedepends=('python-setuptools' 'python2-setuptools')
source=("$pkgbase-$_commit.tar.gz::https://github.com/hamstah/ukpostcodeparser/archive/$_commit.tar.gz")
sha512sums=('e9b797548a064c295ef94afba2df88a050f3bf27f78164a889f0a7ba8ea22dade849a13e8b6fe663386e7c83aa78390ca51a5354b524cebb68d99254080d4a5c')

prepare() {
  cp -a ukpostcodeparser-$_commit{,-py2}
}

build() {
  cd "$srcdir"/ukpostcodeparser-$_commit
  python setup.py build

  cd "$srcdir"/ukpostcodeparser-$_commit-py2
  python2 setup.py build
}

check() {
  # Expected failure: https://github.com/hamstah/ukpostcodeparser/issues/8

  cd "$srcdir"/ukpostcodeparser-$_commit
  python -m unittest discover -s ukpostcodeparser/test -p '*.py' || warning "Tests failed"

  cd "$srcdir"/ukpostcodeparser-$_commit-py2
  python2 -m unittest discover -s ukpostcodeparser/test -p '*.py' || warning "Tests failed"
}

package_python-ukpostcodeparser() {
  depends=('python')

  cd ukpostcodeparser-$_commit
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-ukpostcodeparser() {
  depends=('python2')

  cd ukpostcodeparser-$_commit-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

