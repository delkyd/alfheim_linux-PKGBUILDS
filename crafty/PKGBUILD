# Maintainer : Özgür Sarıer <echo b3pndXJzYXJpZXIxMDExNjAxMTE1QGdtYWlsLmNvbQo= | base64 -d>
# Contributor: Auguste Pop <auguste [at] gmail [dot] com>

_build_book=0 # 0=Uses defauld book.bin
	      # 1=Builds book.bin from book.pgn (Recommended if big book file not bothers you.)

pkgname=crafty
pkgver=25.2
pkgrel=2
pkgdesc="A free, open-source computer chess program developed by Dr. Robert M. (Bob) Hyatt"
arch=('i686' 'x86_64')
url="http://www.craftychess.com/"
license=('custom')
source=("http://www.craftychess.com/downloads/source/$pkgname-$pkgver.zip"
	"http://www.craftychess.com/downloads/book/book.`[[ "$_build_book" = '0' ]] && echo bin || echo pgn.gz`"
	"http://www.craftychess.com/downloads/book/start.pgn.gz"
	"http://www.craftychess.com/downloads/book/startc.pgn.gz"
	"crafty-data.tar.bz2"
	"security-203541.patch"
	"paths.patch"
	"Makefile.patch")
md5sums=('d8ad87d9b0fc39a437595203d7b302fc'
	 "`[[ "$_build_book" = '0' ]] && echo f8f93189c64324b1959a489da822438e || echo 05efad71289b2d328da5110df4a19f85`"
	 '880279c223dc34164837a351faafe2f0'
	 '7a53d5f09d2baa5e7f0df4ee81961cfb'
         '18cb719601b36825113274335ee0e3f1'
         '02c7ab07c0ff8f77738a055772f4406d'
         '9db34746e90ceb116d162a5fa727f7e0'
         '178436e4fdacdecd58d0bbdeb9aba924')

prepare() {
	cd "$srcdir"
	patch -Np0 -i "${srcdir}/security-203541.patch"
	patch -Np1 -i "${srcdir}/paths.patch"
	patch -Np0 -i "${srcdir}/Makefile.patch"
	sed -i -e "s:mt=4:mt=$(nproc):" Makefile
	sed -i -e "s:mt=4:mt=$(nproc):" crafty-data/crafty.rc
}

build() {
	cd "$srcdir"
	make CFLAGS="${CFLAGS} `grep -q "popcnt" /proc/cpuinfo && echo "-mpopcnt"`" opt="-DUNIX -DCPUS=$(nproc)" unix-gcc

	cd "$srcdir"
	# create crafty's rc-files within the BUILD directory
	homeorj=$HOME; HOME=.
	msg "Creating bookfiles, this might take a moment..."
	if [[ "$_build_book" = '1' ]]; then
		msg "Creating book.bin..."
		./crafty "book create book.pgn 60" quit
	fi
	# create books.bin (for crafty's opening strategy)
	msg "Creating books.bin..."
	./crafty "books create start.pgn 60" quit
	# create  bookc.bin (computer opening book)
	msg "Creating bookc.bin..."
	./crafty "bookc create startc.pgn 60 3" quit
	HOME=$homeorj
}

package() {
	cd "$srcdir"
	install -Dpm 0755 crafty "$pkgdir/usr/bin/crafty"
	install -Dpm 0644 crafty-data/crafty.6 "$pkgdir/usr/share/man/man6/crafty.6.gz"
	install -dm 0755 "$pkgdir/usr/share/crafty/syzygy/"
	install -pm 0666 book.bin "$pkgdir/usr/share/crafty/book.bin"
	install -pm 0644 bookc.bin books.bin crafty.hlp "$pkgdir/usr/share/crafty/"
	install -Dpm 0644 crafty-data/crafty.rc "$pkgdir/etc/crafty.rc"
	install -Dpm 0644 crafty-data/copyright "$pkgdir/usr/share/licenses/crafty/copyright"
	install -dpm 0644 crafty-data/{crafty.doc.v18.html,crafty.faq,readme,tournament.howto "$pkgdir/usr/share/doc/crafty/"
}
