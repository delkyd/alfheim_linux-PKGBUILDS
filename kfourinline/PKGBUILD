# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kfourinline
pkgver=17.08.1
pkgrel=1
pkgdesc="A four-in-a-row game"
url="https://kde.org/applications/games/kfourinline/"
arch=(i686 x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kdegames)
depends=(libkdegames hicolor-icon-theme)
makedepends=(extra-cmake-modules python kdoctools)
conflicts=(kdegames-kfourinline)
replaces=(kdegames-kfourinline)
source=("https://download.kde.org/stable/applications/$pkgver/src/kfourinline-$pkgver.tar.xz"{,.sig})
sha256sums=('7e7a5813e97f9b1fc7e78d78d745764e79c13a13fd2eba230f667896951e9e89'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
