# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: PepeSmith
# Contributor: Aron Asor <aronasorman at gmail.com>
# Contributor: Chris Brannon <chris@the-brannons.com>
# Contributor : Douglas Soares de Andrade <dsa@aur.archlinux.org>

pkgbase=ipython
pkgname=(ipython ipython2)
pkgver=5.3.0
pkgrel=1
pkgdesc="An enhanced Interactive Python shell."
arch=('any')
url="http://ipython.org"
license=('BSD')
depends=('python' 'python-traitlets' 'python-pexpect' 'sqlite'
         'python-setuptools' 'python-pickleshare' 'python-prompt_toolkit')
makedepends=('python-setuptools' 'python2-setuptools')
optdepends=("python-nose: for IPython's test suite")

# some of these will update rapidly, others never
# so _vars will be phased in as new updates are released

source=("ipython-$pkgver.tgz::https://github.com/ipython/ipython/archive/$pkgver.tar.gz"
        "https://pypi.python.org/packages/source/s/simplegeneric/simplegeneric-0.8.1.zip"
        "http://www.packal.org/sites/default/files/public/styles/icon_large/public/workflow-files/nkeimipynbworkflow/icon/icon.png")
md5sums=('30045499fa745e2f1893cadcba3f94c5'
         'f9c1fab00fd981be588fc32759f474e3'
         '2901d65f1b1fe354e72850085cd1c072')

# With 4.0 many previously bundled external libraries are no longer bundled
# Most are written/maintained/used only by the ipython project
# Since there are so many and they are so small, they get re-bundled here
# Break them out if any non-ipython package ends up needing these

# simplegeneric (so old that it only comes in .zip and still refers to the cheeseshop)

# confirm that an update does not break sage?

prepare() {
  cd "$srcdir"
  cp -r ipython-$pkgver        ipython2-$pkgver
  cp -r simplegeneric-0.8.1    python2-simplegeneric-0.8.1

  # python 3.5 does not need a 3.3 backport hack
  cd "$srcdir/ipython-$pkgver"
  sed -i 's|backports.shutil_get_terminal_size|shutil|' IPython/utils/terminal.py
}

build() {
  cd "$srcdir"
}

package_ipython() {
  cd "$srcdir/ipython-$pkgver"

  # see https://github.com/ipython/ipython/issues/2057
  #export LC_ALL=en_US.UTF-8
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/simplegeneric-0.8.1"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/ipython-$pkgver"
  install -Dm644 docs/source/about/license_and_copyright.rst "$pkgdir/usr/share/licenses/ipython/LICENSE"

  cd "$srcdir/ipython-$pkgver/examples/IPython Kernel/"
  # FS#45120
  sed -i 's/gnome-netstatus-idle/ipython/' *.desktop
  install -Dm644 ipython.desktop "$pkgdir/usr/share/applications/ipython.desktop"
  #install -Dm644 ipython-qtconsole.desktop "$pkgdir/usr/share/applications/ipython-qtconsole.desktop"
  # FS#47046
  install -Dm644 "$srcdir/icon.png" "$pkgdir/usr/share/pixmaps/ipython.png"

  #cd $srcdir/ipython-$pkgver/IPython/qt/console/resources/icon/
  #install -Dm644 IPythonConsole.svg "$pkgdir/usr/share/icons/hicolor/scalable/apps/ipython.svg"
}

package_ipython2() {
  pkgdesc="An enhanced Interactive Python2 shell."
  depends=('python2' 'python2-traitlets' 'python2-pexpect' 'sqlite'
    'python2-setuptools' 'python2-pickleshare' 'python2-pathlib'
    'python2-backports.shutil_get_terminal_size' 'python2-prompt_toolkit')
  optdepends=("wxpython: needed for ipython2 --gui=wx"
              "python2-nose: for IPython's test suite")
  # "wxpython: needed for ipython2 --gui=wx"   does this still work?

  cd "$srcdir/ipython2-$pkgver"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/python2-simplegeneric-0.8.1"
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/ipython2-$pkgver"
  install -Dm644 docs/source/about/license_and_copyright.rst "$pkgdir/usr/share/licenses/ipython2/LICENSE"

  # hack to get around ipython collision
  cd "$pkgdir/usr/share/man/man1/"
  for i in *; do
    mv $i ${i/%.1/2.1}
  done
  find "$pkgdir/usr/bin/" -type f -regex '.*[^2]$' -delete

  cd "$srcdir/ipython2-$pkgver/examples/IPython Kernel/"
  sed -i 's/ython/ython2/g' *.desktop
  sed -i 's/gnome-netstatus-idle/ipython2/' *.desktop
  install -Dm644 ipython.desktop "$pkgdir/usr/share/applications/ipython2.desktop"
  #install -Dm644 ipython-qtconsole.desktop "$pkgdir/usr/share/applications/ipython2-qtconsole.desktop"
  install -Dm644 "$srcdir/icon.png" "$pkgdir/usr/share/pixmaps/ipython2.png"

  #cd "$pkgdir/usr/share/man/man1/IPython/qt/console/resources/icon/"
  #install -Dm644 IPythonConsole.svg "$pkgdir/usr/share/icons/hicolor/scalable/apps/ipython2.svg"
}

