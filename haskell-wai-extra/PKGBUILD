# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=wai-extra
pkgname=haskell-wai-extra
pkgver=3.0.20.0
pkgrel=14
pkgdesc="Provides some basic WAI handlers and middleware."
url="http://github.com/yesodweb/wai"
license=("MIT")
arch=('i686' 'x86_64')
depends=('ghc-libs' "haskell-aeson" "haskell-ansi-terminal" "haskell-base64-bytestring"
         "haskell-blaze-builder" "haskell-case-insensitive" "haskell-cookie"
         "haskell-data-default-class" "haskell-fast-logger" "haskell-http-types"
         "haskell-iproute" "haskell-lifted-base" "haskell-network" "haskell-old-locale"
         "haskell-resourcet" "haskell-streaming-commons" "haskell-stringsearch"
         "haskell-text" "haskell-unix-compat" "haskell-vault" "haskell-void" "haskell-wai"
         "haskell-wai-logger" "haskell-word8" "haskell-zlib")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('296f9be3d5907a0dcf0cbc7a56b0d8cbb432c5df816bebc15dd1120ad6aaa163d4d96e9eea5121f1d829cf43345809e8c8b5480bb6c43bfe6e15ca64960f26ff')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
