# Maintainer: Vlad M. <vlad@archlinux.net>
# Contributor: Jakub Schmidtke <sjakub-at-gmail-dot-com>
# Contributor: Forrest L <cybercyst at gmail com>
# Contributor: Michael P <ptchinster@archlinux.us>
# Contributor: Marcin "eXine" M. <exine@jun.pl>
# Contributor: Artyom Smirnov <smirnoffjr@gmail.com>
# Contributor: Ashok `ScriptDevil` Gautham <ScriptDevil@gmail.com>
# Contributor: Laszlo Papp <djszapi2 at gmail com>
# Contributor: Antonio Santos <asantos at gmail dot com>

pkgname=eclipse-android
pkgver=23.0.7
_sdkver=24.1.2
pkgrel=1
pkgdesc='Eclipse plugin for Android'
url='https://developer.android.com/sdk/eclipse-adt.html'
license=('Apache' 'BSD' 'EPL' 'LGPL')
arch=('any')
depends=("android-sdk>=${_sdkver}" 'java-environment' 'eclipse>=4.5' 'eclipse-wtp')
options=('!strip')
source=("http://dl.google.com/android/ADT-${pkgver}.zip" "LICENSE.kxml2")
sha1sums=('3722429c2adfe9dc6d3dca89f6aa745835100023'
          'c0a96dc032bb53e2921200c85f5cf650e49878e9')

package() {
  _dest="${pkgdir}/usr/lib/eclipse/dropins/android/eclipse"

  # Features
  for _f in features/*.jar; do
    _dir="${_dest}/${_f/.jar}"
    mkdir -p "${_dir}"
    bsdtar -xf "${_f}" -C "${_dir}"
  done

  # Plugins
  for _p in plugins/*.jar; do
    install -Dm644 "${_p}" "${_dest}/${_p}"
  done

  install -Dm644 LICENSE.kxml2 "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.kxml2"
}
