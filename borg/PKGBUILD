# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Vlad M. <vlad@archlinux.net>

pkgname=borg
_pkgname=borgbackup
pkgver=1.0.11
pkgrel=1
pkgdesc='Deduplicating backup program with compression and authenticated encryption'
url='https://borgbackup.github.io/'
license=('BSD')
arch=('i686' 'x86_64')
depends=('acl' 'lz4' 'openssl' 'python-msgpack' 'python-setuptools' 'xz')
optdepends=('openssh: repositories on remote hosts'
            'python-llfuse: mounting backups as a FUSE file system')
makedepends=('cython' 'python-sphinx')
checkdepends=('python-pytest' 'python-pytest-cov' 'python-pytest-benchmark' 'python-mock')
provides=('borgbackup')
replaces=('borgbackup')
source=("https://github.com/$_pkgname/$pkgname/releases/download/$pkgver/$_pkgname-$pkgver.tar.gz"
        borg.coveragerc)
sha256sums=('fc6efd2d19454625532e0defac90cd8e8737665e10ae59edb15356ee5a99d291'
            '183814ae70fce7bbcf04dbbf3866eaf76867896a4327f8a3b553e095d1ff360f')

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  python setup.py build

  cd docs
  make man
}

check() {
  cd "$srcdir/$_pkgname-$pkgver/build/lib.linux-$CARCH-3.6/"
  LANG=en_US.UTF-8 PYTHONPATH="$PWD:$PYTHONPATH" py.test --cov=borg \
    --cov-config="$srcdir/borg.coveragerc" --benchmark-skip --pyargs borg.testsuite -v \
    -k 'not test_non_ascii_acl'
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  install -Dm644 "docs/_build/man/borg.1" "$pkgdir/usr/share/man/man1/borg.1"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  python setup.py -q install --root="$pkgdir" --optimize=1
}
