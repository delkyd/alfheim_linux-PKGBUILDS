# Contributor: Mark Foxwell <fastfret79@archlinux.org.uk>
# Contributor: Nicolas Pouillard [https://nicolaspouillard.fr]
# Contributor: seschwar -- contact via Arch Linux forum or AUR
# Contributor: Ian Denhardt <ian@zenhack.net>
# Contributor: Joakim Reinert <mail+aur@jreinert.com>
# Maintainer: Dylan Baker <dylan@pnwbakers.com>

_gitname=alot
pkgname=alot-git
pkgrel=1
pkgver=r2938.8fabf1cf
pkgdesc="terminal-based MUA for the notmuch mail system"
arch=(any)
url="https://github.com/pazz/alot"
license=('GPL3')
depends=(notmuch
         python2
         python2-gpg
         python2-magic
         python2-configobj
         python2-urwid
         python2-urwidtrees
         python2-twisted
         python2-setuptools)
makedepends=(python2-sphinx git)
provides=(alot)
conflicts=(alot)
options=(!emptydirs !strip libtool staticlibs zipman)
source=('git+https://github.com/pazz/alot.git')
md5sums=(SKIP)

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd "$srcdir/$_gitname"

    # The archlinux package python2-magic's egg calls itself
    # "file-magic", as opposed to the python-magic on pypi. The
    # result is that the alot executable can't find the module, so we patch
    # setup.py to fix the dependency:
    sed -i -e 's/python-magic/file-magic/' setup.py
    python2 setup.py build
    make SPHINXBUILD=sphinx-build2 PYTHON=python2 -C docs man html
}

package() {
    cd "$srcdir/$_gitname"
    python2 setup.py install --optimize=1 --root="$pkgdir"

    # Delete tests
    rm -rvf "$pkgdir/usr/lib/python2.7/site-packages/tests"

    # Install extra data
    install -Dm644 extra/completion/alot-completion.zsh \
        "$pkgdir/usr/share/zsh/functions/_alot"
    install -dm755 "$pkgdir/usr/share/alot/themes"
    install -Dm644 extra/themes/{mutt,solarized_light,solarized_dark,sup,tomorrow} \
        "${pkgdir}/usr/share/alot/themes"
    install -dm755 "$pkgdir/usr/share/doc/$pkgname"
    cp -a docs/build/html/* "$pkgdir/usr/share/doc/$pkgname"
    install -Dm644 docs/build/man/alot.1 "$pkgdir/usr/share/man/man1/alot.1"
}
