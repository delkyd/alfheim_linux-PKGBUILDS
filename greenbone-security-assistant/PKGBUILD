# $Id$
# Maintainer: Daniel Micay <danielmicay@gmail.com>
pkgname=greenbone-security-assistant
pkgver=6.0.12
pkgrel=1
pkgdesc='Greenbone Security Assistant (gsa) - OpenVAS web frontend'
url='http://www.openvas.org/'
license=(GPL)
arch=('i686' 'x86_64')
depends=('libmicrohttpd' 'openvas-libraries' 'libxslt')
makedepends=('cmake' 'doxygen' 'xmltoman')
groups=(openvas)
backup=(etc/openvas/gsad_log.conf)
# These URLs need to be manually updated on every pkgver upgrade.
# http://wald.intevation.org/frs/?group_id=29
source=("http://wald.intevation.org/frs/download.php/2442/$pkgname-$pkgver.tar.gz"
        "http://wald.intevation.org/frs/download.php/2443/$pkgname-$pkgver.tar.gz.asc")
sha512sums=('d9d14e2b99577197ac00745693ca2b1205445708efe6170ab23bc52f48eafd7d7584196241129617720983cca8fed78fd4849b4c198a31799361b37c0615dddf'
            'SKIP')
validpgpkeys=(
              'C3B468D2288C68B9D526452248479FF648DB4530' # OpenVAS Transfer Integrity
)
conflicts=(gsa)
provides=(gsa)
replaces=(gsa)

build() {
  cd $pkgname-$pkgver
  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr \
    -DSYSCONFDIR=/etc -DLOCALSTATEDIR=/var . -DSBINDIR=/usr/bin
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
