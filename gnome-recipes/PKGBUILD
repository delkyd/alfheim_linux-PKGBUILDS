# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-recipes
pkgver=1.4.6
pkgrel=1
pkgdesc="Recipe management application for GNOME"
arch=(x86_64 i686)
url="https://wiki.gnome.org/Apps/Recipes"
license=(GPL3)
depends=(gnome-autoar gspell libcanberra libsoup)
makedepends=(git itstool meson)
source=("https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz")
sha256sums=('792585df5cb77ee670ca9eb8e6b0fceb823fe092805c026466f01d153f3e9833')

prepare() {
  mkdir build
}

build() {
  cd build
  meson --prefix=/usr --buildtype=release ../$pkgname-$pkgver
  ninja
}

check() {
  cd build
  ninja test
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
