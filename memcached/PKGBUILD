# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Dan McGee <dan@archlinux.org>
# Contributor: Michael Irwin <6d6469@gmail.com>

pkgname=memcached
pkgver=1.5.1
pkgrel=1
pkgdesc='Distributed memory object caching system'
url='https://memcached.org/'
arch=('i686' 'x86_64')
license=('GPL')
depends=('libevent')
optdepends=('perl: for memcached-tool usage')
install=memcached.install
source=(https://www.memcached.org/files/${pkgname}-${pkgver}.tar.gz
        memcached.service
        memcached.tmpfiles)
sha256sums=('a87908936b51d1bd3ef10a2347eef5647f003b63407a5d8161fa7928b1cd6b5c'
            'fd60fde92b959dc4160facc0d165f04319d2ece4d2c59b68d8ae24824abea7dd'
            'c4d0ae2218b99a276ff6e0084ae81e66add0ca9347e4bde70e9172db6e44002a')
sha512sums=('686ad82f1fd73f683fa09930d4e14aa85bd8237106e39752c9676d31aa397db1687378c4ddfadf4591f7c623837cadf90e6643263aea78a80eb294683ed0dfbb'
            '5b006064b3ab31a6982f5c7b1ab4a49d64118a459913bd4be18ca63bf606dcae3550121d05a34ac8932d28b367e18fa76699c46e311b0b6a22f36ab1885ebebe'
            '960705ff74d25afed477e0b2a5872a3a4fb49ed3105a351f0d0224abc947778f9dbda81e80be94ab636da4a8411a9dd56a8fd4513e5b86a3096a14fa67f1548b')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make test
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 755 scripts/memcached-tool -t "${pkgdir}/usr/bin"
  install -Dm 644 "${srcdir}/memcached.service" -t "${pkgdir}/usr/lib/systemd/system"
  install -Dm 644 "${srcdir}/memcached.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/memcached.conf"
}

# vim: ts=2 sw=2 et:
