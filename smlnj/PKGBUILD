# Maintainer: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Thomas Weißschuh <thomas@t-8ch.de>

pkgname=smlnj
pkgver=110.81
pkgrel=1
pkgdesc='Standard ML compiler from New Jersey'
url='http://www.smlnj.org/'
license=('BSD')
arch=('x86_64' 'i686')
provides=('sml')
install=smlnj.install
depends=('bash')
depends_x86_64+=('lib32-glibc')
makedepends_x86_64+=('gcc-multilib')
_url="http://smlnj.cs.uchicago.edu/dist/working/$pkgver/"
source=(
  "smlnj-$pkgver-boot.x86-unix.tgz::${_url}boot.x86-unix.tgz"
  "smlnj-$pkgver-config.tgz::${_url}config.tgz"
  "smlnj-$pkgver-cm.tgz::${_url}cm.tgz"
  "smlnj-$pkgver-compiler.tgz::${_url}compiler.tgz"
  "smlnj-$pkgver-runtime.tgz::${_url}runtime.tgz"
  "smlnj-$pkgver-system.tgz::${_url}system.tgz"
  "smlnj-$pkgver-MLRISC.tgz::${_url}MLRISC.tgz"
  "smlnj-$pkgver-smlnj-lib.tgz::${_url}smlnj-lib.tgz"
  "smlnj-$pkgver-ckit.tgz::${_url}ckit.tgz"
  "smlnj-$pkgver-nlffi.tgz::${_url}nlffi.tgz"
  "smlnj-$pkgver-cml.tgz::${_url}cml.tgz"
  "smlnj-$pkgver-ml-lpt.tgz::${_url}ml-lpt.tgz"
  "smlnj-$pkgver-ml-lex.tgz::${_url}ml-lex.tgz"
  "smlnj-$pkgver-ml-yacc.tgz::${_url}ml-yacc.tgz"
  "smlnj-$pkgver-ml-burg.tgz::${_url}ml-burg.tgz"
  "smlnj-$pkgver-trace-debug-profile.tgz::${_url}trace-debug-profile.tgz"
  "urlgetter.sh"
  "profile.d-smlnj.sh"
)
sha256sums=('5168f96600e0e97b533582519b51e607345b454733277051c505d86165ef532e'
            'a993d2d967455ec48d7689348ee3a7dce7c80cf5e6ce40ee5e0b30bbba620f2f'
            '2b5448906967e55ceb8acab1e301a80d26c23cb717e1f17a73eaa6340fb4dbf3'
            '44bbff8bcaedd00d7c87f51c9d28e1d03ff791a7e15bd11d5e377b2495f81a8f'
            '65cd837ebedaaae4dc02135e6903d53db4c24bb0a3f795081f634f1c20f0e34c'
            '0087f7c5ec45d8f85ea8206bd006eb89777c17a8d9eed6ab9ca937c67a88cf7c'
            '835868b96a054d0d5c8cede73905a9a3d94f32a4c0240bd7bf85c3e8989c4fdb'
            '5bdc9fe2fa20ea6b108e54725dac3933b5dbc68e43b818b4af2b99f49178a7ee'
            'e94cd79aa36e798aa842b4f35ad8dabb6bbcb32fe0abca3770b670af506b050d'
            '11b4fe2b67b095ee594a4c04b5b8fb88bd4788c939b79857da5955c580243d24'
            '6809094b0597918bed2c86a0fe4a6c95b842fe1891b13f7abbadc337b3b1f917'
            '061e0eff80ea0e51aded9e9ab97d7cc1fefd014d8b4f147db2c79bbdc6bce896'
            '3ecdd0e21a2563569526cd7c1d9073506bfebf976c94c7e4821920c3a475a4b4'
            'fcdc749ec587f271880aeea356189cbf48761eedca5478ea811afce4302ae2a6'
            '91949f33578be7e3e9160401fc86ca8932fc127335be98ab70b1614d42baa747'
            '6b7caa5038f1a80107d7b141299749271c63ad2b665bd66d76cf857a69b67159'
            '4da78effe7d3644c28c731c8e4003a9cecec9f8f61d2fa4553981a729f2d200b'
            'dd20a81a5d2899f60183215ab6a412d522d2c6801d454c142225716899e089f3')

prepare() {
  mkdir -p install
}

build() {
  unset SMLNJ_HOME MAKEFLAGS
  export srcdir
  export pkgver
  export INSTALLDIR="$srcdir/install"
  URLGETTER="$srcdir/urlgetter.sh"
  config/install.sh
}

package() {
  install -d "$pkgdir/usr/"{lib/smlnj,share}
  install -Dm755 "$srcdir/profile.d-smlnj.sh" "$pkgdir/etc/profile.d/smlnj.sh"
  install -Dm644 compiler/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  cp -R "$srcdir/install/"{bin,lib} "$pkgdir/usr/lib/smlnj"
  cp -R "$srcdir/doc/man" "$pkgdir/usr/share/"
  find "$pkgdir/usr/share/man" -name '._*' -delete
}

# vim:set ts=2 sw=2 et:
