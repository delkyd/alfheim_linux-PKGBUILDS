# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Jonne Ha√ü <me@jhass.eu>

pkgname=shards
pkgver=0.7.1
pkgrel=2
pkgdesc='The package manager for the Crystal language'
arch=(i686 x86_64)
url='https://github.com/crystal-lang/shards'
license=(Apache)
depends=(libyaml git libpcl libevent gc)
makedepends=(crystal)
source=(shards-$pkgver.tar.gz::https://github.com/crystal-lang/shards/archive/v$pkgver.tar.gz)
sha256sums=('31de819c66518479682ec781a39ef42c157a1a8e6e865544194534e2567cb110')

build() {
  cd shards-$pkgver
  crystal build --release -o bin/shards src/shards.cr
}

check() {
  cd shards-$pkgver
  ./bin/shards install
  # currently tests fail because they try to write to read-only directory
  #make test
}

package() {
  cd shards-$pkgver

  install -Dm755 bin/shards "$pkgdir/usr/bin/shards"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
