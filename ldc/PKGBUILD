# $Id$
# Maintainer:
# Contributor: Mihails Strasuns <public@dicebot.lv>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=('ldc' 'liblphobos')
groups=('dlang' 'dlang-ldc')
pkgver=1.4.0
_pkgcommit=5a79ff6abc882dd076d1a862911de288cc4ae3cd
epoch=1
pkgrel=1
pkgdesc="A D Compiler based on the LLVM Compiler Infrastructure including D runtime and libphobos2"
arch=('x86_64')
url="https://github.com/ldc-developers/ldc"
license=('BSD')
makedepends=('git' 'cmake' 'llvm' 'dmd')

source=(
    "git+https://github.com/ldc-developers/ldc#commit=$_pkgcommit"
		"git+https://github.com/ldc-developers/druntime.git"
		"git+https://github.com/ldc-developers/phobos.git"
		"git+https://github.com/ldc-developers/dmd-testsuite.git"
    "ldc2.conf"
)

sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            '266c1888da29715b277c2f56090b2e6dbb7531499f3434fe60938d6ad01705ae')

prepare() {
	cd "$srcdir/ldc"

	git submodule init
	git config submodule.druntime.url "$srcdir/druntime"
	git config submodule.phobos.url "$srcdir/phobos"
	git config submodule.tests/d2/dmd-testsuite.url "$srcdir/dmd-testsuite"
	git submodule update
}

build() {
    mkdir -p "$srcdir/tmp_install_dir"
    cd "$srcdir/ldc"

    mkdir -p build && cd build

    # workaround for wrong ld flag format generated by cmake:
    export LDFLAGS="-L-O1 -L--sort-common -L--as-needed -L-z -Lrelro"

    cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DINCLUDE_INSTALL_DIR=/usr/include/dlang/ldc \
    -DBUILD_SHARED_LIBS=ON \
		-DLDC_WITH_LLD=OFF \
    ..
    make
    make install DESTDIR="$srcdir/tmp_install_dir"
}

package_ldc() {
    depends=('liblphobos' 'llvm-libs')
    backup=('etc/ldc2.conf')
    provides=('d-compiler')

    cd "$srcdir/tmp_install_dir"

    # binaries
    install -D -m755 ./usr/bin/ldmd2 "$pkgdir/usr/bin/ldmd"
    ln -s /usr/bin/ldmd "$pkgdir/usr/bin/ldmd2"
    install -D -m755 ./usr/bin/ldc2 "$pkgdir/usr/bin/ldc"
    ln -s /usr/bin/ldc "$pkgdir/usr/bin/ldc2"

    # supplementaries
    install -D -m644 "$srcdir/ldc/bash_completion.d/ldc2" "$pkgdir/usr/share/bash-completion/completions/ldc"

    # licenses
    install -D -m644 "$srcdir/ldc/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

    # default configuration files
    install -D -m644 "$srcdir/ldc2.conf" "$pkgdir/etc/ldc2.conf"
}

package_liblphobos() {
    provides=('d-runtime' 'd-stdlib')
    depends=('curl')

    # licenses
    install -D -m644 "$srcdir/ldc/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

    cd "$srcdir/tmp_install_dir"

    # libraries
    install -D -m644 ./usr/lib/libphobos2-ldc.so "$pkgdir/usr/lib/liblphobos2.so"
    install -D -m644 ./usr/lib/libdruntime-ldc.so "$pkgdir/usr/lib/libldruntime.so"
    install -D -m644 ./usr/lib/libphobos2-ldc-debug.so "$pkgdir/usr/lib/liblphobos2-debug.so"
    install -D -m644 ./usr/lib/libdruntime-ldc-debug.so "$pkgdir/usr/lib/libldruntime-debug.so"

    # imports
    mkdir -p "$pkgdir/usr/include/dlang"
    cp -r "./usr/include/dlang/ldc" "$pkgdir/usr/include/dlang/ldc"
}
