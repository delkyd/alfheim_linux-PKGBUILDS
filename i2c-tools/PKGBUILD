# $Id$
# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: DonVla <donvla@users.sourceforge.net>

pkgname=i2c-tools
pkgver=3.1.2
pkgrel=1
pkgdesc="Heterogeneous set of I2C tools for Linux that used to be part of lm-sensors"
arch=('i686' 'x86_64')
url="http://www.lm-sensors.org/wiki/I2CTools"
license=('GPL')
depends=('perl')
makedepends=('python2' 'python')
optdepends=('read-edid: for decode-edid script'
            'python: for smbus module'
            'python2: for smbus module')
source=("http://http.debian.net/debian/pool/main/i/i2c-tools/i2c-tools_3.1.2.orig.tar.bz2")
md5sums=('7104a1043d11a5e2c7b131614eb1b962')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make
  make -C eepromer CFLAGS="$CFLAGS -I../include"
  cd py-smbus
  CPPFLAGS="$CPPFLAGS -I../include" python setup.py build
  CPPFLAGS="$CPPFLAGS -I../include" python2 setup.py build
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make prefix="${pkgdir}/usr" sbindir="$pkgdir/usr/bin" install
  install -Dm755 eepromer/eeprog eepromer/eeprom eepromer/eepromer "${pkgdir}/usr/bin"
  cd py-smbus
  python setup.py install --prefix "${pkgdir}/usr"
  python2 setup.py install --prefix "${pkgdir}/usr"
  rm -rf "${pkgdir}/usr/include"
}

# vim:set ts=2 sw=2 et:
