# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=libcdio-paranoia
pkgver=10.2+0.94+1
pkgrel=2
pkgdesc="CD paranoia libraries from libcdio"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
url="http://www.gnu.org/software/libcdio/"
depends=('libcdio')
source=(https://ftp.gnu.org/gnu/libcdio/${pkgname}-${pkgver}.tar.bz2{,.sig}
        enable_symbol_versions.patch)
sha256sums=('10304e64e5a125d5b4b336ba75eccb39ebfd977512fb04c40a7035f6ff4c6cd2'
            'SKIP'
            'f84a2467ee7ffd747cea7c783d4d53d5439b69f49d01380d650bea8454ddf5f2')
validpgpkeys=('DAA63BC2582034A02B923D521A8DE5008275EC21') # R. Bernstein <rocky@panix.com>

prepare() {
  cd "${pkgname}-${pkgver}"
  patch -Np1 -i ../enable_symbol_versions.patch
  autoreconf -fi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr --enable-cpp-progs --disable-static --disable-example-progs
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make -j1 DESTDIR="${pkgdir}" install
}
