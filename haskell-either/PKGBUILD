# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=either
pkgname=haskell-either
pkgver=4.4.1.1
pkgrel=32
pkgdesc="An either monad transformer"
url="https://github.com/ekmett/either/"
license=("custom:BSD3")
arch=('i686' 'x86_64')
depends=('ghc-libs' "haskell-bifunctors" "haskell-exceptions" "haskell-free"
         "haskell-mmorph" "haskell-monad-control" "haskell-monadrandom"
         "haskell-mtl" "haskell-profunctors" "haskell-semigroupoids"
         "haskell-semigroups" "haskell-transformers-base")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('ebc61b0f83812c7479a4fe5422ae4b73f219f0d57938e8c43e0f1e8932374c874709453773b7577f98b07ab17c884e8f4f77a28d457291b5d3f988da71fb6195')

prepare() {
    sed -e 's/MonadRandom       >= 0.1     && < 0.5,/MonadRandom       >= 0.1     \&\& < 0.6,/' \
        -e 's/mmorph            >= 1.0.0   && < 1.1,/mmorph            >= 1.0.0   \&\& < 1.2,/' \
        -i ${_hkgname}-${pkgver}/${_hkgname}.cabal
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
