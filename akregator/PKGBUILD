# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=akregator
pkgver=17.08.1
pkgrel=2
pkgdesc="A Feed Reader by KDE"
arch=(i686 x86_64)
url="https://community.kde.org/KDE_PIM"
license=(GPL2)
depends=(kontactinterface messagelib knotifyconfig ktexteditor kde-syndication)
makedepends=(extra-cmake-modules boost kdoctools python)
optdepends=('kdepim-addons: additional plugins')
groups=(kde-applications kdepim)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
        kdebug-377129.patch::"https://cgit.kde.org/akregator.git/patch/?id=d0a5f415")
sha256sums=('e728588e732fa0c56abf781d1ebe578577c4d28cef8dc721c4fe86543bdde10a'
            'SKIP'
            '587c48bc5c0415d785b606b47c1c26ca44e69726dcb29076cf6921b2b5eb0bd9')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../kdebug-377129.patch # fix crash at startup
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
