# Maintainer: Alex Forencich <alex@alexforencich.com>
pkgname=python2-ivi-git
pkgver=0.14.9.r0.g7edbd63
pkgrel=1
pkgdesc="Python Interchangeable Virtual Instrument Library"
arch=('any')
url="https://github.com/python-ivi/python-ivi"
license=('MIT')
depends=('python2' 'python2-numpy')
makedepends=('git')
provides=('python2-ivi')
optdepends=(
  'python2-vxi11: VXI11 instrument support'
  'python2-usbtmc: USBTMC instrument support'
  'python2-pyvisa: PyVISA interface for NI drivers'
)

_gitroot='https://github.com/python-ivi/python-ivi.git'
_gitname='python-ivi'

source=("$_gitname::git+$_gitroot")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  git describe --tags --long | sed -E 's/^v//;s/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/$_gitname"
}

package() {
  cd "$srcdir/$_gitname"
  python2 setup.py install --prefix=/usr --root="$pkgdir/" --optimize=1
}

