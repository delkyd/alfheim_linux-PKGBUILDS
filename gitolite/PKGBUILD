# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Maintainer:  Christian Hesse <mail@eworm.de>
# Contributor: Jon Eyolfson <jon@eyolfson.com>

pkgname=gitolite
pkgver=3.6.7
pkgrel=1
pkgdesc='An access control layer on top of git'
url='https://github.com/sitaramc/gitolite'
arch=('any')
license=('GPL2')
depends=('git' 'perl' 'openssh')
install=gitolite.install
source=($pkgname-$pkgver.tar.gz::$url/archive/v${pkgver}.tar.gz)
sha256sums=('f75d15bb438986b77bd8aeda7839dcd3624ae9d09dbfce245c00b906de8dbdc5')

prepare() {
  cd $pkgname-$pkgver/src
  echo "$pkgver" > VERSION
}

package() {
  install -d "$pkgdir"/usr/{bin,lib/gitolite}
  cp -a $pkgname-$pkgver/src/* "$pkgdir"/usr/lib/gitolite
  ln -s /usr/lib/gitolite/gitolite "$pkgdir"/usr/bin/
}
