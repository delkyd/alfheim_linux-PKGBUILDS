# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Chris van Dijk (quigybo) <quigybo at hotmail dot com>
# Contributor: Scott Olson <scott at scott-olson dot org>

pkgname=gist
pkgver=4.6.1
pkgrel=1
pkgdesc="Potentially the best command line gister"
url="https://github.com/defunkt/gist"
arch=('any')
license=('custom:MIT')
depends=('ruby')
optdepends=(
  "xclip: put pasted gist's url into X selection automagically"
  "xsel: put pasted gist's url into X selection automagically"
)
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/defunkt/${pkgname}/archive/v${pkgver}.tar.gz)
sha512sums=('25e80a2cf17c271acc1ad40600ddd3573092c34ae2dadfef5ba7385ac7d64e7798fdfe61a21356d8d5fe91e40ccd62f7d3defcd4a36acfa4bf61ec11c864e432')

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 build/${pkgname} "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 644 build/${pkgname}.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
  install -Dm 644 LICENSE.MIT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README"
}

# vim: ts=2 sw=2 et:
