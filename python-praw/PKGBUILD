# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-praw
pkgname=('python-praw' 'python2-praw')
pkgver=5.1.0
pkgrel=1
pkgdesc="Python Reddit API Wrapper that allows for simple access to reddit's API"
arch=('any')
license=('GPL')
url="http://praw.readthedocs.org"
makedepends=('python-setuptools' 'python2-setuptools' 'python-prawcore' 'python2-prawcore'
             'python-update-checker' 'python2-update-checker' 'python-pytest-runner'
             'python2-pytest-runner')
checkdepends=('python-mock' 'python2-mock' 'python-betamax-serializers'
              'python2-betamax-serializers' 'python-betamax-matchers' 'python2-betamax-matchers')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/praw-dev/praw/archive/v$pkgver.tar.gz")
sha512sums=('c49d920c529506e696df1436b38f84fdbbb0c37fb6978200eb9e43e0eb0428f44a461e835c06897adab4713678507da00f0f02809d281206f3473e6b7345862e')

prepare() {
  cp -a praw-$pkgver{,-py2}
}

check() {
  cd "$srcdir"/praw-$pkgver
  python setup.py pytest

  cd "$srcdir"/praw-$pkgver-py2
  python2 setup.py pytest
}

package_python-praw() {
  depends=('python-prawcore' 'python-update-checker')

  cd praw-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-praw() {
  depends=('python2-prawcore' 'python2-update-checker')

  cd praw-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
