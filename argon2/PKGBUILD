# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>
pkgname=argon2
pkgver=20161029
pkgrel=3
pkgdesc="A password-hashing function (reference C implementation)"
arch=("x86_64" "i686")
url="https://github.com/P-H-C/phc-winner-argon2"
license=('Apache')
depends=("glibc")
source=("https://github.com/P-H-C/phc-winner-argon2/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        "libargon2.pc"
        "fix-cflags.patch"
        "only-install-shared-lib.patch")
sha256sums=('fe0049728b946b58b94cc6db89b34e2d050c62325d16316a534d2bedd78cd5e7'
            '4189ce6c183319ceaf2c4ec96facd424deeaa9e2819efb7511893bcf3ef5c836'
            '4e6d2676d28a9a9a34ec58788ea942083e3892e6b67d2d765eafb32150246728'
            '5b032ecbbdf057518f13ba29a8599c376551170de891780923502b65ff8bc128')

prepare() {
  cd "$srcdir/phc-winner-$pkgname-$pkgver"
  # Fix an attempt at voodoo magic with the build flags (resulting in
  # -march=native, which produces invalid opcodes when run on older CPUs)
  # Similar issue: https://bugs.debian.org/cgi-bin/bugreport.cgi?bug=828779
  patch -p1 < "${srcdir}/fix-cflags.patch"
  # This just avoids compiling the static lib in the package() function.
  patch -p1 < "${srcdir}/only-install-shared-lib.patch"
  # Copy custom pkg-config file
  cp "${srcdir}/libargon2.pc" libargon2.pc
  sed -i -e "s/@UPSTREAM_VER@/${pkgver}/" libargon2.pc
}

build() {
  cd "$srcdir/phc-winner-$pkgname-$pkgver"
  make argon2 libargon2.so OPTTARGET="${pkgarch/_/-}"
}

check() {
  cd "$srcdir/phc-winner-$pkgname-$pkgver"
  make test OPTTARGET="${pkgarch/_/-}"
}

package() {
  cd "$srcdir/phc-winner-$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  chmod -x "${pkgdir}/usr/include/argon2.h"
  install -Dm644 libargon2.pc "${pkgdir}/usr/lib/pkgconfig/libargon2.pc"
}

# vim:set ts=2 sw=2 et:
