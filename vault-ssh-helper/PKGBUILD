# Maintainer: Adrián Pérez de Castro <aperez@igalia.com>
pkgname='vault-ssh-helper'
pkgdesc='Allows using OTP authentication generated by a Vault server'
pkgver='0.1.3'
pkgrel='1'
url='https://github.com/hashicorp/vault-ssh-helper/'
arch=('x86_64' 'i686')
license=('MPL')
makedepends=('go')
depends=('glibc')
source=("${url}/archive/v${pkgver}.tar.gz")
sha512sums=('ff42e86c941ada0352eb58f29ab36785d9ca2777fbfa8f1b830e752e37376b258ad812e75b764709a1b1f22d34dffafb3515e3f196386987875a1a5fcfb04759')

_srcpath='src/github.com/hashicorp/vault-ssh-helper'
prepare () {
	if [[ ! -r ${_srcpath} ]] ; then
		mkdir -p "$(dirname "${_srcpath}")"
		ln -s "$(pwd)/${pkgname}-${pkgver}" "${_srcpath}"
	fi

	export GOPATH="${srcdir}:$(pwd)"
	cd "${_srcpath}"
	make updatedeps NAME=${pkgname}
	go generate ./...
}

build () {
	export GOPATH="${srcdir}:$(pwd)"
	cd "${_srcpath}"
	go build -v -o "${srcdir}/vault-ssh-helper"
}

package () {
	install -Dm755 "${srcdir}/vault-ssh-helper" \
		           "${pkgdir}/usr/bin/vault-ssh-helper"
	install -Dm644 "${pkgname}-${pkgver}/README.md" \
		           "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}
