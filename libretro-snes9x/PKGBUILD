# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=libretro-snes9x
pkgver=660
pkgrel=1
pkgdesc='Super Nintendo Entertainment System core'
arch=('i686' 'x86_64')
url='https://github.com/libretro/snes9x'
license=('custom' 'GPL2' 'LGPL2.1')
groups=('libretro')
depends=('gcc-libs' 'glibc')
makedepends=('git')
_commit='95715f8ed09b8632c7ae978ed374b36ee260facb'
source=("libretro-snes9x::git+https://github.com/libretro/snes9x.git#commit=${_commit}"
        'git+https://github.com/libretro/libretro-core-info.git')
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd libretro-snes9x

  git rev-list --count HEAD
}

build() {
  cd libretro-snes9x/libretro

  make
}

package() {
  cd libretro-snes9x/libretro

  install -dm 755 "${pkgdir}"/usr/{lib/libretro,share/libretro/info}
  install -m 644 snes9x_libretro.so "${pkgdir}"/usr/lib/libretro/
  install -m 644 ../../libretro-core-info/snes9x_libretro.info "${pkgdir}"/usr/share/libretro/info/

  install -dm 755 "${pkgdir}"/usr/share/licenses/libretro-snes9x
  install -m 644 ../docs/snes9x-license.txt "${pkgdir}"/usr/share/licenses/libretro-snes9x/
}

# vim: ts=2 sw=2 et:
