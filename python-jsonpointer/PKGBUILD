# $Id$
# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Limao Luo <luolimao+AUR@gmail.com>

pkgbase=python-jsonpointer
pkgname=(python2-jsonpointer python-jsonpointer)
pkgver=1.12
pkgrel=1
pkgdesc="Identify specific nodes in a JSON document (RFC 6901)"
arch=('any')
url="https://github.com/stefankoegl/python-json-pointer"
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://pypi.io/packages/source/j/jsonpointer/jsonpointer-$pkgver.tar.gz"
        "https://raw.github.com/stefankoegl/python-json-pointer/master/COPYING")
sha512sums=('9832752d93bc93c96293243ef703418e1b29b196809c70e9d1a669747d7cdb453a48288f90687b69a83f51b0859e9501286ea411edbc216c24ae72e7ceffe1f1'
            '6a4c6b57a9cfe227175852c67ab7536fcc23646761cd10d25800edfccf408b139b1f589313f6cf6717c5764ca2e554cc16921118e01aebfbdf3ddfada82c6bac')

prepare(){
    cp -a jsonpointer-$pkgver jsonpointer-$pkgver-2
    find jsonpointer-$pkgver-2 -name \*.py -exec sed -i '1s/python$/&2/' {} +
}

build() {
    cd "$srcdir"/jsonpointer-$pkgver
    python setup.py build

    cd "$srcdir"/jsonpointer-$pkgver-2
    python2 setup.py build
}

check() {
    cd "$srcdir"/jsonpointer-$pkgver
    python tests.py

    cd "$srcdir"/jsonpointer-$pkgver-2
    python2 tests.py
}

package_python-jsonpointer() {
    depends=('python')

    cd jsonpointer-$pkgver
    python setup.py install --prefix=/usr --root="$pkgdir"
    install -Dm644 "$srcdir"/COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
package_python2-jsonpointer() {
    depends=('python2')

    cd jsonpointer-$pkgver-2
    python2 setup.py install --prefix=/usr --root="$pkgdir"
    mv "$pkgdir"/usr/bin/jsonpointer{,2}
    install -Dm644 "$srcdir"/COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
