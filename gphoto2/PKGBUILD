# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Eduardo Romero <eduardo@archlinux.org>
# Contributor: Jason Chu <jason@archlinux.org>

pkgname=gphoto2
pkgver=2.5.14
pkgrel=1
pkgdesc="A digital camera download and access program."
arch=(i686 x86_64)
url="https://www.gphoto.org"
license=('GPL' 'LGPL')
depends=('libgphoto2' 'popt' 'readline')
source=(https://downloads.sourceforge.net/gphoto/${pkgname}-${pkgver}.tar.bz2{,.asc})
sha256sums=('9302d02fb472d4936988382b7277ccdc4edaf7ede56c490278912ffd0627699c'
            'SKIP')
validpgpkeys=('7C4AFD61D8AAE7570796A5172209D6902F969C95')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
}
