# $Id$
# Maintainer: Sergej Pupykin <pupykin.s@gmail.com>
# Contributor: damir <damir@archlinux.org>

pkgname=libextractor
pkgver=1.4
pkgrel=2
pkgdesc="A library used to extract meta-data from files of arbitrary type"
arch=("i686" "x86_64")
license=('GPL')
url="http://gnunet.org/libextractor/"
depends=('libltdl')
makedepends=('flac' 'poppler' 'exiv2' 'qt4' 'libvorbis' 'gtk2' 'libgsf' 'libmpeg2')
optdepends=('flac: flac support'
	    'poppler: pdf support'
	    'exiv2: metadata support'
	    'qt: the Qt GUI interface/thumbnails'
	    'gtk2: gtk2 GUI interface/thumbnails'
	    'libgsf: (optional) for ODF support'
	    'libmpeg2: mpeg support'
	    'libvorbis: ogg/vorbis support')
options=('!makeflags')
validpgpkeys=('19647543F7861D3BF4E64FF7BF60708B48426C7E'
	      'D8423BCB326C7907033929C7939E6BE1E29FC3CC')
source=("https://ftp.gnu.org/gnu/libextractor/${pkgname}-${pkgver}.tar.gz"{,.sig})
sha256sums=('84128170a4a9aa3a19942dd53fdf30ed17b56d7fae79b5f6e7e17a0d65d1f66c'
            'SKIP')

build() {
  cd "$srcdir"/${pkgname}-${pkgver}
  ./configure --prefix=/usr --enable-shared --disable-static
  make
}

package() {
  cd "$srcdir"/${pkgname}-${pkgver}
  make DESTDIR="$pkgdir" install
  rm "$pkgdir"/usr/share/info/dir
}
