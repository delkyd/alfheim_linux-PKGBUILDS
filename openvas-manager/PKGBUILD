# $Id$
# Maintainer: Daniel Micay <danielmicay@gmail.com>
pkgname=openvas-manager
pkgver=6.0.11
pkgrel=1
pkgdesc='A layer between the OpenVAS Scanner and various client applications'
arch=('i686' 'x86_64')
url='http://www.openvas.org/'
license=('GPL')
depends=('libxslt' 'openvas-libraries' 'rsync' 'sqlite3')
makedepends=('cmake' 'doxygen' 'xmltoman')
optdepends=('python2: SPLIT_PART_SIZE feature via xml_split script')
groups=(openvas)
install=$pkgname.install
# These URLs need to be manually updated on every pkgver upgrade.
# http://wald.intevation.org/frs/?group_id=29
source=("http://wald.intevation.org/frs/download.php/2445/$pkgname-${pkgver}.tar.gz"
        "http://wald.intevation.org/frs/download.php/2446/$pkgname-${pkgver}.tar.gz.asc")
sha512sums=('29452400b3d2a999e4c71bc015a45add38be668a1d697d839cb05cea25ca55baff7463e669f844d43dad6ab6d293b4589570f80eddc4955c9bf611dc6d850890'
            'SKIP')
validpgpkeys=(
              'C3B468D2288C68B9D526452248479FF648DB4530' # OpenVAS Transfer Integrity
)

prepare() {
  cd $pkgname-$pkgver
  sed -i '1c#!/usr/bin/python2' tools/extra/xml_split
}

build() {
  cd $pkgname-$pkgver
  cmake -DCMAKE_BUILD_TYPE=Release -DSBINDIR=/usr/bin \
    -DCMAKE_INSTALL_PREFIX=/usr -DSYSCONFDIR=/etc -DLOCALSTATEDIR=/var .
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
