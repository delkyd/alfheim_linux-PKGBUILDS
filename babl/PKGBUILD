# $Id$
# Maintainer: Daniel Isenmann <daniel@archlinux.org>

pkgname=babl
pkgver=0.1.30
pkgrel=1
pkgdesc="Dynamic, any to any, pixel format conversion library"
arch=('i686' 'x86_64')
url="http://gegl.org/babl/"
license=('LGPL3')
depends=('glibc')
makedepends=('gobject-introspection' 'vala')
options=('!makeflags')
source=(https://ftp.gimp.org/pub/babl/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('45c12c7b06d965123756821fc70c968137d16d44151a6eb55075f904e11d53cc')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
