# $Id$
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Mikko Seppälä <t-r-a-y@mbnet.fi>

_pkgbasename=glib2
pkgname=lib32-$_pkgbasename
pkgver=2.52.2+9+g3245eba16
pkgrel=1
pkgdesc="Low level core library (32-bit)"
url="http://www.gtk.org/"
arch=(x86_64)
license=(LGPL)
depends=(lib32-dbus lib32-libffi lib32-pcre lib32-util-linux lib32-libelf "$_pkgbasename")
makedepends=(gcc-multilib python git)
_commit=3245eba169c439180cc13ed3c76f47298d723031  # glib-2-52
source=("git+https://git.gnome.org/browse/glib#commit=$_commit"
        gio-querymodules-32.hook)
sha256sums=('SKIP'
            '73b6791b58180e2a02fe908938e6c547ee1b263f8c387262f9a8c4105ffa999a')

pkgver() {
  cd glib
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd glib
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd glib
  ./configure --prefix=/usr --libdir=/usr/lib32 \
      --sysconfdir=/etc \
      --with-pcre=system \
      --disable-fam \
      --disable-gtk-doc \
      $(check_option debug y && echo --enable-debug=yes)
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd glib
  make DESTDIR="$pkgdir" install

  rm -r "$pkgdir"/usr/{share,include}
  find "$pkgdir/usr/bin" -type f -not -name gio-querymodules -delete

  mv "$pkgdir"/usr/bin/gio-querymodules{,-32}

  # install hooks
  install -d "$pkgdir/usr/share/libalpm/hooks/"
  install -m644 "$srcdir"/gio-querymodules-32.hook \
    "$pkgdir/usr/share/libalpm/hooks/"
}
