# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-logs
pkgver=3.26.0+2+g9068aaa
pkgrel=1
pkgdesc="A log viewer for the systemd journal"
arch=(i686 x86_64)
url="https://wiki.gnome.org/Apps/Logs"
license=(GPL)
groups=(gnome-extra)
depends=(systemd gtk3 gsettings-desktop-schemas)
makedepends=(intltool itstool gnome-common appstream-glib git)
_commit=9068aaadc4ca80470f0f54df40378e7e5b49dfef  # gnome-3-26
source=("git+https://git.gnome.org/browse/gnome-logs#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^gnome-logs-//;s/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
