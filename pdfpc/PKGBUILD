# $Id$
# Maintainer: Jiachen Yang <farseerfc@archlinux.org>
# Contributor: Andreas Bilke <andreas@bilke>

pkgname=pdfpc
pkgver=4.0.8
_pkgver=$pkgver
pkgrel=3
pkgdesc='A presenter console with multi-monitor support for PDF files'
arch=('i686' 'x86_64')
url='https://pdfpc.github.io/'
license=('GPL')
depends=('gtk3' 'poppler-glib' 'libgee' 'gstreamer' 'gst-plugins-base')
optdepends=('gst-plugins-good: more codecs for video playback support'
            'gst-libav: more codecs for video playback support')
makedepends=('cmake' 'vala')
source=("$pkgname-$_pkgver.tar.gz::https://github.com/pdfpc/pdfpc/archive/v$_pkgver.tar.gz"
"pr277.patch::https://github.com/pdfpc/pdfpc/commit/202abb5d2f9095689fbdf295c796ea57bcd0e934.patch")

sha256sums=('c04539398176b0911ec7216c48a04d90d7c893aa5b16cfdef8ea861885991066'
            '376f33bdd919a15564ebec25e48e97aadd42cf386ade5b5362ddaddc773ac1fa')

prepare() {
    cd "$srcdir/$pkgname-$_pkgver"
    patch -p1 <../pr277.patch

}

build() {
    cd "$srcdir/$pkgname-$_pkgver"
    cmake  -DCMAKE_INSTALL_PREFIX="/usr/" -DSYSCONFDIR="/etc" .
    make
}

package() {
    cd "$srcdir/$pkgname-$_pkgver"
    make DESTDIR="$pkgdir/" install
}

# vim:set ts=4 sw=4 et:
