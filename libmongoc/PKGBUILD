# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=libmongoc
pkgver=1.8.0
pkgrel=1
pkgdesc='A client library written in C for MongoDB'
arch=('i686' 'x86_64')
url='http://www.mongodb.org/display/DOCS/C+Language+Center'
license=('Apache')
depends=('libbson' 'libsasl')
checkdepends=('python-pifpaf')
checkdepends_x86_64=('mongodb')
source=("$pkgname-$pkgver.tar.gz::https://github.com/mongodb/mongo-c-driver/archive/$pkgver.tar.gz")
sha512sums=('8e46c695abf72186a2bc220a837580bdbb33f3bef6a618b84c4e47053317415228b95751d2b306f799d46dea0c0eaeee2536c0d0e854ba23be35996061a0f26a')

prepare() {
  cd mongo-c-driver-$pkgver
  # Remove check for bundled libbson
  sed -i 's|src/libbson/autogen.sh|/usr/bin/true|' autogen.sh
}

build() {
  cd mongo-c-driver-$pkgver
  PTHREAD_LIBS=-pthread ./autogen.sh --prefix=/usr
  make
}

check() {
  cd mongo-c-driver-$pkgver
  pifpaf run mongodb --port 27017 make test || warning "Tests failed"
}

package() {
  cd mongo-c-driver-$pkgver
  make DESTDIR="$pkgdir/" install
}
