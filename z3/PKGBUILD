# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: rudy.matela

pkgname=z3
pkgver=4.5.0
pkgrel=4
pkgdesc="A high-performance theorem prover"
arch=('i686' 'x86_64')
url="https://github.com/Z3Prover/z3"
license=('MIT')
depends=('gcc-libs')
makedepends=('python2') # 'java-environment-common' 'mono' 'ocaml' 'ocaml-findlib')
optdepends=('python2-setuptools: bindings for python2')
source=("https://github.com/Z3Prover/z3/archive/z3-$pkgver.tar.gz")
sha256sums=('aeae1d239c5e06ac183be7dd853775b84698db1265cb2258e5918a28372d4a0c')

build() {
  cd "$srcdir/$pkgname-$pkgname-$pkgver"
  python2 scripts/mk_make.py --python # --dotnet --java --ml
  cd build
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgname-$pkgver/build
  make install DESTDIR="$pkgdir"
  install -Dm644 ../LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
