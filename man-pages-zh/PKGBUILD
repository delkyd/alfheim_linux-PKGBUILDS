# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: dickeny@gmail.com
# Contributor: cuihao <cuihao dot leo at gmail dot com>

pkgbase=man-pages-zh
pkgname=(man-pages-zh_cn man-pages-zh_tw)
pkgver=1.6.3.1
pkgrel=1
arch=('any')
url='https://github.com/man-pages-zh/manpages-zh'
license=('GPL2')
depends=('man-db')
makedepends=('opencc' 'python' 'git')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/man-pages-zh/manpages-zh/archive/v$pkgver.tar.gz")
sha512sums=('4a7acc415195ed108e80b2ec3e92ade903a66e187c69937c84727ba6d95bbc441e2500e1adbc09f07bdd8c57ba6ee8d2708fbbcfa6f375cf8dc5bc4808de40cd')

build() {
  cd manpages-zh-$pkgver
  autoreconf -i
  ./configure --prefix=/usr
  make
}

package_man-pages-zh_cn() {
  pkgdesc='Simplified Chinese Linux man pages'

  cd manpages-zh-$pkgver
  make install DESTDIR="$pkgdir"

  rm -r "$pkgdir"/usr/share/man/zh_TW

  # remove conflicted files with shadow
  cd "$pkgdir"/usr/share/man/zh_CN
  rm -f ./man1/groups*
}

package_man-pages-zh_tw() {
  pkgdesc='Traditional Chinese Linux man pages'

  cd manpages-zh-$pkgver
  make install DESTDIR="$pkgdir"

  rm -r "$pkgdir"/usr/share/man/zh_CN

  # remove conflicted files with shadow
  cd "$pkgdir"/usr/share/man/zh_TW
  rm -f ./man1/groups*
}
