# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Michael Straube <straubem@gmx.de>

pkgname=latte-dock
pkgver=0.7.1
pkgrel=1
pkgdesc='A dock based on Plasma Frameworks'
arch=(i686 x86_64)
url='https://github.com/psifidotos/Latte-Dock'
license=(GPL)
depends=(plasma-framework hicolor-icon-theme)
makedepends=(extra-cmake-modules python)
source=("$pkgname-$pkgver.tar.gz::https://github.com/psifidotos/Latte-Dock/archive/v$pkgver.tar.gz")
sha256sums=('b57a5495370fea6ae61f8045590d958c477efbe454690ea52d992d8ec76b80ea')

prepare() {
  mkdir -p build
}

build() {
  cd build

  cmake ../Latte-Dock-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build

  make DESTDIR="$pkgdir" install
}
