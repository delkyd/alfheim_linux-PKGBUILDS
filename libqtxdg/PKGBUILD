# $Id$
# Maintainer: Jerome Leclanche <jerome@leclan.ch>

pkgname=libqtxdg
pkgver=2.0.0
pkgrel=3
pkgdesc="Library providing freedesktop.org XDG specs implementations for Qt."
arch=("i686" "x86_64")
url="http://lxqt.org"
license=("GPL2")
depends=("libsm" "libxkbcommon-x11" "qt5-base" "qt5-svg")
makedepends=("cmake")
source=(
	"https://github.com/lxde/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz"
	"https://github.com/lxde/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz.asc"
)
sha512sums=('f4e32346eddedd31195093214a45e9831742ee886a14b5d7ba1d640a91e05473b9b8adc7bca9970090f1fa8187597cd10796c32ecea4ac02229e9869cd4f1db3'
            'SKIP')
validpgpkeys=(
	"169704C6FB490C6892C7F23C37E0AF1FDA48F373"
	"7C733BA5F585AAD669E4D23A42C9C8D3AF5EA5E3"
)


build() {
	mkdir -p build
	cd build
	cmake "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INSTALL_LIBDIR=lib
	make
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
