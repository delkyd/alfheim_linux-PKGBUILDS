# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=brial
pkgver=1.0.1
pkgrel=1
pkgdesc="Library for polynomials over boolean rings"
arch=(i686 x86_64)
url="https://github.com/BRiAl/BRiAl"
license=(GPL)
depends=(python2 m4ri)
makedepends=(boost)
source=("https://github.com/BRiAl/BRiAl/releases/download/$pkgver/brial-$pkgver.tar.bz2")
sha256sums=('75b514b952d46e42e235821a9ba33c5b8b1bf207f5116fff06a5ad7cb81cb746')

build() {
  cd $pkgname-$pkgver 

  export PYTHON=python2
  export CXXFLAGS="$CXXFLAGS -std=c++98"
  ./configure --prefix=/usr --enable-shared
  make

  cd sage-brial
  python2 setup.py build
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install

  cd sage-brial
  python2 setup.py install --skip-build --root="$pkgdir" --optimize=1
}
