# $Id$
# Maintainer: Maxime Gauduin <alucryd at gmail dot com>
# Contributor: Ner0 <darkelfdarkelf666 at yahoo dot co dot uk>

pkgname=granite
pkgver=0.4.1
pkgrel=1
pkgdesc='Library that extends Gtk+'
arch=('i686' 'x86_64')
url='https://github.com/elementary/granite'
license=('LGPL3')
groups=('pantheon')
depends=('cairo' 'gdk-pixbuf2' 'glib2' 'glibc' 'gtk3' 'libgee' 'pango')
makedepends=('cmake' 'gobject-introspection' 'vala')
provides=('libgranite.so')
source=("granite-${pkgver}.tar.gz::https://github.com/elementary/granite/archive/${pkgver}.tar.gz")
sha256sums=('035a9b36ef73352ff8a76be06b876d9e2ea6633abb693fb67297d998c8d3a240')

prepare() {
  cd granite-${pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd granite-${pkgver}/build

  cmake .. \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_LIBDIR='/usr/lib'
  make
}

package() {
  cd granite-${pkgver}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
