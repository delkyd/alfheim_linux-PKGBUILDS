# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Hyacinthe Cartiaux <hyacinthe.cartiaux AT free DOT fr>
# Contributor: Juan Diego Tascon
# Contributor: Tom Vincent <http://tlvince.com/contact/>

_gemname=rainbow
pkgname=ruby-${_gemname}
pkgver=2.2.2
pkgrel=1
pkgdesc="Rainbow is extension to ruby's String class adding support for colorizing text on ANSI terminal"
url='https://rubygems.org/gems/rainbow'
arch=('any')
license=('MIT')
depends=('ruby')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/sickill/rainbow/archive/v${pkgver}.tar.gz)
sha256sums=('6e5c6e852cd62b6975a9594d0a0a03f9f9cc997f308a12f38f040698e6e1b9f7')
sha512sums=('fac9a8cc39f23a1b268f3a01d755a5ccb79f8ee11a28228d958a5b77472c5b3fbaac748c917f7d4afebb911cbef0e0242c5224c8df29672073c1f54bf44c9307')

prepare() {
  cd ${_gemname}-${pkgver}
  sed -r 's|~>|>=|g' -i rainbow.gemspec # don't give a fuck about rubys bla bla
  sed -r 's|git ls-files|find|g' -i rainbow.gemspec
}

build() {
  cd ${_gemname}-${pkgver}
  gem build rainbow.gemspec
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}/${_gemdir}" -n "${pkgdir}/usr/bin" rainbow*.gem
  install -Dm 644 README.markdown Changelog.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
