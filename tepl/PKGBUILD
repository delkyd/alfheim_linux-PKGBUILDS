# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Tobias Bohrmann (PlainTextField) <honeypot.carbage@aechelon.de>

pkgname=tepl
_oldname=gtef
pkgver=2.0.1
pkgrel=1
pkgdesc="Library that eases the development of GtkSourceView-based text editors and IDEs"
arch=('x86_64' 'i686')
url="https://wiki.gnome.org/Projects/Tepl"
license=('LGPL2.1')
depends=('gtksourceview3' 'uchardet')
makedepends=('gobject-introspection' 'vala')
provides=("$_oldname=$pkgver")
conflicts=("$_oldname")
replaces=("$_oldname")
source=("https://download.gnome.org/sources/$_oldname/${pkgver%.*}/$_oldname-$pkgver.tar.xz")
sha256sums=('8432f0f404b93e5a2702978b5f121b8f9ae2167c906e9f2ed7b5165142e27a4f')

build() {
	cd $_oldname-$pkgver
	./configure --prefix=/usr
	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
	make
}

package() {
	cd $_oldname-$pkgver
	make DESTDIR="$pkgdir" install
}
