# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=quine-relay-git
pkgver=r306.9e3cf6c
pkgrel=1
pkgdesc='Uroboros program with 100 programming languages (master)'
arch=('x86_64' 'i686')
url='http://github.com/mame/quine-relay'
license=('MIT')
depends=('algol68g' 'bash' 'boo' 'chicken' 'clisp' 'clojure' 'fpc' 'gawk' 'gcc'
         'gcc-fortran' 'ghc' 'go' 'groovy' 'llvm' 'make' 'mono' 'nodejs' 'ocaml'
         'octave' 'parrot' 'perl' 'php' 'python' 'r' 'ruby' 'scala' 'tcl'
         'ucblogo' 'vala' 'c-intercal' 'coffee-script' 'f2c' 'gforth' 'icon'
         'iverilog' 'gnu-cobol' 'pike' 'regina-rexx-das' 'swi-prolog'
         'smalltalk' 'jasmin' 'gcc-objc' 'gcc-ada' 'scilab' 'lci-git'
         'spl' 'maxima' 'slang' 'fpiet' 'neko' 'nickle' 'pari' 'bsd-games'
         'ratfor' 'gambas3-script' 'mlton' 'thue' 'mono-basic' 'zoem' 'asymptote'
         'afnix' 'a+' 'ats2-postiats' 'cfunge' 'boarpig-brainfuck' 'cduce')
makedepends=('setconf')
optdepends=('advancecomp: For generating langs.png'
            'optipng: For generating langs.png')
source=('quine-relay::git://github.com/mame/quine-relay.git'
        'quine-relay.sh')
sha256sums=('SKIP'
            '2409f6ccaed7070143b365fb1e276ff73620d9651f12d5058c2291b3d093be95')
conflicts=('quine-relay')
replaces=('quine-relay')

pkgver() {
  cd "${pkgname%-git}"

  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${pkgname%-git}"

  <README.md tail -22 >LICENSE

  # lolcode is available in Arch Linux
  sed -i 's:vendor/lci-\*/::' Makefile

  setconf Makefile BF "bpf -src"
}

package() {
  cd "${pkgname%-git}"

  install -d "$pkgdir/usr/share/quine-relay"
  cp -r * "$pkgdir/usr/share/quine-relay"
  install -Dm755 ../quine-relay.sh "$pkgdir/usr/bin/quine-relay"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
