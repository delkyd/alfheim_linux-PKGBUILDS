# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kmines
pkgver=17.08.1
pkgrel=1
pkgdesc="The classic Minesweeper game"
url="https://kde.org/applications/games/kmines/"
arch=(i686 x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kdegames)
depends=(libkdegames hicolor-icon-theme)
makedepends=(extra-cmake-modules python kdoctools)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('4868791466b90e3240fbe8c0dcd2693ac94257b8f64882f709374be47f4589aa'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
