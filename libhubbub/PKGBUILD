# $Id$
# Maintainer: Alexander F Rødseth <xyproto@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>

pkgname=libhubbub
pkgver=0.3.3
pkgrel=2
pkgdesc='HTML5 compliant parsing library'
arch=('x86_64' 'i686')
url='http://www.netsurf-browser.org/projects/hubbub/'
license=('MIT')
provides=('hubbub')
replaces=('hubbub')
conflicts=('hubbub')
depends=('libparserutils>=0.2.3-2')
makedepends=('netsurf-buildsystem')
source=("http://download.netsurf-browser.org/libs/releases/$pkgname-$pkgver-src.tar.gz")
sha256sums=('066dffd127ead12da60050aa13b80891fb996d00197767d038e48ac163402780')

prepare() {
  sed -i 's:-Werror::' "$pkgname-$pkgver/Makefile"
}

build() {
  make -C "$pkgname-$pkgver" INCLUDEDIR=include LIBDIR=lib PREFIX=/usr \
    COMPONENT_TYPE='lib-shared'
}

package() {
  cd "$pkgname-$pkgver"

  make install INCLUDEDIR=include LIBDIR=lib PREFIX=/usr DESTDIR="$pkgdir" \
    COMPONENT_TYPE='lib-shared'
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
