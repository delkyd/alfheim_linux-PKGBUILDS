# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Hyacinthe Cartiaux <hyacinthe.cartiaux@free.fr>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: Sarah Hay <sarahhay@mb.sympatico.ca>
# Contributor: Mark Pustjens <pustjens@dds.nl>

pkgname=xpdf
pkgver=4.00
pkgrel=1
pkgdesc='Viewer for Portable Document Format (PDF) files'
url='https://www.xpdfreader.com/'
arch=('i686' 'x86_64')
license=('GPL3')
backup=('etc/xpdfrc')
depends=('openmotif' 'gsfonts' 'libxt' 'libpaper' 'xdg-utils' 'qt5-base' 'libpng' 'libxpm')
optdepends=('poppler: tools that used to be included in xpdf'
            'desktop-file-utils: for desktop environments')
makedepends=('cmake' 'qt5-tools')
source=(https://www.xpdfreader.com/dl/${pkgname}-${pkgver}.tar.gz{,.sig}
        xpdf-4.00-64bit.patch
        xpdf-4.00-man-path-fix.patch
        xpdf-4.00-permissions.patch
        xpdf-4.00-libpaperfix.patch
        xpdf.desktop)
sha512sums=('32fb928aeb053be29c7342dda8caac8a7a3720e8f39507df29ad5a070e1423cc0960da4d7b36046d0be27b842b108e984363c0285839a0fd371a119948af0cac'
            'SKIP'
            '25707095b0825b5d5c0fd63f357dfac57a81ec65b9a6b75155126be04de8ac24cf8048bf55eb9baaf9dd8cf79ed2aa8e19ebcacb79b48a569462d4cc7ad620f3'
            '77d533e0cb2ed5e232a0497891eb6ec634659305a1c19d7c3a8f7efee1d18ba9e03b1ee009a9834497f8d7721694d4bd65e897ee4c9499544194bf6b240052a0'
            'f17c1c19c29518a3538a202c19fb60efc7721a42065d32c841f9ed929e571d9373c8558fdae251c1ef3781857cf51483244276179862998f94c9c1fd8bd63695'
            'aa101da42a3f4d8a559f8387143909c863de80dd6c4d2454a9a6cecef41a889f6e5d88225c22ec2c271c05eb77a412a0e0d66b94554b9ff5ccfe97953f1c1c4e'
            'd6163eb220bd567226c6006bef30c6db1eac1263ae161392db9766dca1ce78014cc555dd9d27febd2b924150053fa01b47c06777b1e760864459574c7614bfc4')
validpgpkeys=('A56006CA75CF8B13FA2F120DF4825F5397271342') # -Xpdf- <xpdf@xpdfreader.com>

prepare() {
  cd ${pkgname}-${pkgver}
  mkdir -p build
  cp doc/sample-xpdfrc xpdfrc
  patch -p1 < "${srcdir}/xpdf-4.00-64bit.patch"
  patch -p1 < "${srcdir}/xpdf-4.00-man-path-fix.patch"
  patch -p1 < "${srcdir}/xpdf-4.00-permissions.patch"
  patch -p1 < "${srcdir}/xpdf-4.00-libpaperfix.patch"
  sed 's:/usr/share/fonts/type1/gsfonts:/usr/share/fonts/Type1:' -i xpdf/GlobalParams.cc
  sed 's:/usr/local/share/:/usr/share/:g' -i xpdfrc
}

build() {
  cd ${pkgname}-${pkgver}/build
  cmake .. \
  -DMULTITHREADED=ON \
  -DOPI_SUPPORT=ON \
  -DCMAKE_INSTALL_PREFIX=/usr \
  -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make -C build DESTDIR="${pkgdir}" install

  install -Dm 644 xpdfrc -t "${pkgdir}/etc"
  install -Dm 644 "${srcdir}/xpdf.desktop" -t "${pkgdir}/usr/share/applications"
  install -Dm 644 xpdf-qt/xpdf-icon.svg "${pkgdir}/usr/share/pixmaps/xpdf.svg"
  install -Dm 644 README CHANGES -t "${pkgdir}/usr/share/doc/${pkgname}"

  # remove stuff provided by poppler
  for tool in pdfdetach pdffonts pdfimages pdfinfo pdftoppm pdftops pdftotext pdftohtml; do
    rm "${pkgdir}/usr/bin/${tool}" "${pkgdir}/usr/share/man/man1/${tool}.1"
  done
}

# vim: ts=2 sw=2 et:
