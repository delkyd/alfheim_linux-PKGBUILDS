# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Robert Knauer <robert@privatdemail.net>
# Contributor: xav <xav at ethertricks dot net>

pkgname=umurmur
pkgver=0.2.17
pkgrel=3
pkgdesc='Minimalistic Mumble server'
url='https://github.com/umurmur/umurmur'
arch=('i686' 'x86_64')
license=('custom')
depends=('mbedtls' 'libconfig' 'protobuf-c')
makedepends=('cmake')
backup=('etc/umurmur/umurmur.conf')
install=${pkgname}.install
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/umurmur/umurmur/archive/${pkgver}.tar.gz
        umurmur.service)
sha256sums=('e77b7b6616768f4a1c07442afe49a772692f667b00c23cc85909d4dd0ce206d2'
            '3575e69da5ad1fc8541ef37af90cdd245e3202980589ffd26d124fd9e785b738')
sha512sums=('a496a51fd7815ad117f5aee17bb78cbd319c584ad60ab8aebbfd8ddf7b1760f443f2337bc74be1e0d5af17d3c3df2ae6c9060eca576cf1e6ed4c6cb0825e9c15'
            'ceb42e7ed4385d16c9d9f30312cc5a57b0ee0ba0ec423e7e2f3e9abc1956506e8817e9a3cb2257112434d568594f4e495f1661af0f89451f802f924790df8ffa')

prepare() {
  cd ${pkgname}-${pkgver}
  mkdir build
}

build() {
  cd ${pkgname}-${pkgver}/build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSSL=mbedtls
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make -C build DESTDIR="${pkgdir}" install
  install -Dm 644 "${pkgdir}/usr/etc/umurmur.conf" -t "${pkgdir}/etc/umurmur"
  rm -r "${pkgdir}/usr/etc"
  install -Dm 644 "${srcdir}/umurmur.service" -t "${pkgdir}/usr/lib/systemd/system"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
