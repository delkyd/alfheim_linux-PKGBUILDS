# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Johnathan Jenkins <twodopeshaggy@gmail.com>
# Contributor: prettyvanilla <prettyvanilla@posteo.at>
# Contributor: almostalive <almostalive2003@gmail.com>

pkgname=libretro-desmume
pkgver=5301
pkgrel=1
pkgdesc='Nintendo DS core'
arch=('i686' 'x86_64')
url='https://github.com/libretro/desmume'
license=('GPL2')
groups=('libretro')
depends=('gcc-libs' 'glibc')
makedepends=('git')
_commit='62786c58933b680e4ce211e92e4ea33f2caa2418'
source=("libretro-desmume::git+https://github.com/libretro/desmume.git#commit=${_commit}"
        'https://raw.githubusercontent.com/libretro/libretro-super/master/dist/info/desmume_libretro.info')
sha256sums=('SKIP'
            'a851e34cacb4714006d74f722ec67a0f252e900d60afdb74d4f75dd7461fbaa8')

pkgver() {
  cd libretro-desmume

  git rev-list --count HEAD
}

build() {
  cd libretro-desmume/desmume

  make -f Makefile.libretro
}

package() {
  cd libretro-desmume/desmume

  install -dm 755 "${pkgdir}"/usr/{lib/libretro,share/libretro/info}
  install -m 644 desmume_libretro.so "${pkgdir}"/usr/lib/libretro/
  install -m 644 ../../desmume_libretro.info "${pkgdir}"/usr/share/libretro/info/
}

# vim: ts=2 sw=2 et:
