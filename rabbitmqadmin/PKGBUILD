# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Dmitry Chusovitin <dchusovitin@gmail.com>

pkgname=rabbitmqadmin
pkgver=3.6.12
pkgrel=1
pkgdesc='Command-line tool for managing RabbitMQ server (requires "management" plugin)'
url='https://www.rabbitmq.com/management-cli.html'
arch=('any')
license=('MPL')
depends=('python')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/rabbitmq/rabbitmq-management/archive/rabbitmq_v${pkgver//./_}.tar.gz)
sha256sums=('8f75bf2d5add21b63a2d9dd3fd7dd6522d21981b75cd2f277ee24bdb9d22dfc7')
sha512sums=('4dbfae7eee1461450aae5654a805638ff497df37ce65c2a7af222b2c6431ae32e8f70425df71c31d52779cc14f0319c7168bfbb610b27f2de262658be5a3138d')

prepare() {
  cd rabbitmq-management-rabbitmq_v${pkgver//./_}
  sed -e "s|%%VSN%%|${pkgver}|" -i bin/rabbitmqadmin
}

package() {
  cd rabbitmq-management-rabbitmq_v${pkgver//./_}
  install -Dm 755 bin/rabbitmqadmin -t "${pkgdir}/usr/bin"
  install -Dm 644 LICENSE-MPL-RabbitMQ "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
