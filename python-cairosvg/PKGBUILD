# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: razer <razer[at]neuf[dot]fr>
# Contributor: pzl <alsoelp@gmail.com>
# Contributor: Dennis Fink <dennis.fink@c3l.lu>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>

pkgname=python-cairosvg
pkgver=2.1.1
pkgrel=1
pkgdesc="A Simple SVG Converter for Cairo"
arch=('any')
url="http://cairosvg.org/"
license=('LGPL3')
depends=('python-cairocffi' 'python-cssselect2' 'python-defusedxml' 'python-pillow'
         'python-tinycss2')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-pytest-isort'
              'python2-pytest-isort' 'python-pytest-flake8' 'python2-pytest-flake8'
              'python-pytest-cov' 'python2-pytest-cov')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Kozea/CairoSVG/archive/$pkgver.tar.gz")
sha512sums=('ca70cfc43c5fc1e57ef07074f03ca6606a86e6fca2322fcd684c70fa977169196be30fd16f1cd656183fd69e73f8063ecbd244b00a85f6f3fe22c1496708f4b4')

prepare() {
  cd CairoSVG-$pkgver
  rmdir test/cairosvg_reference
  cp -a "$srcdir"/CairoSVG-$pkgver{,-copy}
  mv "$srcdir"/CairoSVG-$pkgver-copy test/cairosvg_reference
}

build() {
  cd CairoSVG-$pkgver
  python setup.py build
}

check() {
  cd CairoSVG-$pkgver
  python setup.py test
}

package() {
  cd CairoSVG-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
