# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=gpgme
pkgname=ruby-gpgme
pkgver=2.0.13
pkgrel=1
pkgdesc='Ruby interface to GnuPG Made Easy (GPGME)'
url='https://github.com/ueno/ruby-gpgme'
arch=('i686' 'x86_64')
license=('GPL2')
depends=('ruby' 'ruby-mini_portile2')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/ueno/ruby-gpgme/archive/${pkgver}.tar.gz)
sha256sums=('e037d9969aa5eefe14e2ad53fd8f6a1fbce1f85409135139a64aae45f89d74f3')
sha512sums=('f53ed7fce40cecdae07d53a2b57115076c5b2d399ecd99d2a3539933af18fc613d0fa83456fe0d427f33a1d069b7e999d0748fbc1f1f8b6d30ce229da431333b')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec # don't give a fuck about rubys bla bla
}

build() {
  cd ${pkgname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${pkgname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README.rdoc NEWS -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 examples/* -t "${pkgdir}/usr/share/doc/${pkgname}/examples"

  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
  find "${pkgdir}" \( -name gem_make.out -o -name mkmf.log \) -delete
}

# vim: ts=2 sw=2 et:
