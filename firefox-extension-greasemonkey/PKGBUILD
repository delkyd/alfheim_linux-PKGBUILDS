# Maintainer: Marcs <aur (at) emarcs [.] org>
# Contributor: OmeGa <omega [U+0040] mailoo [.] org>
# Contributor: Vinycius Maia <suportevg@uol.com.br>

pkgname=firefox-extension-greasemonkey
_file=402242
pkgver=3.12
pkgrel=1
pkgdesc="Customize the way a web page displays or behaves, by using small bits of JavaScript."
arch=('any')
url="http://www.greasespot.net/"
license=('MPL')
depends=('firefox>=38.0')
source=("https://addons.mozilla.org/firefox/downloads/file/$_file/greasemonkey-$pkgver.xpi")
sha1sums=('d1a43b10e9ffadaf95d0558bb57dd467758e6de2')

package() {
  cd ${srcdir}
  local _emid=$(sed -n '/.*<em:id>\(.*\)<\/em:id>.*/{s//\1/p;q}' install.rdf) || return 1
  test ! -z "${_emid}"
  local _file=(*.xpi)
  test "${#_file[@]}" -eq 1
  install -Dpm644 "${_file}" "${pkgdir}/usr/lib/firefox/browser/extensions/${_emid}.xpi"
}

# vim:set ts=2 sw=2 et:
