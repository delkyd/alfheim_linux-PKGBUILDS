# Maintainer: Frederic Bezies <fredbezies at gmail dot com>
# Contributor rtfreedman (rob<d0t>til<d0t>freedman<aT>gmail<d0t>com
# 

pkgname=easytag-git
_gitname=easytag
pkgver=2.4.3.r55.g6e3a7e9
pkgrel=1
pkgdesc="Utility for viewing and editing tags for most audio formats - git version"
arch=('i686' 'x86_64')
license=('GPL')
url='http://projects.gnome.org/easytag/'
depends=('id3lib' 'taglib' 'libid3tag' 'gtk3' 'libvorbis' 'flac' 'speex' 'opusfile' 'wavpack' 'desktop-file-utils' 'yelp')
makedepends=('git' 'intltool' 'appdata-tools' 'autoconf-archive' 'yelp-tools')
## depends on building gnome help files

provides=('easytag')
conflicts=('easytag')
source=("git://git.gnome.org/easytag")
sha1sums=('SKIP')

pkgver() {
  cd "$_gitname"
  git describe --tags | sed 's/^easytag-//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$_gitname"
  sed "s/(\[EasyTAG\], \[\([0-9]\).\([0-9]\).\([0-9]\)\],/(\[EasyTAG\], [\1.\2.\3-$(git rev-list --count master).$(git rev-parse --short master)], /" -i configure.ac
  ./autogen.sh
}

build() {
  cd "$_gitname"
  CXXFLAGS="$CXXFLAGS -Wno-error=deprecated-declarations -Wno-error"
  # Workaround for gcc 7 
  CFLAGS="$CFLAGS -Wno-error"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$_gitname"
  make DESTDIR="${pkgdir}" install
}
