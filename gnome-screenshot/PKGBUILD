# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-screenshot
pkgver=3.25.0+48+g7812e98
pkgrel=1
pkgdesc="Take pictures of your screen"
url="https://git.gnome.org/browse/gnome-screenshot"
arch=(i686 x86_64)
license=(GPL2)
depends=(dconf gtk3 libcanberra)
makedepends=(git meson appstream-glib)
groups=(gnome)
_commit=7812e98a39e142167f427753f22c436b22eb6847  # gnome-3-26
source=("git+https://git.gnome.org/browse/gnome-screenshot#commit=$_commit")
sha256sums=('SKIP')

check() {
  cd build
  meson test
}

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  mkdir build
  cd $pkgname
}

build() {
  cd build
  meson setup --prefix=/usr --buildtype=release ../$pkgname
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}

# vim:set ts=2 sw=2 et:
