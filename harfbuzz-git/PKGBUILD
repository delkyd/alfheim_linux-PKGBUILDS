# Maintainer: Eric Bailey <nerflad@gmail.com>
# Contributor: Kamran Mackey <kamranm1200@gmail.com>
pkgbase=(harfbuzz-git)
pkgname=(harfbuzz-git harfbuzz-icu-git)
pkgver=1.4.5.r11.g7d64c0ef
pkgrel=1
pkgdesc="OpenType text shaping engine"
arch=(i686 x86_64)
url="http://www.freedesktop.org/wiki/Software/HarfBuzz"
license=(MIT)
makedepends=(ragel python2 glib2 freetype2 graphite cairo icu gobject-introspection)
source=(git://github.com/behdad/harfbuzz.git)
sha256sums=('SKIP')
_gitname=harfbuzz

pkgver() {
  cd "$_gitname"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  mkdir path
  ln -s /usr/bin/python2 path/python

  cd "$_gitname"
}


build() {
  cd "$_gitname"
  ./autogen.sh
  ./configure --prefix=/usr \
    --with-glib --with-freetype --with-cairo --with-icu --with-graphite2 \
    --with-gobject
  make
}

check() {
  cd "$_gitname"
  PATH="$srcdir/path:$PATH" make -k check || :
}

package_harfbuzz-git() {
  depends=(glib2 freetype2 graphite)
  optdepends=('cairo: hb-view program')
  conflicts=(harfbuzz)
  provides=(harfbuzz)

  cd "$_gitname"
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/harfbuzz/COPYING"

  # Split harfbuzz-icu
  mkdir -p ../hb-icu/usr/{include/harfbuzz,lib/pkgconfig}; cd ../hb-icu
  mv "$pkgdir"/usr/lib/libharfbuzz-icu* ./usr/lib
  mv "$pkgdir"/usr/lib/pkgconfig/harfbuzz-icu.pc ./usr/lib/pkgconfig
  mv "$pkgdir"/usr/include/harfbuzz/hb-icu.h ./usr/include/harfbuzz
}

package_harfbuzz-icu-git(){
  pkgdesc="$pkgdesc (ICU integration)"
  depends=(harfbuzz icu)
  conflicts=(harfbuzz-icu)
  provides=(harfbuzz-icu)

  mv hb-icu/* "$pkgdir"

  install -Dm644 $_gitname/COPYING "$pkgdir/usr/share/licenses/harfbuzz-icu/COPYING"
}
