# Maintainer: Vianney le Clément <vleclement AT gmail·com>
_pkgname=jbig2enc
pkgname=$_pkgname-git
pkgver=0.28.9.g0693dcd
pkgrel=1
pkgdesc="A JBIG2 image encoder"
arch=('i686' 'x86_64')
url="https://github.com/agl/jbig2enc"
license=('Apache')
depends=('gcc-libs' 'leptonica>=1.68' 'libpng' 'libjpeg' 'libtiff')
optdepends=('python2: for pdf.py')
makedepends=('git')
source=("git://github.com/agl/$_pkgname.git")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  git describe | sed 's/-/./g'
}

prepare() {
  cd "$srcdir/$_pkgname"
  sed -i 's@^#!/usr/bin/python$@#!/usr/bin/env python2@' pdf.py
}

build() {
  cd "$srcdir/$_pkgname"
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$_pkgname"
  make install DESTDIR="$pkgdir"
}
