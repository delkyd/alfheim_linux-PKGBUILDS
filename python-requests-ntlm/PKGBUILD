# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Christoph W <c w e g e n e r at gmail dot com>
# Contributor: Gary van der Merwe <g a r y v d m at gmail dot com>

pkgbase=('python-requests-ntlm')
pkgname=('python-requests-ntlm' 'python2-requests-ntlm')
_module='requests_ntlm'
pkgver='1.0.0'
pkgrel=3
pkgdesc="This package allows for HTTP NTLM authentication using the requests library."
url="https://github.com/requests/requests-ntlm"
makedepends=('python-setuptools' 'python2-setuptools')
license=('custom:ISC License (ISCL)')
arch=('any')
source=("https://files.pythonhosted.org/packages/source/r/requests_ntlm/requests_ntlm-${pkgver}.tar.gz")
sha512sums=('bdba9f8c241dbe8e55be28208e240dd44a62156e5ee65d69897b0d427c7905430f6ba3582d37f5e8d6fc50e3c9f63814333c6c654d4e51ab9b9a553f53fe3dc5')

prepare() {
    cp -a "${srcdir}/${_module}-${pkgver}"{,-python2}
}

build() {
    cd "${srcdir}/${_module}-${pkgver}"
    python setup.py build

    cd "${srcdir}/${_module}-${pkgver}-python2"
    python2 setup.py build
}

package_python-requests-ntlm() {
    depends+=('python' 'python-requests' 'python-ntlm-auth')
    cd "${srcdir}/${_module}-${pkgver}"
    install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/python-requests-ntlm/LICENSE"
    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

package_python2-requests-ntlm() {
    depends+=('python2' 'python2-requests' 'python2-ntlm-auth')
    cd "${srcdir}/${_module}-${pkgver}-python2"
    install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/python2-requests-ntlm/LICENSE"
    python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
