# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libxfont
pkgver=1.5.2
pkgrel=1
pkgdesc="X11 font rasterisation library"
arch=(i686 x86_64)
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('freetype2' 'libfontenc' 'xproto' 'fontsproto>=2.1.3')
makedepends=('xorg-util-macros' 'xtrans')
source=(${url}/archive/individual/lib/libXfont-${pkgver}.tar.bz2{,.sig})
sha256sums=('02945ea68da447102f3e6c2b896c1d2061fd115de99404facc2aca3ad7010d71'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alan.coopersmith@oracle.com>
validpgpkeys+=('DD38563A8A8224537D1F90E45B8A2D50A0ECD0D3') # Adam Jackson <ajax@benzedrine.nwnk.net>

build() {
  cd "${srcdir}/libXfont-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd "${srcdir}/libXfont-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
