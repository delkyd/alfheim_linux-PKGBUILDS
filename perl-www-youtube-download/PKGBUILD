# Mantainer: 3ED <krzysztof1987 at gmail dot com>

pkgname=perl-www-youtube-download
_lastauthor=O/OA/OALDERS
_pkgname=WWW-YouTube-Download
pkgver=0.59
pkgrel=1
pkgdesc="Very simple YouTube video download interface"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-html-parser' 'perl-json' 'perl-libwww' 'perl-uri' 'perl-xml-treepp')
makedepends=('perl-module-build-tiny')
url="https://metacpan.org/release/${_pkgname}"
source=(https://cpan.metacpan.org/authors/id/${_lastauthor}/${_pkgname}-${pkgver}.tar.gz)
sha256sums=('0176d0e7320b15e5450db7dc3526e4d7d482f9cebd6f3d5a9afbf150f76d6512')

build() {
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL="--skipdeps" \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  cd "${srcdir}/${_pkgname}-${pkgver}"
  perl Build.PL
  perl Build
}
check() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  perl Build test
}
package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  perl Build install
}
