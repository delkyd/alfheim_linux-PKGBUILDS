# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libkdepim
pkgver=17.08.1
pkgrel=1
pkgdesc="Libraries for KDE PIM applications"
arch=(i686 x86_64)
url="https://community.kde.org/KDE_PIM"
license=(LGPL)
depends=(akonadi-contacts akonadi-search kldap)
makedepends=(extra-cmake-modules kdoctools python boost qt5-tools kdesignerplugin)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('007728d902f68c35c8b4b31af2679d7d0895e0f40ffbcf1d01723676b943ef9e'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
