# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=menu-cache
pkgver=1.0.2
pkgrel=2
pkgdesc='Caching mechanism for freedesktop.org compliant menus'
arch=('i686' 'x86_64')
license=('GPL2')
url='http://lxde.org/'
depends=('libfm-extra')
makedepends=('gtk-doc')
source=(https://downloads.sourceforge.net/lxde/$pkgname-$pkgver.tar.xz
        Fix-CVE-2017-8933.patch)
sha256sums=('6f83edf2de34f83e701dcb52145d755250a5677580cd413476cc4d7f2d2012d5'
            'd0ee65717d07ae423bac48d0676989cec624674bdacd3619f5d9fcc64c9e5796')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../Fix-CVE-2017-8933.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --enable-gtk-doc
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
