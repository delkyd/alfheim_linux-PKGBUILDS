# $Id$
# Maintainer:  speps <speps at aur dot archlinux dot org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Marcel Wysocki <maci@satgnu.net>
# Contributor: coolkehon <coolkehon at g m a i l>

pkgname=libircclient
pkgver=1.9
pkgrel=2
pkgdesc='Small but powerful library, which implements client-server IRC protocol'
arch=('i686' 'x86_64')
url='http://www.ulduzsoft.com/libircclient/'
license=('GPL')
depends=('openssl')
makedepends=('python2-sphinx' 'python2-rst2pdf') 
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('783c48fe9153ed55a5565c818a178d67')

prepare() {
  cd $pkgname-$pkgver/src

  # fix include dir path
  sed -i "s/@\/include/&\/$pkgname/" Makefile.in
  # support openssl 1.1
  cd ..
  sed -e 's|SSL_library_init|OPENSSL_init_ssl|' -i configure.in
  autoreconf -vi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
              --libdir=/usr/lib \
              --enable-shared \
              --enable-openssl \
              --enable-ipv6
  cd src && make

  cd ../doc
  make SPHINXBUILD=sphinx-build2 singlehtml man
}

package() {
  cd $pkgname-$pkgver/src
  make DESTDIR="$pkgdir" install

  # docs
  install -d "$pkgdir/usr/share/doc/$pkgname"
  cp -a ../doc/_build/singlehtml/* "$pkgdir/usr/share/doc/$pkgname"

  # man
  install -Dm644 ../doc/_build/man/$pkgname.1 \
    "$pkgdir/usr/share/man/man1/$pkgname.1"

  # examples
  install -d "$pkgdir/usr/share/$pkgname/examples"
  install -Dm644 ../examples/* \
    "$pkgdir"/usr/share/$pkgname/examples
}
