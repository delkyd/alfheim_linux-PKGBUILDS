# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=vdrift-data
_pkgname=vdrift
pkgver=2014.10.20
pkgrel=2
epoch=2
pkgdesc="An open source driving simulation made with drift racing in mind (data files)"
arch=('any')
url="http://vdrift.net/"
license=('GPL')
makedepends=('scons' 'bullet' 'mesa' 'sdl2' 'sdl2_image' 'libvorbis')
source=(http://downloads.sourceforge.net/sourceforge/$_pkgname/$_pkgname-${pkgver//./-}.tar.bz2
        vdrift-gcc6.patch)
sha512sums=('5f6afbb4bdc273455f6bf4e3eba6e3b702d96fbe3c7b5068cee0c114d5de28b82ceed44ee891f59e9bfe194f0c597f464a13aadf7f34b204630ec54df282e4ee'
            '4f77497aa3d1b9299028fa58fec2a42ca47b2dd5f132581d408bad7b7ca77b76f2b6c5fb89b4ac4e72b546c4e993addf55445c49b3f5f00a75bace0dd12b9753')

prepare() {
  cd vdrift
  # Fix build with GCC 6 (Fedora)
  patch -p1 -i ../vdrift-gcc6.patch
}

build() {
  cd vdrift

  # build and install
  scons $MAKEFLAGS \
    "destdir"="$pkgdir" \
    "release"=1 \
    "force_feedback"=1 \
    "prefix"=/usr \
    "datadir"=share/$_pkgname/ \
    "extbullet"=1
}

package() {
  cd vdrift

  scons install

  rm -r "$pkgdir/usr/bin"
}
# vim: sw=2:ts=2 et:
