# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-tldextract
pkgver=2.1.0
pkgrel=3
pkgdesc="Accurately separate the TLD from the registered domain and subdomains of a URL, using the Public Suffix List"
arch=('any')
url="https://github.com/john-kurkowski/tldextract"
license=('BSD')
depends=('python-idna' 'python-requests' 'python-requests-file' 'python-setuptools')
checkdepends=('python-pytest-runner' 'python-pytest-mock')
source=("https://pypi.io/packages/source/t/tldextract/tldextract-$pkgver.tar.gz")
sha512sums=('484141e8af67f5a5df608d5d3da972f25d73a5a28a88ca4ba75dac5dc63614efe2b259629893d316617126d230b6c7acdf618b2c73e561454965e6fb748c3b9d')

prepare() {
  export LC_CTYPE=en_US.UTF-8
}

build() {
  cd tldextract-$pkgver
  python setup.py build
}

check() {
  cd tldextract-$pkgver
  python setup.py pytest
}

package() {
  cd tldextract-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  # use the snapshot version, because generating a new one requires Internet access and root permission
  ln -s .tld_set_snapshot "$pkgdir"/usr/lib/python3.6/site-packages/tldextract/.tld_set
}

# vim:set sw=2 et:
