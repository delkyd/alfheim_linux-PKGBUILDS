# Maintainer: Guillaume Hayot <ghayot@postblue.info>
# Thanks to: SÃ½lvan Heuser <sylvan.heuser@gmx.net>

pkgname=libpurple-carbons-git
_pkgname=carbons
pkgver=r16.35dd48d
pkgrel=1
pkgdesc='Experimental XEP-0280: Message Carbons plugin for libpurple (Pidgin, Finch, etc.)'
arch=('i686' 'x86_64')
url="https://github.com/gkdr/carbons"
license=('GPL')
depends=('libpurple' 'glib2' 'libxml2')
makedepends=('git')
source=("$_pkgname::git+https://github.com/gkdr/carbons.git")
sha256sums=('SKIP')

pkgver () {
  cd "$srcdir/$_pkgname"
  ( set -o pipefail
    git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

prepare() {
  cd "$srcdir/$_pkgname"
  msg2 "Getting submodules"
  git submodule update --init --recursive
}

build() {
  cd "$srcdir/$_pkgname"
  make
}

package() {
  cd "$srcdir/$_pkgname"
  env PURPLE_PLUGIN_DIR="$pkgdir/usr/lib/purple-2" make -e install
}

# vim:set ts=2 sw=2 et:
