# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Guillermo Ramos <0xwille at gmail dot com>

pkgname=radare2
pkgver=1.6.0
pkgrel=1
pkgdesc='Open-source tools to disasm, debug, analyze and manipulate binary files'
url='https://radare.org'
arch=('i686' 'x86_64')
license=('GPL3' 'LGPL3')
depends=('sh' 'capstone' 'openssl' 'libzip')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/radare/radare2/archive/${pkgver}.tar.gz)
sha256sums=('759d1c65dcd69d1189fc73e427c568ec234a7ca1958c19f5001c255dd31a3787')
sha512sums=('6e1485c4edaa007790c07a8cfe190c4ea3e4df843620e2575587478e60f1be3dacaa0c545c84e135a51d9f7aff61c27f712c0ffa6038b556d347151f2a3cd2bb')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --with-syscapstone \
    --with-syszip \
    --with-openssl
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  # remove shit
  rm "${pkgdir}"/usr/bin/r2-{indent,docker} \
    "${pkgdir}/usr/share/man/man1/r2-docker.1"
}

# vim: ts=2 sw=2 et:
