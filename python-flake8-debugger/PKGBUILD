# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-flake8-debugger
pkgname=('python-flake8-debugger' 'python2-flake8-debugger')
pkgver=1.4.0
pkgrel=1
pkgdesc='ipdb/pdb statement checker plugin for flake8'
arch=('any')
license=('MIT')
url='https://github.com/JBKahn/flake8-debugger'
makedepends=('python-setuptools' 'python2-setuptools' 'flake8' 'python2-flake8' 'git')
checkdepends=('python-nose' 'python2-nose')
source=("git+https://github.com/JBKahn/flake8-debugger.git#tag=$pkgver")
md5sums=('SKIP')

prepare() {
  cp -a flake8-debugger{,-py2}
}

build() {
  cd "$srcdir"/flake8-debugger
  python setup.py build

  cd "$srcdir"/flake8-debugger-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/flake8-debugger
  python setup.py test

  cd "$srcdir"/flake8-debugger-py2
  python2 setup.py test
}

package_python-flake8-debugger() {
  depends=('flake8')

  cd "$srcdir"/flake8-debugger
  python setup.py install --root="$pkgdir"/ --optimize=1
}

package_python2-flake8-debugger() {
  depends=('python2-flake8')

  cd "$srcdir"/flake8-debugger-py2
  python2 setup.py install --root="$pkgdir"/ --optimize=1
}

# vim:set ts=2 sw=2 et:
