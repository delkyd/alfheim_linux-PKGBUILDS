# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=logstash
pkgver=5.6.2
pkgrel=1
pkgdesc='Tool for managing events and logs'
url='https://www.elastic.co/products/logstash/'
arch=('i686' 'x86_64')
license=('Apache')
depends=('java-runtime-headless>=8' 'ruby' 'ruby-bundler')
# TODO: switch to up-to-date rake when 6.x arrives
makedepends=('java-environment>=8' 'gradle' 'git' 'ruby2.3')
backup=('etc/conf.d/logstash'
        'etc/logstash/logstash.yml')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/elastic/logstash/archive/v${pkgver}.tar.gz
        logstash.service
        logstash@.service
        logstash-sysuser.conf
        logstash-tmpfile.conf
        bundle.config)
sha256sums=('89c3e44ed21fc0fd93a1a70fe92b29043d947e64b85328ed915346db82ba8647'
            '2b8b29297202334c784fdd7f2eb8d7e776c24c783b3c9f8387b500ab0039335c'
            'a01ea29d4f53d785f6eb926ebfe445e64ed5b3dab5d0418848589dd79502d876'
            '18a68a59ddb0ce19778e83b65e68dd568d65b7180bf45b4cf298fb332d69eb26'
            'b0c334b9f6f5450d0d6e801dbe43972bf32ead92705a389df891a32aa803a55f'
            'fe05315345e4489458c3eecac43726800109c1e390e74a14584096f6c041fee1')
sha512sums=('52b674173e07236f932635eadaf094451ab90182096a2c628989868aa291c4acd03d6bec8a2681396a0ecbffe5200118d7b906f7fb1c867bd1aa569c8dac91c9'
            '817097565519dc7c5eac7521339947c74c6148683ca594356dd2ceb3274a1e94f8e7318ce310e0fe5789d7ab0d4c23404f814bef31036a11ddfec08d16814c69'
            'ce2cef4a784845b00d7c867273555811450bc459669abb5be944bfbbb02708129983e45376a9b308d6db22b2c7b4a7a212827a4826f2a27bc7e143cebc9abfe0'
            'd811dc3b18d0032b79b4669c9f6aefca49963897c309d83cbf87616c7b8cb5944c17c8072980bcd115d0fb57ef1624d98259ff1082d402d308c33e766ee89699'
            '6b67bc195da2b137d63de9ee79033e0a0f56a822b6f620827d3ee9dcf7f1174cff2716f27dc055580dcda345a5c876ea551444858b2a8fa6c9e4df7d551cdb6b'
            '5091aa34cc31acd50fd2865714080cf6e67c2d437424f27e001bb409c2885a0e82eabe4ce17461d60c181f460a16df0a03d53bac6015fb731b3e5225735fd4da')

prepare() {
  cd ${pkgname}-${pkgver}
  rm bin/*.bat
	sed -e 's|LS_SETTINGS_DIR=.*|LS_SETTINGS_DIR="/etc/logstash"|' -i config/startup.options
  sed -e '1i [ -f /etc/profile.d/jre.sh ] && . /etc/profile.d/jre.sh' -i bin/logstash.lib.sh
}

build() {
  cd ${pkgname}-${pkgver}
  msg "Rake bootstrap..."
  /opt/ruby2.3/bin/rake bootstrap
  msg "Rake plugin:install-default..."
  /opt/ruby2.3/bin/rake plugin:install-default

  rm -r vendor/_ build ci logstash-core/{build,src,spec} qa pkg spec rakelib tools vendor/bundle/jruby/*/cache .gradle
  cd vendor/jruby/lib/jni
  rm -r *Darwin *Windows *SunOS *FreeBSD arm-* ppc64*
}

package() {
  cd ${pkgname}-${pkgver}

  install -dm 755 "${pkgdir}"/usr/share "${pkgdir}"/etc/conf.d
  mv config/startup.options "${pkgdir}/etc/conf.d/logstash"
  mv config "${pkgdir}/etc/logstash"
  chmod 750 "${pkgdir}/etc/logstash"
  cp -a . "${pkgdir}/usr/share/logstash"

  install -Dm 644 "${srcdir}"/{logstash.service,logstash@.service} -t "${pkgdir}/usr/lib/systemd/system"
	install -Dm 644 "${srcdir}/logstash-sysuser.conf" "${pkgdir}/usr/lib/sysusers.d/logstash.conf"
	install -Dm 644 "${srcdir}/logstash-tmpfile.conf" "${pkgdir}/usr/lib/tmpfiles.d/logstash.conf"
  install -Dm 644 "${srcdir}/bundle.config" "${pkgdir}/usr/share/logstash/.bundle/config"

  install -dm 755 "${pkgdir}/var/lib/logstash"
  install -dm 755 "${pkgdir}/var/log/logstash"
  install -dm 755 "${pkgdir}/etc/logstash/conf.d"
}

# vim: ts=2 sw=2 et:
