# $Id$
# Maintainer:
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Cilyan Olowen <gaknar@gmail.com>
# Contributor: fill <mailbox@fillo.ru>
# Contributor: Anton Shestakov <engored*ya.ru>

pkgname=flashrom
pkgver=0.9.9
pkgrel=2
pkgdesc="Utility for reading, writing, erasing and verifying flash ROM chips"
url="http://www.flashrom.org/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libftdi-compat' 'pciutils')
optdepends=('dmidecode: for SMBIOS/DMI table decoder support')
source=("http://download.flashrom.org/releases/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('cb3156b0f63eb192024b76c0814135930297aac41f80761a5d293de769783c45')

prepare() {
cd $pkgname-$pkgver

  sed -i "s:sbin:bin:g" Makefile
}

build() {
  cd $pkgname-$pkgver

  make
}

package() {
  cd $pkgname-$pkgver

  make PREFIX="$pkgdir"/usr install
}
