# Maintainer: Josh Hoffer < hoffer dot joshua at gmail dot com > 

pkgname=python-bempp-git
_pkgname=bempp
pkgdesc='The BEM++ boundary element library'
pkgver=v3.1.5.r27.gdb2c636b
pkgrel=1
epoch=1
arch=('i686' 'x86_64')
provides=('python-bempp')
url="https://bempp.com/"
license=(MIT)
options=('!strip')
depends=('python-scipy' 'python-mpi4py')
makedepends=('dune-common' 'dune-geometry' 'doxygen' 'intel-tbb' 'dune-uggrid' 'cython>=0.22')
optdepends=('ipython: interactive console'
	    'gmsh: external plots'
)
source=('git+https://bitbucket.org/bemppsolutions/bempp.git')
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	cd "$srcdir"/"$_pkgname"
	python setup.py build
}

package() {
	cd "$srcdir"/"$_pkgname"
	python setup.py install --skip-build -O1 --root="$pkgdir"

	cd "$pkgdir"
	install -m0644 -D "$srcdir"/$_pkgname/LICENSE usr/share/licenses/$pkgname/LICENSE
	chmod -R a+r usr
}

