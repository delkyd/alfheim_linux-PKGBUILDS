# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pyte
pkgname=('python-pyte' 'python2-pyte')
pkgver=0.6.0
pkgrel=1
pkgdesc='Simple VTXXX-compatible terminal emulator'
arch=('any')
license=('LGPL3')
url='https://github.com/selectel/pyte'
makedepends=('python-setuptools' 'python2-setuptools' 'python-wcwidth' 'python2-wcwidth')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/selectel/pyte/archive/$pkgver.tar.gz")
sha512sums=('64de04ff4f1c2996855c18a6b4e94b8e7116106f39a376ba29b1819c4655bff14ab2d591a9694d6e44b3cb789bafb9069e6e1ed35b69c79744101b2548c51168')

prepare() {
  cp -a pyte-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/pyte-$pkgver
  python setup.py build

  cd "$srcdir"/pyte-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/pyte-$pkgver
  python setup.py pytest

  cd "$srcdir"/pyte-$pkgver-py2
  python2 setup.py pytest
}

package_python-pyte() {
  depends=('python-wcwidth')

  cd pyte-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-pyte() {
  depends=('python2-wcwidth')

  cd pyte-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
