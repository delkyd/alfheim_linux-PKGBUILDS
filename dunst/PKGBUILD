# $Id$
# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: feuri

pkgname=dunst
pkgver=1.2.0
pkgrel=1

pkgdesc="Customizable and lightweight notification-daemon"
url="https://dunst-project.org/"
arch=('i686' 'x86_64')
license=('BSD')

depends=('libxinerama' 'libxss' 'pango' 'libnotify' 'gtk2' 'libxdg-basedir')

provides=('notification-daemon')

source=("dunst-$pkgver.tar.gz::https://github.com/dunst-project/dunst/archive/v$pkgver.tar.gz")
md5sums=('425d5bb10a8b571c7494c5e60aaae7e1')

build() {
	make -C "$srcdir/$pkgname-$pkgver" X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
	make -C "$pkgname-$pkgver" DESTDIR="${pkgdir}" PREFIX=/usr install
	install -Dm644 "${srcdir}/$pkgname-$pkgver/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
