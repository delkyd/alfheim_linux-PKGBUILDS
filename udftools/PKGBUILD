# $Id$
# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: lucck <lucck@ep.com.pl>
# Contributor: aksr <aksr at t-com dot me>

pkgname=udftools
pkgver=1.3
pkgrel=2
pkgdesc="Linux tools for UDF filesystems and DVD/CD-R(W) drives"
url="https://github.com/pali/udftools"
arch=('i686' 'x86_64')
license=(GPL)
depends=('ncurses' 'readline')
options=(!libtool)
validpgpkeys=('B856B21074A8AE9B692B80858BF0C93D03E44352') # Pali Roh√°r <pali.rohar@gmail.com>
source=("https://github.com/pali/$pkgname/releases/download/$pkgver/$pkgname-${pkgver}.tar.gz"{,.asc})
sha256sums=('00562a440de7b855df8127f8f798df657d53f20d9a205a7041fed37c8a07d4cb'
            'SKIP')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --sbindir=/usr/bin
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  mkdir -p $pkgdir/usr/share/licenses/$pkgname
  mv $pkgdir/usr/share/doc/$pkgname/COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
}

