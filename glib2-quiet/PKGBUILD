# $Id$
# Original Maintainer: Jan de Groot <jgc@archlinux.org>
# Fork Maintainer: epitron <chris@ill-logic.com>

pkgname=glib2-quiet
pkgver=2.50.1
pkgrel=1
pkgdesc="A patched version of glib2, with spammy console WARNING messages disabled"
url="http://www.gtk.org/"
arch=(i686 x86_64)
provides=('glib2')
conflicts=('glib2')
depends=(pcre libffi)
optdepends=('python: for gdbus-codegen and gtester-report'
              'libelf: gresource inspection tool')
options=(!docs !emptydirs)
license=(LGPL)

makedepends=(gettext gtk-doc libffi pcre zlib shared-mime-info python libelf git)
_commit=e44ea516afeb41d22cebf968b3ea5d9543856df2  # tags/2.50.1^0
source=("git://git.gnome.org/glib#commit=$_commit"
        glib-compile-schemas.hook
        gio-querymodules.hook
        revert-warn-glib-compile-schemas.patch
        gmessages-nowarn.patch)
sha256sums=('SKIP'
            'e1123a5d85d2445faac33f6dae1085fdd620d83279a4e130a83fe38db52b62b3'
            '5ba204a2686304b1454d401a39a9d27d09dd25e4529664e3fd565be3d439f8b6'
            '049240975cd2f1c88fbe7deb28af14d4ec7d2640495f7ca8980d873bb710cc97'
            '3c5d812cd1e8905f89de14b0d5272e733c612a8c47bd648da97a3545a727044a')

pkgver() {
  cd glib
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd glib
  patch -Rp1 -i ../revert-warn-glib-compile-schemas.patch
  patch -p1 -i ../gmessages-nowarn.patch
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd glib
  ./configure --prefix=/usr --libdir=/usr/lib \
      --sysconfdir=/etc \
      --with-pcre=system \
      --disable-fam
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

# checkdepends=(desktop-file-utils dbus)
# check() {
#   cd glib
#   if ! make check; then
#     # Rounding error in timer tests?
#     # GLib:ERROR:timer.c:38:test_timer_basic: assertion failed (micros == ((guint64)(elapsed * 1e6)) % 1000000): (1 == 0)
#     make check
#   fi
# }

package() {
  cd glib
  make completiondir=/usr/share/bash-completion/completions DESTDIR="$pkgdir" install

  chmod -x "$pkgdir"/usr/share/bash-completion/completions/*

  # install hooks
  install -d "$pkgdir/usr/share/libalpm/hooks/"
  install -m644 "$srcdir"/{glib-compile-schemas,gio-querymodules}.hook \
    "$pkgdir/usr/share/libalpm/hooks/"
}
