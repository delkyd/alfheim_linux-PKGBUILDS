# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Douglas Soares de Andrade
# Contributor: Roberto Alsina <ralsina@kde.org>

_pkgname=mechanize
pkgbase=python-mechanize
pkgname=python2-mechanize
pkgver=0.3.5
pkgrel=1
epoch=1
pkgdesc='Stateful programmatic web browsing in Python'
arch=('any')
url="https://github.com/python-mechanize/mechanize"
license=('BSD')
depends=('python2' 'python2-html5lib')
makedepends=('python2-distribute')
checkdepends=('python2-zope-interface' 'python2-twisted')
source=("https://pypi.io/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz"{,.asc})
sha256sums=('d4f999b5a3028c900cb0e6ad3c996c73e3c69a664e6575493258104fa84d7166'
            'SKIP')
validpgpkeys=('3CE1780F78DD88DF45194FD706BC317B515ACE7C')

package_python2-mechanize() {
  cd "mechanize-$pkgver"

  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

check() {
  cd "mechanize-$pkgver"
  python2 run_tests.py
}

# vim:set ts=2 sw=2 et:
