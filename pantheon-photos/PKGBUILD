# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-photos
pkgver=0.2.2
pkgrel=1
pkgdesc='The Pantheon Photos Manager'
arch=('i686' 'x86_64')
url='https://launchpad.net/pantheon-photos'
license=('GPL3')
depends=('cairo' 'gdk-pixbuf2' 'geocode-glib' 'glib2' 'glibc'
         'gst-plugins-base-libs' 'gstreamer' 'gtk3' 'json-glib' 'libexif'
         'libgee' 'libgexiv2' 'libgphoto2' 'libraw' 'libsoup' 'libxml2' 'pango'
         'rest' 'sqlite'
         'libgranite.so' 'libgudev-1.0.so')
makedepends=('cmake' 'intltool' 'vala' 'webkit2gtk')
optdepends=('webkit2gtk: Publishing plugins')
source=("https://launchpad.net/pantheon-photos/0.2.x/${pkgver}/+download/pantheon-photos-${pkgver}.tar.xz")
sha256sums=('f5718de48c33026ff0bff5e9b702a2b0de8a6453a49feec388588aa3aa9eec3d')

prepare() {
  cd pantheon-photos-${pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd pantheon-photos-${pkgver}/build

  cmake .. \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_LIBDIR='/usr/lib' \
    -DCMAKE_INSTALL_LIBEXECDIR='/usr/lib' \
    -DGSETTINGS_COMPILE='OFF'
  make
}

package() {
  cd pantheon-photos-${pkgver}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
