# Maintainer: Chocobo1 <chocobo1 AT archlinux DOT net>

pkgname=irqbalance-git
pkgver=1.2.0.r36.gaae4eb6
pkgrel=1
pkgdesc="A daemon to help balance the CPU load generated by interrupts across all of a systems CPUs"
arch=('i686' 'x86_64')
url="https://irqbalance.github.io/irqbalance/"
license=('GPL')
depends=('glibc' 'glib2' 'libcap-ng' 'numactl')
makedepends=('git')
provides=('irqbalance')
conflicts=('irqbalance')
source=("git+https://github.com/Irqbalance/irqbalance.git"
        "irqbalance.service::https://git.archlinux.org/svntogit/packages.git/plain/trunk/irqbalance.service?h=packages/irqbalance")
sha256sums=('SKIP'
            'SKIP')


pkgver() {
  cd "irqbalance"

  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "irqbalance"

  ./autogen.sh
  ./configure --prefix="/usr" --sbindir="/usr/bin" --with-systemd
  make
}

package() {
  cd "irqbalance"

  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/irqbalance.service" "$pkgdir/usr/lib/systemd/system/irqbalance.service"
}
