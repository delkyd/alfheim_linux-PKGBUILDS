# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=sugar-activity-browse
_pkgname=Browse
pkgver=201.2
pkgrel=1
pkgdesc="Sugar activity to browse the internet"
arch=('any')
url="https://wiki.sugarlabs.org/go/Activities/Browse"
license=('GPL')
groups=('sugar-fructose')
depends=('evince' 'gconf' 'sugar-toolkit-gtk3' 'webkit2gtk')
source=(https://download.sugarlabs.org/sources/sucrose/fructose/$_pkgname/$_pkgname-$pkgver.tar.bz2)
sha256sums=('b6e4e2c41de02af7d306af1fb7b4bc10e92b982efa5a1682782e896e24bb4948')

prepare() {
  cd $_pkgname-$pkgver
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' setup.py
  sed -i 's/"Feladó"/"Feladó: %s"/' po/hu.po
}

package() {
  cd $_pkgname-$pkgver
  python2 setup.py install --prefix "$pkgdir/usr"
  rm -r "$pkgdir"/usr/share/applications
}
