# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Michael P <ptchinster@archlinux.us>
# Contributor: Aaron Griffin <aaron@archlinux.org>
# Contributor: Elmo Todurov <todurov@gmail.com>
# Contributor: Alexander De Sousa <archaur.xandy21@spamgourmet.com>

pkgname=scanmem
pkgver=0.16.1
pkgrel=1
pkgdesc='Memory scanner designed to isolate the address of an arbitrary variable in an executing process'
url='https://github.com/scanmem/scanmem'
arch=('i686' 'x86_64')
license=('GPL3')
depends=('gtk3' 'python-gobject' 'polkit')
makedepends=('intltool')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/v${pkgver}.tar.gz)
sha256sums=('6eb8b843a34ef70f0d8f42f1d3aff779ae4cf7fde468ba4728519f45908b765b')
sha512sums=('5e0a97e020a8a392aed75e2654cc6bcfad68590b9caab43444e57c3180be45ce1269f5acb02355bee57ff8bb5b9349b4f65e0fe96655206a812ea645796b153b')

prepare() {
  cd ${pkgname}-${pkgver}
  ./autogen.sh
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --enable-gui --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
