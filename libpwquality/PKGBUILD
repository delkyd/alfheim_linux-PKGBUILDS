# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libpwquality
pkgver=1.3.0
pkgrel=2
pkgdesc="Library for password quality checking and generating random passwords"
arch=('i686' 'x86_64')
url="https://github.com/libpwquality/libpwquality"
license=('GPL')
depends=('cracklib' 'pam')
optdepends=('python2: Python bindings')
makedepends=('python2' git)
backup=('etc/security/pwquality.conf')
_commit=6e95824d07660d3c3a8d11252cf5a033f2d189d5  # master
source=("git+https://github.com/libpwquality/libpwquality.git#commit=$_commit")
md5sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^libpwquality-//;s/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
  sed '/mr_IN/d' -i po/LINGUAS
}

build() {
    cd "$pkgname"
    ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-python-rev=2.7 \
        --with-python-binary=python2
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
    make
}

package() {
    cd "$pkgname"
    make DESTDIR="$pkgdir" install
    install -m755 -d "$pkgdir"/etc/security/pwquality.conf.d
}
