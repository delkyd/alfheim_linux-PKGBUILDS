# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=variety
pkgver=0.6.5
pkgrel=1
pkgdesc='Changes the wallpaper on a regular interval using user-specified or automatically downloaded images.'
arch=('any')
url="http://peterlevi.com/variety/"
license=('GPL3')
depends=('gtk3' 'imagemagick' 'libgexiv2' 'libnotify' 'python2-beautifulsoup4' 'python2-cairo'
         'python2-configobj' 'python2-dbus' 'python2-gobject' 'python2-httplib2' 'python2-pillow'
         'python2-lxml' 'python2-pycurl' 'python2-requests')
makedepends=('python2-distutils-extra')
source=(https://launchpad.net/variety/trunk/${pkgver}/+download/${pkgname}_${pkgver}.tar.gz{,.asc}
        fix-vauleerror.patch)
#validpgpkeys=('53A44FA64701571F59CD8C9BA7FAEF1B4791406E') # Peter Levi
validpgpkeys=('D5D568B2D34AB32A337944D22EC3F60DE71C0B9D') # James Lu <bitflip3@gmail.com>
sha256sums=('d3eeb5b800d881fc0e1055f5614e65b6b6f64e0f1eb5b72fdf2869ae50c51bcc'
            'SKIP'
            'b16842754013398eb85499137b4b2ea36748505e65eef46864847f7bb8606264')

prepare() {
  cd "${srcdir}"/$pkgname-$pkgver

  # Fix error when appindicator is missing
  patch -Np1 -i ../fix-vauleerror.patch
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  export XDG_RUNTIME_DIR="/tmp/${pkgname}"
  python2 setup.py install --root="${pkgdir}" --optimize=1
}
