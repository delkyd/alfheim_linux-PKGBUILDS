# Contributor: Dalton Miller
# Contributor: Kilian Lackhove kilian@lackhove.de
# Contributor: Justin Patera serialhex@gmail.com
# Maintainer: ava1ar <mail(at)ava1ar(dot)me>

pkgname=btsync-1.4
pkgver=1.4.111
pkgrel=1
pkgdesc="BitTorrent Sync - automatically sync files via secure, distributed technology (beta version)"
license=("custom:btsync")
arch=('i686' 'x86_64' 'arm' 'armv6h' 'armv7h')
url="http://www.bittorrent.com/sync"
install=btsync.install
backup=('etc/btsync.conf')
optdepends=("btsync-autoconfig: Auto-create users' config files if needed") 
conflicts=('btsync')
source=(btsync.service btsync_user.service btsync.conf)
source_arm+=("btsync_arm-${pkgver}.tar.gz::https://www.dropbox.com/s/m10evmy9it2gxst/btsync_arm-${pkgver}.tar.gz?dl=1")
source_armv6h+=("btsync_arm-${pkgver}.tar.gz::https://www.dropbox.com/s/m10evmy9it2gxst/btsync_arm-${pkgver}.tar.gz?dl=1")
source_armv7h+=("btsync_arm-${pkgver}.tar.gz::https://www.dropbox.com/s/m10evmy9it2gxst/btsync_arm-${pkgver}.tar.gz?dl=1")
source_i686+=("btsync_i386-${pkgver}.tar.gz::https://www.dropbox.com/s/n9h9b7ptetxj2ie/btsync_i386-${pkgver}.tar.gz?dl=1")
source_x86_64+=("btsync_x64-${pkgver}.tar.gz::https://www.dropbox.com/s/l9fqf3toj37yku4/btsync_x64-${pkgver}.tar.gz?dl=1")
sha1sums=('05038b03a673dd0207b2758a2e01db5d28c409d1'
          'd30deb7e41ba1d163b4a5e442b8d8118758d312a'
          'bf7d6bd7ffe2d23cce51a51e92fac3b82a7c87bb')
sha1sums_i686=('5e09a31fab0cf5032e22df12e4d67651bfe9edfe')
sha1sums_x86_64=('bb2dec020e5d8ec3a0ce18a38ae08ddabd2a7750')
sha1sums_arm=('00769d20b05e816a43c797f32a690f0fb8cd5dd5')
sha1sums_armv6h=('00769d20b05e816a43c797f32a690f0fb8cd5dd5')
sha1sums_armv7h=('00769d20b05e816a43c797f32a690f0fb8cd5dd5')

package() {
	# install main binary
	install -Dm755 "${srcdir}"/btsync "${pkgdir}"/usr/bin/btsync

	# generate and install system-wide config
	mkdir -p "${pkgdir}"/etc
	./btsync --dump-sample-config \
		| sed 's:/home/user/\.sync:/var/lib/btsync:' \
		| sed 's:\/\/ "pid_file":  "pid_file":' \
		| sed 's:\/\/ "storage_path":  "storage_path":' \
		> "${pkgdir}"/etc/btsync.conf

	# install systemd config files
	install -Dm644 "${srcdir}"/btsync.service "${pkgdir}"/usr/lib/systemd/system/btsync.service
	install -Dm644 "${srcdir}"/btsync.conf "${pkgdir}"/usr/lib/tmpfiles.d/btsync.conf
	install -Dm644 "${srcdir}"/btsync_user.service "${pkgdir}"/usr/lib/systemd/user/btsync.service

	# install license
	install -Dm644 "${srcdir}"/LICENSE.TXT "${pkgdir}"/usr/share/licenses/${pkgname}/license.txt
}
