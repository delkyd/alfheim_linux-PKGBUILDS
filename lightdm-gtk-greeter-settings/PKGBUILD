# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Arthur Zamarin <arthurzam@gmail.com>
# Contributor: andrew.p <pan.pav.7c5@gmail.com>

pkgname=lightdm-gtk-greeter-settings
pkgver=1.2.1
pkgrel=5
pkgdesc='Settings editor for the LightDM GTK+ Greeter'
arch=('any')
url='https://launchpad.net/lightdm-gtk-greeter-settings'
license=('GPL3')
depends=('gtk-update-icon-cache' 'lightdm-gtk-greeter' 'python-gobject')
makedepends=('python-distutils-extra')
source=("https://launchpad.net/lightdm-gtk-greeter-settings/${pkgver%.*}/${pkgver}/+download/lightdm-gtk-greeter-settings-${pkgver}.tar.gz")
sha256sums=('ab91027b56ca0e0c98a38169acc453c08801bc4937dc1a53bd6e98b7aaceea96')

build() {
  cd lightdm-gtk-greeter-settings-${pkgver}

  python setup.py build
}

package() {
  cd lightdm-gtk-greeter-settings-${pkgver}

  python setup.py install --root="${pkgdir}" --optimize='1'
}

# vim: ts=2 sw=2 et:
