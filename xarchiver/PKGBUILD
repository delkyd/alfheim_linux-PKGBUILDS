# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: Andrew Simmons <andrew.simmons@gmail.com>

pkgbase=xarchiver
pkgname=(xarchiver xarchiver-gtk2)
pkgver=0.5.4.11
_gitrev=79a73d46b782cf8bb93d058d00d2cb81a9b3df8a
pkgrel=1
pkgdesc="GTK+ frontend to various command line archivers"
arch=('i686' 'x86_64')
url="https://github.com/ib/xarchiver"
license=('GPL')
depends=('gtk2' 'gtk3')
makedepends=('docbook-xsl' 'intltool' 'xmlto')
optdepends=('zip: ZIP support'
            'unzip: ZIP support'
            'p7zip: 7z support'
            'lzop: LZOP support'
            'cpio: RPM support'
            'unrar: RAR support')
source=($pkgbase-$pkgver.tar.gz::https://github.com/ib/xarchiver/archive/$_gitrev.tar.gz)
sha256sums=('2dabd6f5d96c5d886d9a5ed7d299cfc43d1fef88be37d1d298a78c4c01a63b45')

build() {
  # GTK+ 3 version
  [ -d gtk3 ] || cp -r $pkgbase-$_gitrev gtk3
  cd gtk3
  ./configure --prefix=/usr --libexecdir=/usr/lib/xfce4
  make -j1

  cd "$srcdir"
  # GTK+ 2 version
  [ -d gtk2 ] || cp -r $pkgbase-$_gitrev gtk2
  cd gtk2
  ./configure --prefix=/usr --libexecdir=/usr/lib/xfce4 --enable-gtk2
  make -j1
}

package_xarchiver() {
  depends=('gtk3')

  cd gtk3
  make DESTDIR="$pkgdir" install
}

package_xarchiver-gtk2() {
  pkgdesc+=' (GTK+ 2 version)'
  depends=('gtk2')
  conflicts=('xarchiver')

  cd gtk2
  make DESTDIR="$pkgdir" install
}
