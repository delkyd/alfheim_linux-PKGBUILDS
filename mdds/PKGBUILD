# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: kusakata <shohei atmark kusakata period com>

pkgname=mdds
pkgver=1.2.3
pkgrel=1
pkgdesc="A collection of multi-dimensional data structures and indexing algorithms"
arch=('any')
url="https://gitlab.com/mdds/mdds"
license=('MIT')
checkdepends=('boost')
#source=(https://gitlab.com/mdds/mdds/repository/archive.tar.gz?ref=${pkgver})
source=(https://kohei.us/files/mdds/src/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('402fec18256f95b89517d54d85f00bce1faa6e517cb3d7c98a720fddd063354f')

build() {
  cd ${pkgname}-${pkgver}
  ./autogen.sh --prefix=/usr
  # make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
