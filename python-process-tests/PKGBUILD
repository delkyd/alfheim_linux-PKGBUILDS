# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-process-tests
pkgname=('python-process-tests' 'python2-process-tests')
pkgver=1.2.2
pkgrel=1
pkgdesc="Tools for testing processes"
arch=('any')
url="https://pypi.python.org/pypi/process-tests"
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://pypi.io/packages/source/p/process-tests/process-tests-$pkgver.tar.gz")
sha512sums=('63c5c94e75bdf4bbfa70959bd474f5ac6d707629773af1a0c9d041455e3da64d7e99c0bea7d5028ec1346714b4b14cfe968db8e4f4b11cba99d48d395c4a1655')

prepare() {
  cp -a process-tests-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/process-tests-$pkgver
  python setup.py build

  cd "$srcdir"/process-tests-$pkgver-py2
  python2 setup.py build
}

package_python-process-tests() {
  depends=('python')

  cd process-tests-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-process-tests() {
  depends=('python2')

  cd process-tests-$pkgver
  python2 setup.py install --root="$pkgdir/" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
