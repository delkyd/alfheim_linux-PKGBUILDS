# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=reiserfsprogs
pkgver=3.6.25
pkgrel=1
pkgdesc="Reiserfs utilities"
arch=('i686' 'x86_64')
url="http://www.kernel.org/"
license=('GPL')
groups=('base')
depends=('util-linux')
source=("http://ftp.kernel.org/pub/linux/kernel/people/jeffm/reiserfsprogs/v${pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('027a598b055dccb8da3aea1378a19f3c')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  CFLAGS+=' -fgnu89-inline'
  ./configure --prefix=/usr --sbindir=/usr/bin
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
