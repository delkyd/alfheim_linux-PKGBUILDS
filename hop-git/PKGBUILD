# Contributor: Manuel Serrano <Manuel.Serrano@inria.fr>
# Contributor: MCMic <come.bernigaud@laposte.net>
# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=hop-git
pkgver=3.1.0r3937.0ad65718
pkgrel=1
pkgdesc="Software Development Kit for the Web"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
depends=('bigloo-devel' 'gmp' 'libunistring' 'libuv' 'avahi' 'sqlite')
conflicts=('hop')
provides=('hop')
install=hop.install
url='http://hop.inria.fr'
source=("git+https://github.com/manuel-serrano/hop.git")
md5sums=('SKIP')
options=('!makeflags')

pkgver() {
  cd ${pkgname%-git}
  printf "%sr%s.%s" "3.1.0" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd ${pkgname%-git}
  ./configure --prefix=/usr --etcdir=/etc/hop --mandir=/usr/share/man \
	      --disable-ssl --bigloobindir=/usr/bin \
	      --bigloolibdir=/usr/lib/bigloo/4.3b 
  make
  make doc
}

check() {
  cd ${pkgname%-git}
  make test
}
  
package() {
  cd ${pkgname%-git}
  make DESTDIR="$pkgdir" install
  install -Dm644 arch/archlinux/conf.d/hop.in "$pkgdir"/etc/conf.d/hop
  install -Dm755 arch/archlinux/rc.d/hop.in "$pkgdir"/etc/rc.d/hop
  install -Dm644 arch/archlinux/systemd/hop.service.in \
	  "$pkgdir"/usr/lib/systemd/system/hop.service
  install -Dm644 arch/archlinux/systemd/hop.socket.in \
	  "$pkgdir"/usr/lib/systemd/system/hop.socket

  install -Dm755 arch/archlinux/bin/hop.sh "$pkgdir"/usr/bin/hop.sh
  cd "$pkgdir"/usr/bin; rm hop; ln -s hop-3.0.0 hop
}
