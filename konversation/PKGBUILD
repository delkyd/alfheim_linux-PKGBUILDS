# $Id: PKGBUILD 225717 2014-11-07 02:03:06Z foutrelis $
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: James Rayner <iphitus@gmail.com>
# Contributor: Sergio Jovani Guzman <moret@paretux.org>

pkgname=konversation
pkgver=1.7.2
pkgrel=1
pkgdesc="A user-friendly and fully-featured IRC client"
arch=(i686 x86_64)
url="http://konversation.kde.org"
depends=(knotifyconfig kemoticons kparts kidletime qca-qt5 hicolor-icon-theme)
optdepends=('python: python scripting support' 'keditbookmarks: for managing bookmarks')
makedepends=(extra-cmake-modules kdoctools python)
license=(GPL2 FDL)
source=("http://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('5ff96e84cee4e1eefc404a31d778067ea50dddd8a6c848911fac70bd52812618'
            'SKIP')
validpgpkeys=(DA6B31C4B98A877DBB2EF9A292220328C632316E) # Eike Hein <hein@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
