# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Xavier D. <magicrhesus@ouranos.be>
# Contributor: Zarra <zarraxx@gmail.com>

pkgname=dbus-c++
_pkgname=lib$pkgname
pkgver=0.9.0
pkgrel=7
pkgdesc="A C++ API for D-BUS"
url="http://sourceforge.net/projects/dbus-cplusplus/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus>=1.2.0' 'glib2')
makedepends=('autoconf' 'automake' 'libtool')
source=("http://sourceforge.net/projects/dbus-cplusplus/files/dbus-c%2B%2B/$pkgver/libdbus-c%2B%2B-$pkgver.tar.gz"
	      'gcc47.patch'
        'disable-threading.patch'
        'fix-writechar.patch')
sha256sums=('bc11ac297b3cb010be904c72789695543ee3fdf3d75cdc8225fd371385af4e61'
            '9d504faf769026dcaabca94c7645373fcadf43ef93c8263c6a85e905798f07c9'
            '38730ee73fe4de320fceab6619579b55861b6b8e0f6d8069a9ddf3d1ad1266c0'
            '34ada5bf31918c63558d0fb3fff63e0f1596f25082ff22e84117927ad7236fce')
 
prepare() {
    cd "$srcdir/$_pkgname-$pkgver"
    patch -Np0 -i "$srcdir/gcc47.patch"
    patch -Np1 -i "$srcdir/disable-threading.patch"
    patch -Np1 -i "$srcdir/fix-writechar.patch"
}

build() {
    cd "$srcdir/$_pkgname-$pkgver"

    export LDFLAGS+=" -lexpat -lpthread" # -lpthread needed for i686

    ./autogen.sh --prefix=/usr \
                 --disable-static \
                 --enable-glib \
                 --disable-ecore
    make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
