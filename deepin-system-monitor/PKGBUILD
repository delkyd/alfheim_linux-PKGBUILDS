# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-system-monitor
pkgver=1.3
pkgrel=1
pkgdesc='A more user-friendly system monitor'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/deepin-system-monitor"
license=('GPL3')
depends=('dtkwidget' 'dtkwm' 'procps-ng' 'nethogs' 'libcap')
makedepends=('qt5-tools')
groups=('deepin')
install=deepin-system-monitor.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-system-monitor/archive/$pkgver.tar.gz")
sha512sums=('ead92d5e11ef527e0aef8b861ad496ee8a5fa533925029b177ae3f8b47fb41cb53bb47ac48f61ebc566aa739e2c785f9aed52b213b0c786f8dfcd6cf5ea3feb9')

prepare() {
  cd deepin-system-monitor-$pkgver
  rm -r nethogs
  sed -i 's|error("Build nethogs static library failed.")||' deepin-system-monitor.pro
}

build() {
  cd deepin-system-monitor-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-system-monitor-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
