# $Id$
# Maintainer: Alexander F Rødseth <xyproto@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>

pkgname=libnsgif
pkgver=0.1.4
pkgrel=1
pkgdesc='Decoding library for the GIF image file format'
arch=('x86_64' 'i686')
url='http://www.netsurf-browser.org/projects/libnsgif/'
license=('MIT')
depends=('glibc')
makedepends=('netsurf-buildsystem')
source=("http://download.netsurf-browser.org/libs/releases/$pkgname-$pkgver-src.tar.gz")
sha256sums=('133c2354c61564080ad1bac253ee0df7e6c82bb6090644ca438ce3a799f6bad1')

build() {
  export CFLAGS="$CFLAGS -fno-strict-aliasing"

  make -C "$pkgname-$pkgver" \
    INCLUDEDIR=include \
    LIBDIR=lib PREFIX=/usr \
    COMPONENT_TYPE="lib-shared"
}

package() {
  cd "$pkgname-$pkgver"

  make install \
    INCLUDEDIR=include \
    LIBDIR=lib \
    PREFIX=/usr \
    DESTDIR="$pkgdir" \
    COMPONENT_TYPE="lib-shared"

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
