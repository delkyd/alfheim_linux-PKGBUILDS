# Maintainer: Tinu Weber <martin.weber@epfl.ch>

pkgname=epfl-scripts-git
_pkgname=epfl-scripts
pkgver=r53.4ff9727
pkgrel=1
pkgdesc="Collection of scripts for simplifying one's life at EPFL"
arch=('any')
url='https://gitlab.gnugen.ch/gnugen/epfl-scripts'
license=('Apache')
provides=(epfl-scripts)
conflicts=(epfl-scripts)
depends=(coreutils)
changelog='changelog'
source=('git+https://gitlab.gnugen.ch/gnugen/epfl-scripts.git')
md5sums=('SKIP')

pkgver() {
  cd "$_pkgname"
  printf 'r%s.%s' "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
  depends+=(curl bash file openconnect perl-html-tree perl-io-stringy
            perl-lwp-protocol-https perl-html-treebuilder-xpath
            perl-www-mechanize perl-xml-xpathengine)
  optdepends=('iproute2: network-namespaces in epfl-vpn'
              'iptables: network-namespaces in epfl-vpn')
  cd "$srcdir"/"$_pkgname"
  install -Dm 755 bin/epfl-vpn "$pkgdir"/usr/bin/epfl-vpn
  install -Dm 755 bin/gnupaste "$pkgdir"/usr/bin/gnupaste
  install -Dm 755 bin/pastegnugen.pl "$pkgdir"/usr/bin/pastegnugen.pl
  install -Dm 755 bin/tl.pl "$pkgdir"/usr/bin/tl.pl
  install -Dm 755 bin/velo.pl "$pkgdir"/usr/bin/velo.pl
}
