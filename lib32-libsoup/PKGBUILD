# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Maximilian Stein <theoddbird@posteo.org>
# Contributor: josephgbr <rafael.f.f1@gmail.com>

pkgname=lib32-libsoup
pkgver=2.58.2
pkgrel=1
pkgdesc='HTTP client/server library for GNOME'
arch=('x86_64')
url="https://wiki.gnome.org/Projects/libsoup"
license=('LGPL')
depends=('lib32-glib-networking' 'lib32-krb5' 'lib32-libxml2' 'lib32-sqlite'
         'libsoup')
makedepends=('gcc-multilib' 'gobject-introspection' 'intltool' 'python'
             'vala')
conflicts=('lib32-libsoup-gnome')
replaces=('lib32-libsoup-gnome')
options=('!emptydirs')
source=("https://download.gnome.org/sources/libsoup/${pkgver:0:4}/libsoup-${pkgver}.tar.xz")
sha256sums=('442300ca1b1bf8a3bbf2f788203287ff862542d4fc048f19a92a068a27d17b72')

build() {
  cd libsoup-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --disable-static
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd libsoup-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{include,share}
}

# vim: ts=2 sw=2 et:
