# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=haskell-cairo
pkgver=0.13.3.1
pkgrel=5
pkgdesc="Binding to the cairo library for Gtk2Hs."
url="https://hackage.haskell.org/package/cairo"
license=('LGPL2.1')
arch=('i686' 'x86_64')
depends=('ghc-libs' 'cairo' 'haskell-mtl' 'haskell-utf8-string' 'haskell-text')
makedepends=("haskell-gtk2hs-buildtools" "mesa" 'ghc')
conflicts=('gtk2hs-cairo')
provides=('gtk2hs-cairo')
replaces=('gtk2hs-cairo')
source=("https://github.com/gtk2hs/gtk2hs/archive/843d83548bd076975f1869e6768768b5ed24b268.tar.gz")
sha512sums=('610a080ddf8ad8511176a7fd5ed5457aa26b5ce48539b7eb46563d86db1ffab6501f8b4deac4152f7d9cea84093de34e1bd1bb9467f558f1c202026337e1c708')

prepare() {
    mv gtk2hs-843d83548bd076975f1869e6768768b5ed24b268/cairo cairo-$pkgver
}

build() {
    cd "${srcdir}/cairo-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/cairo-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
}
