# $Id: PKGBUILD 55397 2011-09-11 21:15:58Z spupykin $
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: see .contrib

pkgname=libconfig
pkgver=1.6
pkgrel=2
pkgdesc="C/C++ Configuration File Library"
arch=('i686' 'x86_64')
url="http://hyperrealm.com/libconfig/libconfig.html"
license=('LGPL')
depends=('gcc-libs' 'texinfo')
options=('!emptydirs' 'zipman')
source=($pkgname-$pkgver.tar.gz::"https://github.com/hyperrealm/libconfig/archive/v$pkgver.tar.gz")
md5sums=('2ccd24b6a2ee39f7ff8a3badfafb6539')

prepare() {
  cd $pkgname-$pkgver

# See: https://github.com/hyperrealm/libconfig/issues/47
  rm lib/scanner.c
}

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
  rm "$pkgdir/usr/share/info/dir"
}

# vim:set ts=2 sw=2 et:
