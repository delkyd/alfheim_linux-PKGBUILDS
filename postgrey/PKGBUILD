# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Sean Timothy Noonan <stnoonan@obsolescence.net>

pkgname=postgrey
pkgver=1.37
pkgrel=1
pkgdesc="a Postfix policy server implementing greylisting"
arch=('any')
url="https://postgrey.schweikert.ch/"
options=(emptydirs)
license=("GPL")
depends=('perl-net-server' 'perl>=5.6.0' 'perl-berkeleydb' 'perl-io-multiplex'
	 'db>=4.1' 'perl-netaddr-ip')
optdepends=('postfix: the recommended MTA for use with postgrey'
	    'exim: an alternative MTA'
	    'perl-net-dns: for postgreyreport tool')
backup=(etc/postfix/postgrey_whitelist_recipients
	etc/postfix/postgrey_whitelist_clients)
install=$pkgname.install
source=($url/pub/$pkgname-$pkgver.tar.gz
	postgrey.service
	postgrey.tmpfiles
	untaint.patch)
sha256sums=('ff4d9543f8f5cb0356c30ffe22255d942ac6128da734c376de211c02630fa5f7'
            'd36c73b6a8ba96ba3eaa3e56098afda828ee56ec12000d3392ab450246d38e09'
            '213e5548c6355a2c57a15919aec6932def611a12cf20855ad6642626cae27f71'
            'e5f80653c02681b54f28051104f1ce45991b014a86965610611b1b4921284bd7')

prepare() {
  cd "$srcdir"/$pkgname-$pkgver
  patch -p0 <"$srcdir"/untaint.patch
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  chown -R root:root .
  mkdir -p "$pkgdir"/var/spool/postfix/postgrey
  install -D -m 0755 postgrey "$pkgdir"/usr/bin/postgrey
  install -D -m 0755 contrib/postgreyreport "$pkgdir"/usr/bin/postgreyreport
  install -D -m 0644 postgrey_whitelist_recipients "$pkgdir"/etc/postfix/postgrey_whitelist_recipients
  install -D -m 0644 postgrey_whitelist_clients "$pkgdir"/etc/postfix/postgrey_whitelist_clients

  install -Dm0644 "$srcdir"/$pkgname.service "$pkgdir"/usr/lib/systemd/system/$pkgname.service
  install -Dm0644 "$srcdir"/$pkgname.tmpfiles "$pkgdir"/usr/lib/tmpfiles.d/$pkgname.conf
}
