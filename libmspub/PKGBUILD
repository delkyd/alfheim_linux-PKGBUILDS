# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Simone Sclavi 'Ito' <darkhado@gmail.com>

pkgname=libmspub
pkgver=0.1.2
pkgrel=7
pkgdesc="Microsoft Publisher file format parser library (latest snapshot)"
arch=('i686' 'x86_64')
url="https://wiki.documentfoundation.org/DLP/Libraries/libmspub"
license=('GPL2' 'LGPL2.1' 'MPL')
depends=('libwpd' 'icu' 'librevenge')
makedepends=('libwpg' 'boost' 'doxygen')
source=(https://dev-www.libreoffice.org/src/${pkgname}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('b0baabf82d20c08ad000e80fa02154ce2f2ffde1ee60240d6e3a917c3b35560f')

build() {
    cd ${pkgname}-${pkgver}
    ./configure --prefix=/usr --with-docs
    make
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR=$pkgdir install
}
