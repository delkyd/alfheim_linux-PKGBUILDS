# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: dibblethewrecker dibblethewrecker.at.jiwe.dot.org
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=gpsbabel
pkgver=1.5.4
pkgrel=2
pkgdesc="Reads, writes, and manipulates GPS waypoints in a variety of formats"
arch=('i686' 'x86_64')
url="http://www.gpsbabel.org"
license=('GPL')
depends=('libusb-compat' 'qt5-base')
makedepends=('docbook-xsl' 'fop' 'qt5-tools') # 'qt5-webengine'
# optdepends=('qt5-webengine: run the GUI')
options=('docs')
source=("https://arch.p5n.pp.ru/~sergej/dl/gpsbabel-$pkgver.tar.gz"
	"build-fix.patch"
	"drop-map.patch")
sha256sums=('59e29b83a16af16556fffc9315d3d3626d11fc7f2f50d6ec1f99dfc64412989e'
            '537752bd5d097d836d4d5a77b4f7a5ea68d408d6a648511e4a98865d2742fb46'
            'bee2cc0549fe9d2aacb7266520428569438c7d7e40c283722eb403676d9aef7a')

prepare() {
  cd "$srcdir"/$pkgname-$pkgver
  patch -p1 <../build-fix.patch

  # Map support is currently broken
  patch -p1 <../drop-map.patch

  sed -i 's|langPath_ = QApplication::applicationDirPath();|langPath_ = "/usr/share/gpsbabel";|' gui/mainwindow.cc
  sed -i 's|baseFile =  QApplication::applicationDirPath() |baseFile = "/usr/share/gpsbabel" |' gui/map.cc
}

build() {
  cd "$srcdir"/$pkgname-$pkgver
  ./configure --prefix=/usr --with-doc=babelweb
  make
  make doc

  # GUI
  pushd gui
  qmake-qt5
  lrelease-qt5 *.ts
  make
  popd
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  # GUI
  install -Dm755 gui/objects/gpsbabelfe-bin "$pkgdir"/usr/bin/gpsbabelfe-bin
  install -Dm644 gui/images/appicon.png "$pkgdir"/usr/share/pixmaps/gpsbabel.png
  install -Dm644 gui/gpsbabel.desktop "$pkgdir"/usr/share/applications/gpsbabel.desktop
  install -Dm644 gui/gmapbase.html "$pkgdir"/usr/share/gpsbabel/gmapbase.html
  install -dm755 "$pkgdir"/usr/share/gpsbabel/translations/
  install -Dm644 gui/*.qm "$pkgdir"/usr/share/gpsbabel/translations/

  # Header install
  install -dm755 "$pkgdir"/usr/include/gpsbabel/
  install -Dm644 *.h "$pkgdir"/usr/include/gpsbabel/

  # Doc install
  install -dm755 "$pkgdir"/usr/share/doc/$pkgname/
  cp -r babelweb/* "$pkgdir"/usr/share/doc/$pkgname/
  install -Dm644 README* "$pkgdir"/usr/share/doc/$pkgname/
}
