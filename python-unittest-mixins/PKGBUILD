# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-unittest-mixins
pkgname=('python-unittest-mixins' 'python2-unittest-mixins')
pkgver=1.3
pkgrel=1
pkgdesc='Helpful mixins for unittest classes'
arch=('any')
license=('Apache')
url='http://github.com/nedbat/unittest-mixins'
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/nedbat/unittest-mixins/archive/v$pkgver.tar.gz")
sha512sums=('8cdc7adf387d3576d1690c2ab62eff93c085a36b82b756d237cc177aa52718e7ce6c5ec103841f1581cd28576bb17fbbe034c21d1c1347da643dfde3b8033d3a')

prepare() {
  cp -a unittest-mixins-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/unittest-mixins-$pkgver
  python setup.py build

  cd "$srcdir"/unittest-mixins-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/unittest-mixins-$pkgver
  python setup.py pytest

  cd "$srcdir"/unittest-mixins-$pkgver-py2
  python2 setup.py pytest
}

package_python-unittest-mixins() {
  depends=('python-six')

  cd unittest-mixins-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-unittest-mixins() {
  depends=('python2-six')

  cd unittest-mixins-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
