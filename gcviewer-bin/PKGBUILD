# Maintainer : Taiki Sugawara <buzz.taiki@gmail.com>
pkgname=gcviewer-bin
_pkgname=gcviewer
pkgver=1.34.1
pkgrel=1
pkgdesc="GCViewer is a little tool that visualizes verbose GC output generated by Sun, IBM, HP and BEA Java Virtual Machines."
arch=('any')
url="https://github.com/chewiebug/GCViewer"
license=('LGPL')
depends=('java-runtime')
makedepends=('imagemagick')
provides=('gcviewer')
conflicts=('gcviewer' 'gcviewer-git')
source=("http://downloads.sourceforge.net/project/gcviewer/$_pkgname-$pkgver.jar"
        "https://raw.githubusercontent.com/chewiebug/GCViewer/$pkgver/README"
        "https://raw.githubusercontent.com/chewiebug/GCViewer/$pkgver/src/main/resources/com/tagtraum/perf/gcviewer/gcviewericon.gif"
        'gcviewer.sh'
        'gcviewer.desktop')
sha256sums=('e5b026f629dba7dc04f035e3267e5663e8225b8ba1d8dacda96d1fddea288b6c'
            '9c4368547a31296d8b4b9403531c927769396db1fead8cc4c24a56d2554eaf4a'
            '016cb8684edccbef21834a274a5e3c9226ef19a2cb88fc6f317757d53661e3ba'
            '20316cd8975215966d9983aab9cc61a1a95acdbd397d30db6ad1da7f2ecc2e0b'
            '255f2a7b966832b98b0e8683d3a2b97a3a71bd354000d226ab21500ec52e8a91')
noextract=("$_pkgname-$pkgver.jar")

build() {
  convert "$srcdir/gcviewericon.gif" "$srcdir/gcviewericon.png"
}

package() {
  install -D -m644 "$srcdir/$_pkgname-$pkgver.jar"  "$pkgdir/usr/share/java/$_pkgname/$_pkgname.jar"
  install -D -m644 "$srcdir/README"  "$pkgdir/usr/share/doc/$_pkgname/README"
  install -D -m644 "$srcdir/gcviewericon.png" "$pkgdir/usr/share/pixmaps/$_pkgname.png"
  install -D -m755 "$srcdir/gcviewer.sh" "$pkgdir/usr/bin/gcviewer"
  install -D -m644 "$srcdir/gcviewer.desktop" "$pkgdir/usr/share/applications/$_pkgname.desktop"
}

# vim:set ts=2 sw=2 et:
