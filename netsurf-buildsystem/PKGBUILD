# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>

pkgname=netsurf-buildsystem
pkgver=1.5
pkgrel=1
pkgdesc='The Netsurf buildsystem'
arch=('x86_64' 'i686')
url='http://source.netsurf-browser.org/buildsystem.git/'
depends=('perl' 'bash')
license=('MIT')
source=("http://download.netsurf-browser.org/libs/releases/buildsystem-$pkgver.tar.gz")
sha256sums=('dbc6afc639f6987b704e6ff4427668787ee7efba77e1bbf76b614699b5daaf71')

build() {
  cd "buildsystem-$pkgver"

  sed -i 's:-Werror::' Makefile
  make PREFIX=/usr COMPONENT_TYPE='lib-shared'
  make PREFIX=/usr COMPONENT_TYPE='lib-static'
}

package() {
  cd "buildsystem-$pkgver"

  make install PREFIX=/usr DESTDIR="$pkgdir" COMPONENT_TYPE='lib-shared'
  make install PREFIX=/usr DESTDIR="$pkgdir" COMPONENT_TYPE='lib-static'
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# getver: -u 7
# vim:set ts=2 sw=2 et:
