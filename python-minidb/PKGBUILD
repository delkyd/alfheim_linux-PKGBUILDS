# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=python-minidb
_pkgname=minidb
pkgver=2.0.2
pkgrel=1
pkgdesc="A simple SQLite3 store for Python objects"
arch=('any')
url="https://thp.io/2010/minidb/"
license=('ISC')
depends=('python')
source=("https://thp.io/2010/minidb/$_pkgname-$pkgver.tar.gz")
md5sums=('b0253f6e8a288fd8f2e476a25dac51ce')

build() {
  cd $_pkgname-$pkgver
  python3 setup.py build
}

package() {
  cd $_pkgname-$pkgver
  python3 setup.py install --root="$pkgdir"

  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  sed -n '/# Copyright/,/OF THIS SOFTWARE./p' minidb.py > "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
