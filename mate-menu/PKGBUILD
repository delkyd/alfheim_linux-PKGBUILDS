# $Id$
# Contriburor: Balló György <ballogyor+arch at gmail dot com>

pkgname=mate-menu
pkgver=17.10.7
pkgrel=1
pkgdesc="Advanced menu for MATE Panel, a fork of MintMenu"
arch=('any')
url="https://bitbucket.org/ubuntu-mate/mate-menu"
license=('GPL')
depends=('mate-panel' 'python2-configobj' 'python2-gobject' 'python2-pyinotify' 'python2-xdg' 'python2-xlib' 'xdg-utils' 'python2-setproctitle')
makedepends=('python2-distutils-extra' 'python2-setuptools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ubuntu-mate/mate-menu/archive/$pkgver.tar.gz")
sha256sums=('f2ab31b6c6f6dee357c4dc6b808b2081e9afd1cf1f73a0ee6ddfc94a4cb7671e')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' lib/*.py
}

package() {
  cd $pkgname-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1
}
