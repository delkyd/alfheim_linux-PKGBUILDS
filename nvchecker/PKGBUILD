# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=nvchecker
pkgver=0.5
pkgrel=1
pkgdesc="New version checker for software"
arch=('any')
url="https://github.com/lilydjwg/nvchecker"
license=('MIT')
depends=('python-setuptools' 'python-tornado' 'python-pycurl')
checkdepends=('python-pytest-runner' 'python-pytest-xdist' 'python-pytest-asyncio' 'python-flaky')
source=("$pkgname-$pkgver.tar.gz::https://github.com/lilydjwg/nvchecker/archive/v$pkgver.tar.gz")
sha512sums=('aec92e7102c8b14db5033d5e2d9e1bff74b86dd76497409451b7e2db6e8df31b77f174b18a23c05de8bf0de92cd37d0ffe5403e60ac974f516a53e43c895f755')

build() {
  cd nvchecker-$pkgver
  python setup.py build 
}

check() {
  cd nvchecker-$pkgver
  python setup.py pytest
}

package() {
  cd nvchecker-$pkgver
  python setup.py install -O1 --prefix=/usr --root="$pkgdir"
}

# vim:set ts=2 sw=2 et:
