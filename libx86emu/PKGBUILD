# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Gergely Imreh <imrehgATgmailDOTcom>

pkgname=libx86emu
pkgver=1.11
pkgrel=1
pkgdesc='x86 emulation library'
arch=('x86_64' 'i686')
url='https://github.com/wfeldt/libx86emu'
license=('BSD')
makedepends=('git')
source=("git://github.com/wfeldt/libx86emu#tag=$pkgver")
md5sums=('SKIP')

build() {
  make -C "$pkgname" LIBDIR=/usr/lib
}

package() {
  make -C "$pkgname" LIBDIR=/usr/lib DESTDIR="$pkgdir" install
  install -Dm644 "$pkgname/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
