# Contributor: Star Brilliant <echo bTEzMjUzQGhvdG1haWwuY29tCg== | base64 -d>

pkgname=stdoutisatty-git
pkgver=8.04881d0
pkgrel=1
pkgdesc='Make programs think their stdout is a tty / terminal.'
arch=('i686' 'x86_64')
url='https://github.com/lilydjwg/stdoutisatty'
license=('BSD')
makedepends=('cmake')
source=('stdoutisatty::git://github.com/lilydjwg/stdoutisatty.git')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/stdoutisatty"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  mkdir "$srcdir/stdoutisatty/build"
  cd "$srcdir/stdoutisatty/build"
  cmake -D CMAKE_INSTALL_PREFIX=/usr ..
  make
}

package() {
  cd "$srcdir/stdoutisatty/build"
  make install DESTDIR="$pkgdir"
}

