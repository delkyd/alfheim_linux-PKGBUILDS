# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>

pkgname=libtorrent-rasterbar
pkgver=1.1.4
pkgrel=2
epoch=1
pkgdesc="A C++ BitTorrent library that aims to be a good alternative to all the other implementations around"
url="http://www.rasterbar.com/products/libtorrent/"
arch=('i686' 'x86_64')
license=('BSD')
depends=('boost-libs')
makedepends=('boost' 'python2' 'python')
options=('!emptydirs')
_pkgver=${pkgver//./_}
source=(https://github.com/arvidn/libtorrent/archive/libtorrent-${_pkgver}/$pkgname-$pkgver.tar.gz
        libtorrent-boost-1.65.patch::"https://github.com/arvidn/libtorrent/commit/7eb3cf6b.patch")
sha512sums=('5a515b44a71582bd81a3794c666f3b88478f7a987527fa6d80bc9b3291932a9f13ca730732056cd155a65934328406eeeebb24b8db5e05c96ea7672ab88b5302'
            'b5b3327c7584b3f2a693c19c8824de5ad8d33713ca88ce29aef9594c552a35355eb30a2876db11b7427d8863116f03df35502acd7d775ea1dc2b5974214e0583')

prepare() {
  mkdir py2 py3
  cd libtorrent-libtorrent-${_pkgver}

  # Fix build with boost 1.65
  patch -p1 -i ../libtorrent-boost-1.65.patch
  # Avoid depending on newer processors
  sed -i 's/-msse4.2//' configure.ac

  ./autotool.sh
}

_build() (
  cd py$1

  # FS#50745
  _boost="boost_python"
  if [ $1 -eq 3 ]; then _boost="boost_python3"; fi

  # https://github.com/qbittorrent/qBittorrent/issues/5265#issuecomment-220007436
  CXXFLAGS="$CXXFLAGS -std=c++11" \
  PYTHON=/usr/bin/python$1 \
  ../libtorrent-libtorrent-${_pkgver}/configure \
    --prefix=/usr \
    --enable-python-binding \
    --enable-examples \
    --disable-static \
    --with-libiconv \
    --with-boost-python=$_boost
)

build() {
  _build 2
  _build 3
}

package() {
  make -C py2 DESTDIR="$pkgdir" install
  make -C py3 DESTDIR="$pkgdir" install
  install -Dm644 libtorrent-libtorrent-${_pkgver}/COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # Remove most example binaries
  rm "$pkgdir"/usr/bin/{*_test,*_tester,simple_client,stats_counters}
}
