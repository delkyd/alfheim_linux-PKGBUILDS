# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kwalletmanager
pkgver=17.08.1
pkgrel=1
pkgdesc='Wallet management tool'
arch=(i686 x86_64)
url='https://www.kde.org/applications/system/kwalletmanager/'
license=(LGPL)
depends=(kio hicolor-icon-theme)
makedepends=(extra-cmake-modules python kcmutils kdoctools)
conflicts=(kdeutils-kwalletmanager)
replaces=(kdeutils-kwalletmanager)
groups=(kde-applications kdeutils)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('00a256906197eab5d9a069e949ad3dd9b61f0b7e6d43a25a9579f1094ca80837'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

