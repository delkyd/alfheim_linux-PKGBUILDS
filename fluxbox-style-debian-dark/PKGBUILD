# Maintainer: Michael Straube <straubem@gmx.de>

pkgname=fluxbox-style-debian-dark
pkgver=1.3.7
_debpkgrel=1~exp1
pkgrel=1
pkgdesc='Fluxbox style from debian'
url='https://www.debian.org/'
license=('custom:Expat')
arch=('any')
source=("http://http.debian.net/debian/pool/main/f/fluxbox/fluxbox_$pkgver-$_debpkgrel.debian.tar.xz")
sha256sums=('0a4bf51f7a9f3a65faf6e7cba0e3862de26b68df9758bf2346c3ef88c1609e09')

prepare() {
  # extract the license from copyright file
  sed -n '12,15p' debian/copyright > LICENSE
  sed -n '40,58p' debian/copyright >> LICENSE

  # change wallpaper path in the style file
  sed -i 's|share/images|share/backgrounds|' \
    debian/additional-themes/Debian/debian-dark
}

package() {
  install -Dm644 debian/additional-themes/Debian/debian-dark \
    "$pkgdir"/usr/share/fluxbox/styles/debian-dark

  install -Dm644 debian/additional-themes/Images/Debian/debian-dark.png \
    "$pkgdir"/usr/share/backgrounds/fluxbox/debian-dark.png

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
