# $Id$
# Maintainer: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Maintainer: Thomas Bächler <thomas@archlinux.org>

pkgname=libnl
pkgver=3.3.0
pkgrel=1
pkgdesc='Library for applications dealing with netlink sockets'
arch=(i686 x86_64)
url='http://www.infradead.org/~tgr/libnl/'
license=(GPL)
depends=(glibc)
backup=(etc/libnl/classid etc/libnl/pktloc)
source=(https://github.com/thom311/libnl/releases/download/libnl${pkgver//./_}/${pkgname}-${pkgver}.tar.gz{,.sig})
sha256sums=('705468b5ae4cd1eb099d2d1c476d6a3abe519bc2810becf12fb1e32de1e074e4'
            'SKIP')
validpgpkeys=('49EA7C670E0850E7419514F629C2366E4DFC5728') # Thomas Haller

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --disable-static
  make
}

check() {
  make -C $pkgname-$pkgver check
}

package() {
  make -C $pkgname-$pkgver DESTDIR="$pkgdir" install
}
