# Maintainer: Cyrus Anakin <cyrantosaurus at gmail dot com>

pkgname=scenarist
pkgver=0.7.1.rc11
pkgrel=1
pkgdesc='Screenwriting software, which developed in Russia.'
url='https://kitscenarist.ru'
arch=('x86_64')
license=('GPL3')
depends=('qt5-webengine')
makedepends=('git' 'make')
source=("${pkgname}::git+https://github.com/dimkanovikov/KITScenarist.git#tag=${pkgver}")
md5sums=('SKIP')


prepare() {

  cd "${srcdir}/${pkgname}/src" &&
  git submodule update --init

  # [GCC 7.2.0] error: ‘isnan’ was not declared in this scope
  sed -i 's/isnan(/std::isnan(/' "${srcdir}/${pkgname}/src/bin/scenarist-desktop/UserInterfaceLayer/Statistics/StatisticsView.cpp"

}


build() {

  cd "${srcdir}/${pkgname}/src" &&
  qmake Scenarist.pro -spec linux-g++ QMAKE_LFLAGS+="-Wl,-rpath,/usr/lib/${pkgname}" &&
  make

}


package() {

  cd "${srcdir}/${pkgname}/build/Release/libs"
  find . -name *.so* -exec install -Dm755 -t "${pkgdir}/usr/lib/${pkgname}" {} \+

  cd "${srcdir}/${pkgname}/build"
  install -Dm755 "Release/bin/scenarist-desktop/Scenarist" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 "Ubuntu/scenarist_amd64/usr/share/applications/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -Dm644 "Ubuntu/scenarist_amd64/usr/share/pixmaps/${pkgname}.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"

}
