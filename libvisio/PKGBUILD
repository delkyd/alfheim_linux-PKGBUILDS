# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: ponsfoot <cabezon dot hashimoto at gmail dot com>
# Contributor: philacs

pkgname=libvisio
pkgver=0.1.5
pkgrel=4
pkgdesc='Library providing ability to interpret and import visio diagrams'
arch=('i686' 'x86_64')
url="https://wiki.documentfoundation.org/DLP/Libraries/libvisio"
license=('LGPL')
depends=('libwpd' 'libwpg' 'libxml2' 'icu' 'librevenge')
makedepends=('boost' 'gperf' 'doxygen' 'cppunit')
source=(https://dev-www.libreoffice.org/src/$pkgname/$pkgname-$pkgver.tar.xz)
sha256sums=('430a067903660bb1b97daf4b045e408a1bb75ca45e615cf05fb1a4da65fc5a8c')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  cd $pkgname-$pkgver
  # Need to set timezone to CET to work around a test failure
  # https://bugs.documentfoundation.org/show_bug.cgi?id=92396
  make -k check TZ=CET
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
