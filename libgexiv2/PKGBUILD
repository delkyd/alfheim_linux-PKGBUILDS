# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=libgexiv2
pkgver=0.10.6
pkgrel=2
pkgdesc='GObject-based wrapper around the Exiv2 library'
url='https://wiki.gnome.org/Projects/gexiv2'
arch=('x86_64' 'i686')
license=('GPL2')
depends=('exiv2' 'glib2')
makedepends=('python-gobject' 'python2-gobject' 'gobject-introspection' 'setconf' 'vala')
source=("https://download.gnome.org/sources/${pkgname/lib/}/${pkgver%.*}/${pkgname/lib/}-$pkgver.tar.xz")
sha256sums=('132788919667254b42c026ab39ab3c3a5be59be8575c05fa4b371ca8aed55825')

prepare() {
  cd "${pkgname/lib/}-$pkgver"

  # fix FS#51673
  setconf Makefile.am typelibdir '$(libdir)/girepository-1.0'
  autoreconf -vi
}

build() {
  cd "${pkgname/lib/}-$pkgver"

  # In order for the build process to include any python-related files,
  # 'import gi; print(gi._overridesdir)' must work with either python or
  # python2. The gi module is provided by python-gobject and python2-gobject.
  ./configure --prefix=/usr --enable-introspection
  make
}

package() {
  make -C "${pkgname/lib/}-$pkgver" DESTDIR="$pkgdir" install
}

# vim: ts=2 sw=2 et:
