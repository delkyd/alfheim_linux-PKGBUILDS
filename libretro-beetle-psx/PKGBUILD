# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgbase=libretro-beetle-psx
pkgname=('libretro-beetle-psx' 'libretro-beetle-psx-hw')
pkgver=1436
pkgrel=1
pkgdesc='Sony PlayStation core'
arch=('i686' 'x86_64')
url='https://github.com/libretro/beetle-psx-libretro'
license=('GPL2')
groups=('libretro')
depends=('gcc-libs' 'glibc')
makedepends=('git' 'libgl' 'mesa' 'vulkan-icd-loader')
_commit='99747d42833809038b7a5c137a06b1b8d0bcdc4e'
source=("libretro-beetle-psx::git+https://github.com/libretro/beetle-psx-libretro.git#commit=${_commit}"
        'https://raw.githubusercontent.com/libretro/libretro-super/master/dist/info/mednafen_psx_libretro.info'
        'https://raw.githubusercontent.com/libretro/libretro-super/master/dist/info/mednafen_psx_hw_libretro.info')
sha256sums=('SKIP'
            '4abaddc3adeecc9695ae0284fd8ead30d855186398392a57f137cfe689de866f'
            'bd911ad07b003b526c10e9dfbfd712bd1bbf427fb0e3bc51ae7196001e7d2a12')

pkgver() {
  cd libretro-beetle-psx

  git rev-list --count HEAD
}

build() {
  cd libretro-beetle-psx

  make
  make HAVE_HW=1
}

package_libretro-beetle-psx() {
  replaces=('libretro-mednafen-psx')

  cd libretro-beetle-psx

  install -dm 755 "${pkgdir}"/usr/{lib/libretro,share/libretro/info}
  install -m 644 mednafen_psx_libretro.so "${pkgdir}"/usr/lib/libretro/
  install -m 644 ../mednafen_psx_libretro.info "${pkgdir}"/usr/share/libretro/info/
}

package_libretro-beetle-psx-hw() {
  depends+=('libgl')
  replaces=('libretro-mednafen-psx-hw')

  cd libretro-beetle-psx

  install -dm 755 "${pkgdir}"/usr/{lib/libretro,share/libretro/info}
  install -m 644 mednafen_psx_hw_libretro.so "${pkgdir}"/usr/lib/libretro/
  install -m 644 ../mednafen_psx_hw_libretro.info "${pkgdir}"/usr/share/libretro/info/
}

# vim: ts=2 sw=2 et:
