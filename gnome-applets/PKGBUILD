# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-applets
pkgver=3.24.1
pkgrel=3
pkgdesc="Small applications for the GNOME Panel"
arch=('i686' 'x86_64')
url="https://wiki.gnome.org/Projects/GnomeApplets"
license=('GPL')
depends=('gnome-panel' 'libgtop' 'libtracker-sparql' 'cpupower' 'upower')
makedepends=('intltool' 'itstool' 'gnome-settings-daemon')
source=(https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz
        0001-brightness-update-D-Bus-service-name-for-GNOME-3.24.patch)
sha256sums=('faacd46a0f115ec8adc9a89e6ce5079ca22e57b5a8bea33a2532cd4eec4aed19'
            '66324b19caf7cd652ef239f6177aecb1d44ffecc6fcf82c46805e451496002d1')

prepare() {
  cd $pkgname-$pkgver
  # brightness: update D-Bus service name for GNOME 3.24
  patch -Np1 -i ../0001-brightness-update-D-Bus-service-name-for-GNOME-3.24.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-schemas-compile --with-cpufreq-lib=cpupower

  #https://bugzilla.gnome.org/show_bug.cgi?id=656231
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
