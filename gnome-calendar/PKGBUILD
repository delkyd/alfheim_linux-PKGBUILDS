# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-calendar
pkgver=3.26.1
pkgrel=1
pkgdesc="Simple and beautiful calendar application designed to perfectly fit the GNOME desktop"
url="https://wiki.gnome.org/Apps/Calendar"
arch=(i686 x86_64)
license=(GPL)
depends=(evolution-data-server gsettings-desktop-schemas)
makedepends=(python appstream-glib git meson gtk-doc)
groups=(gnome-extra)
_commit=3d840aad08f39979bd79197c552a23bb4401f5be  # tags/3.26.1^0
source=("git+https://git.gnome.org/browse/gnome-calendar#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  mkdir build
  cd $pkgname
}

build() {
  cd build
  meson setup --prefix=/usr --buildtype=release ../$pkgname \
    -Denable-gtk-doc=true
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}

# vim:set ts=2 sw=2 et:
