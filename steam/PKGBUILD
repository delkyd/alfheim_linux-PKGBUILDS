# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: K900 <k0009000@gmail.com>

pkgname=steam
pkgver=1.0.0.54
pkgrel=1
pkgdesc="Valve's digital software delivery system"
url='http://steampowered.com/'
arch=('i686' 'x86_64')
license=('custom')
depends=('bash' 'desktop-file-utils' 'hicolor-icon-theme' 'curl' 'dbus' 'freetype2' 'gdk-pixbuf2' 'ttf-font' 'zenity')
depends_x86_64=('lib32-libgl' 'lib32-gcc-libs' 'lib32-libx11' 'lib32-libxss' 'lib32-alsa-plugins')
optdepends=('steam-native-runtime: steam native runtime support')
source=(http://repo.steampowered.com/${pkgname}/pool/${pkgname}/s/${pkgname}/${pkgname}_${pkgver}.tar.gz
        steam-runtime.sh
        steam-native.sh
        alsa_sdl_audiodriver.patch)
sha512sums=('deeacc61d2bd43dfe1c055ae63fc2dfca79c4884eede883215b6434f57a86900f9f61c91cca7fba928582528acc5610c4a3fbf1e2059e8a7e039e78707d7b65b'
            'da40f8e814f547f6a14901d753a2ffed1a4cf83df9200ee86e83a470b57b3103c3fbfb7cc77166ff1db646cbb306d6a41e79450cc1ea113d68a2defc55168ee2'
            'ca25f9de518ef0ae6a2754d06461efb196b3dfdbc1baa6a3933db1a1d11dc1f14b0f0ffcecfb831bf11d9f994686d5c97a1dac22e23065ec75546118fd140226'
            '5c6117d86cb95071246434851bae3bf7d1c2174b0c8744e33e9c98ccc22efae0c956e1d7e3969c572aa96259b1b8164992d265541bce49a24daa12555d825fd5')
prepare() {
  cd ${pkgname}
  patch -Np1 < "${srcdir}/alsa_sdl_audiodriver.patch"

  # apply roundups for udev rules
  sed -r 's|("0666")|"0660", TAG+="uaccess"|g' -i lib/udev/rules.d/99-steam-controller-perms.rules
  sed -r 's|("misc")|\1, OPTIONS+="static_node=uinput"|g' -i lib/udev/rules.d/99-steam-controller-perms.rules
  sed -r 's|(, TAG\+="uaccess")|, MODE="0660"\1|g' -i lib/udev/rules.d/60-HTC-Vive-perms.rules

  # separated runtime/native desktop files
  cp steam{,-native}.desktop
  sed -r 's|(Name=Steam)|\1 (Runtime)|' -i steam.desktop
  sed -r 's|(/usr/bin/steam)|\1-runtime|' -i steam.desktop
  sed -r 's|(Name=Steam)|\1 (Native)|' -i steam-native.desktop
  sed -r 's|(/usr/bin/steam)|\1-native|' -i steam-native.desktop
}

package() {
  cd ${pkgname}
  make DESTDIR="${pkgdir}" install

  install -Dm 755 "${srcdir}/steam-runtime.sh" "${pkgdir}/usr/bin/steam-runtime"
  install -Dm 755 "${srcdir}/steam-native.sh" "${pkgdir}/usr/bin/steam-native"
  install -d "${pkgdir}/usr/lib/steam"
  mv "${pkgdir}/usr/bin/steam" "${pkgdir}/usr/lib/steam/steam"
  ln -sf /usr/bin/steam-runtime "${pkgdir}/usr/bin/steam"

  install -Dm 644 steam-native.desktop -t "${pkgdir}/usr/share/applications"
  install -Dm 644 "${pkgdir}/usr/share/doc/steam/steam_install_agreement.txt" \
    "${pkgdir}/usr/share/licenses/steam/LICENSE"
  install -Dm 644 debian/changelog -t "${pkgdir}/usr/share/doc/${pkgname}"

  # blank steamdeps because apt-get
  ln -sf /usr/bin/true "${pkgdir}/usr/bin/steamdeps"

  install -Dm 644 lib/udev/rules.d/99-steam-controller-perms.rules \
    "${pkgdir}/usr/lib/udev/rules.d/70-steam-controller.rules"
  install -Dm 644 lib/udev/rules.d/60-HTC-Vive-perms.rules \
    "${pkgdir}/usr/lib/udev/rules.d/70-htc-vive.rules"
}

# vim: ts=2 sw=2 et:
