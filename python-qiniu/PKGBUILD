# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-qiniu
pkgname=(python-qiniu python2-qiniu)
pkgver=7.1.7
pkgrel=1
pkgdesc="Qiniu Resource Storage SDK for Python"
arch=('any')
url='https://github.com/qiniu/python-sdk'
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-requests' 'python2-requests')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/qiniu/python-sdk/archive/v$pkgver.tar.gz")
sha512sums=('60fdd8dd31c639ce9ea2be9479cd6a048e38812063b78140d0b138ffbcc096bd57593f33ec30e99d6f2384346338c98cbaefabd4036f4f4a4ab9aea862192613')

prepare() {
  cp -a python-sdk-$pkgver{,-py2}
}

check() {
  # Extracted from .travis.yml
  export QINIU_ACCESS_KEY="QWYn5TFQsLLU1pL5MFEmX3s5DmHdUThav9WyOWOm"
  export QINIU_SECRET_KEY="Bxckh6FA-Fbs9Yt3i3cbKVK22UPBmAOHJcL95pGz"
  export QINIU_TEST_BUCKET="pythonsdk"
  export QINIU_TEST_DOMAIN="pythonsdk.qiniudn.com"
  export QINIU_TEST_ENV="travis"

  cd "$srcdir"/python-sdk-$pkgver
  python setup.py pytest

  cd "$srcdir"/python-sdk-$pkgver-py2
  python2 setup.py pytest
}

package_python-qiniu() {
  depends=('python-requests' 'python-setuptools')

  cd python-sdk-$pkgver
  python setup.py install -O1 --root "$pkgdir"

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-qiniu() {
  depends=('python2-requests' 'python2-setuptools')

  cd python-sdk-$pkgver-py2
  python2 setup.py install -O1 --root "$pkgdir"

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  # Don't conflict with python 3.x version
  mv "$pkgdir"/usr/bin/qiniupy{,2}
}
