# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Jelle van der Waa <jelle@vdwaa.nl>
# Contributor: Pierre Chapuis <catwell@archlinux.us>
# Contributor: Stefano Esposito <ragnarok@email.it>

pkgbase=python-irc
pkgname=('python-irc' 'python2-irc')
pkgver=16.0
pkgrel=1
pkgdesc="IRC (Internet Relay Chat) protocol client library for Python"
depends=('python-six' 'python-pytz' 'python-more-itertools' 'python-jaraco')
makedepends=('python-setuptools' 'python2-setuptools'
    'python2-jaraco' 'python2-more-itertools' 'python2-pytz')
checkdepends=('python-pytest' 'python2-pytest')
arch=('any')
url="http://pypi.python.org/pypi/irc"
license=('LGPL')
source=("https://files.pythonhosted.org/packages/source/i/irc/irc-$pkgver.tar.gz")
md5sums=('c0f7213499d546d5c3af1b9a8a363515')

build() {
  cd "$srcdir"
  cp -a "irc-$pkgver" "irc2-$pkgver"
}

package_python-irc() {
  cd "$srcdir/irc-$pkgver"
  export LC_ALL=en_US.UTF-8
  python3 setup.py install --root="$pkgdir" --optimize=0
}

package_python2-irc() {
  depends=('python2-six' 'python2-pytz' 'python2-more-itertools' 'python2-jaraco')

  cd "$srcdir/irc2-$pkgver"
  python2 setup.py install --root="$pkgdir" --optimize=0
}

check() {
  cd "$srcdir/irc-$pkgver"
  export LC_ALL=en_US.UTF-8
  python3 setup.py test 

  cd "$srcdir/irc2-$pkgver"
  python2 setup.py test 
}
