# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: jiangxq <jiangxueqian at gmail dot com>
# Contributor: zh99998 <zh99998@gmail.com>
# Contributor: 4679kun <admin at 4679 dot us>

pkgname=simple-obfs
pkgver=0.0.3
pkgrel=2
pkgdesc='A simple obfusacting tool designed as plugin server of shadowsocks'
arch=('i686' 'x86_64')
url='https://github.com/shadowsocks/simple-obfs'
license=('GPL')
depends=('libsodium' 'udns' 'libev' 'libcork')
makedepends=('asciidoc' 'xmlto')
source=("$pkgname-$pkgver.tar.gz::https://github.com/shadowsocks/simple-obfs/archive/v$pkgver.tar.gz")
sha512sums=('1e2f1c5a32508426a58d4894a3623d10ff6add875137444cea9bda61d972f3d36bd8b477358f31e719a9950fc745cebff2c632b4fca117cc439a69a747b85837')

prepare() {
  cd simple-obfs-$pkgver
  patch -p1 -i debian/patches/0001-Use-libcork-dev-in-system.patch
}

build() {
  cd simple-obfs-$pkgver
  ./autogen.sh
  ./configure --prefix=/usr --enable-system-shared-lib
  make
}

package() {
  cd simple-obfs-$pkgver
  make DESTDIR="$pkgdir/" install
}
