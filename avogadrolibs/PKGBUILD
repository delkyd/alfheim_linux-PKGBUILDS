# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=avogadrolibs
pkgver=1.90.0
pkgrel=5
pkgdesc="Libraries that provide 3D rendering, visualization, analysis and data processing useful in computational chemistry, molecular modeling, bioinformatics, materials science, and related areas"
arch=(i686 x86_64)
url="http://www.openchemistry.org/"
license=(custom)
depends=(molequeue glew spglib)
makedepends=(cmake eigen python2 boost)
source=($pkgname-$pkgver.tar.gz::"https://github.com/OpenChemistry/avogadrolibs/archive/$pkgver.tar.gz")
md5sums=('f2b7014f48da5b2a72d49c01dafaf225')

prepare() {
  mkdir -p build

# Find spglib
  sed -e 's|NAMES spglib|NAMES spglib symspg|' -i $pkgname-$pkgver/cmake/FindSpglib.cmake
# Fix build with GCC>=7
  sed -e 's|3456|3456789|' -i $pkgname-$pkgver/cmake/GenerateExportHeader.cmake
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir"/$pkgname-$pkgver/COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

# Remove useless static lib
  rm "$pkgdir"/usr/lib/libjsoncpp.a
}
