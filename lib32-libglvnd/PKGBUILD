# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=lib32-libglvnd
pkgver=0.2.999+g4ba53457
_commit=4ba5345742d6c98b528ac28fb7127006d2cc3f75
pkgrel=2
pkgdesc="The GL Vendor-Neutral Dispatch library"
arch=('x86_64')
url="https://github.com/NVIDIA/libglvnd"
license=('custom:BSD-like')
makedepends=('lib32-libx11' 'lib32-libxext' 'glproto' 'git' 'python')
provides=('lib32-libgl' 'lib32-libegl' 'lib32-libgles')
#source=("git+https://github.com/NVIDIA/libglvnd.git#tag=v$pkgver"
source=("git+https://github.com/NVIDIA/libglvnd.git#commit=$_commit")
sha1sums=('SKIP')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  cd libglvnd
  ./autogen.sh
  ./configure --prefix=/usr --libdir=/usr/lib32/ --build=i686-unknown-linux-gnu
  make
}

package() {
  # lib32-libglvnd needs lib32-mesa for indirect rendering
  depends=('lib32-libxext' 'libglvnd' 'lib32-mesa' 'lib32-opengl-driver')
  cd libglvnd
  make DESTDIR="${pkgdir}" install

  rm -r "$pkgdir/usr/include"

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s libglvnd "$pkgdir/usr/share/licenses/$pkgname"
}
