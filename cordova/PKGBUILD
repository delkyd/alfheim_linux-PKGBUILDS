# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=cordova
pkgver=7.0.1
pkgrel=2
pkgdesc='Cordova command line interface tool'
arch=('any')
url='http://cordova.apache.org'
license=('Apache')
depends=('npm' 'acorn' 'semver' 'browserify')
install=cordova.install
source=("https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz")
noextract=("$pkgname-$pkgver.tgz")
sha512sums=('c256867e7d5399e7e0f0d7646da297b50b60a8a6d8f635af5ae418dae9fcd0e05aaf7a87bed27b1649ee884b337d7a69759be11e22f4008ac20bf4f028a8f766')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz
  rm -r "$pkgdir"/usr/etc

  # Why 777? :/
  chmod -R u=rwX,go=rX "$pkgdir"

  # Experimental dedup
  cd "$pkgdir"/usr/lib/node_modules/$pkgname/node_modules
  for dep in acorn semver npm browserify; do
    rm -r $dep;
    npm link $dep;
  done

  cd "$pkgdir"/usr/lib/node_modules/$pkgname/node_modules/syntax-error/node_modules
  for dep in acorn; do
    rm -r $dep;
    npm link $dep;
  done
}
