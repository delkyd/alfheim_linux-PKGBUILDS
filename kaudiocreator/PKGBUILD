# $Id$
# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Nick B <Shirakawasuna at gmail _dot_com>

pkgname=kaudiocreator
pkgver=1.3
pkgrel=9
pkgdesc="A program for ripping and encoding Audio-CDs, encoding files from disk"
arch=('i686' 'x86_64')
url="http://www.kde-apps.org/content/show.php/KAudioCreator?content=107645"
license=('GPL')
depends=('kdebase-runtime' 'taglib' 'libdiscid' 'audiocd-kio4')
makedepends=('cmake' 'automoc4')
source=("https://dl.opendesktop.org/api/files/download/id/1466629608/107645-kaudiocreator-$pkgver.tar.bz2")
md5sums=('6adce848d3cdc9d80a52d35144890c86')

build() {
  cd ${srcdir}

  mkdir build
  cd build

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${srcdir}/build

  make DESTDIR=${pkgdir} install
}
