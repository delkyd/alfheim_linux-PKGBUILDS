# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Vamp898 <vamp898@web.de>
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Todd Maynard <arch@toddmaynard.com>

pkgname=kmymoney
pkgver=4.8.0
pkgrel=5
pkgdesc="Personal finance manager for KDE which operates similarly to MS-Money or Quicken"
arch=('i686' 'x86_64')
url="http://kmymoney2.sourceforge.net/"
license=('GPL')
depends=('kdebase-runtime' 'shared-mime-info' 'aqbanking' 'libofx' 'libalkimia' 'boost-libs' 'kdepimlibs4')
makedepends=('cmake' 'automoc4' 'docbook-xml' 'docbook-xsl' 'boost' 'doxygen' 'patch')
replaces=('kmymoney2')
changelog=$pkgname.changelog
source=(http://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz
        dup-symbols.patch::https://cgit.kde.org/kmymoney.git/patch/?id=6d235f2cd835d5d3e00dee43ed29bdf2c5ca80d5
        4.8_build.patch kmymoney-gpgme-1.7.patch)
sha256sums=('c2a635710aafa6925a49803c480e043e0bf4bc5b62430689aa5eb95619f48ac2'
            '1098e96ec196a1be8f794d7a7e5213513e4ff56730819193c2911b5a9649ad60'
            '21cd9d7fece1b38b87ade8e4fc7f01397cf44a7fe0bc44cd08dda01d05207215'
            '5a240ebedd8e6f80892187fc6543d58125c4832aff0698c0aa53ba6302033535')

prepare() {
  cd "${srcdir}"/$pkgname-$pkgver

#https://bugs.kde.org/show_bug.cgi?id=364355
  patch -Np1 -i "${srcdir}"/4.8_build.patch

  # Build against gpgme++ from gpgme 1.7
  patch -p1 -i "${srcdir}"/kmymoney-gpgme-1.7.patch

#duplicated symbol errors fix
  patch -Np1 -i "${srcdir}"/dup-symbols.patch
}


build() {
  cd "${srcdir}"/$pkgname-$pkgver

  mkdir build
  cd build

  cmake ../ \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=RELEASE -DCMAKE_SKIP_RPATH=YES
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver/build

  make DESTDIR="${pkgdir}" install
}
