# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=libretro-reicast
pkgver=2550
pkgrel=1
pkgdesc='Sega Dreamcast core'
arch=('x86_64')
url='https://github.com/libretro/reicast-emulator'
license=('GPL2')
groups=('libretro')
depends=('gcc-libs' 'glibc' 'libgl')
makedepends=('git' 'mesa')
_commit='148638c654a61aa32df7023498f261c4fb23c9aa'
source=("libretro-reicast::git+https://github.com/libretro/reicast-emulator.git#commit=${_commit}"
        'https://raw.githubusercontent.com/libretro/libretro-super/master/dist/info/reicast_libretro.info')
sha256sums=('SKIP'
            '88f14753a85e7beefdec6bba7edc2a1719cc09f88c77ead67112cc62b742fe3e')

pkgver() {
  cd libretro-reicast

  git rev-list --count HEAD
}

build() {
  cd libretro-reicast

  make
}

package() {
  cd libretro-reicast

  install -dm 755 "${pkgdir}"/usr/{lib/libretro,share/libretro/info}
  install -m 644 reicast_libretro.so "${pkgdir}"/usr/lib/libretro/
  install -m 644 ../reicast_libretro.info "${pkgdir}"/usr/share/libretro/info/
}

# vim: ts=2 sw=2 et:
