# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgbase=python-jaraco
pkgname=('python-jaraco' 'python2-jaraco')
pkgver=2016.10.23
pkgrel=2
pkgdesc='A gaggle of idiosyncratic and questionable wrappers for the stdlib.'
arch=('any')
url='https://github.com/jaraco?page=1&tab=repositories&utf8=%E2%9C%93&q=jaraco'
license=('MIT')
depends=('python-six' 'python-pytz' 'python-more-itertools')
makedepends=('python-setuptools' 'python2-setuptools'
    'python2-backports.functools_lru_cache' 'python2-more-itertools' 'python2-pytz')

# These should not exist and do not deserve 16 packages.
# Furthermore, try to make a replacement for python-irc so we can nuke it all.
_tempora=1.6.1
_text=1.8
_functools=1.15.1
_collections=1.5
_classes=1.4
_stream=1.1.1
_logging=1.5
_itertools=2.0
_pyhost="https://files.pythonhosted.org/packages/source"

source=("$_pyhost/t/tempora/tempora-$_tempora.tar.gz"
        "$_pyhost/j/jaraco.text/jaraco.text-$_text.tar.gz"
        "$_pyhost/j/jaraco.functools/jaraco.functools-$_functools.tar.gz"
        "$_pyhost/j/jaraco.collections/jaraco.collections-$_collections.tar.gz"
        "$_pyhost/j/jaraco.classes/jaraco.classes-$_classes.tar.gz"
        "$_pyhost/j/jaraco.stream/jaraco.stream-$_stream.tar.gz"
        "$_pyhost/j/jaraco.logging/jaraco.logging-$_logging.tar.gz"
        "$_pyhost/j/jaraco.itertools/jaraco.itertools-$_itertools.tar.gz")
md5sums=('0008beb2aa932efc9668c350b9694893'
         '37886d2ad04834ce13cdd525a0ab98f9'
         '4ed8737ad7c4d8ad12fa9d97cdcd3c74'
         'd942e2c6ee54a76eaa7c8e0ad45c799c'
         '83f82d523ef2b5b48107e47f3fbdeb1f'
         '6502157c365081e00c28b047bed7214e'
         '4d21c7d0f457e4a46da514ec0449d7c2'
         'a26a222b010a6b51a0ace62a48a8bf89')

prepare() {
  # remove inflect dep
  cd "$srcdir/jaraco.itertools-$_itertools"
  sed -i "s/'inflect',//" setup.py
  sed -i 's/import inflect//' jaraco/itertools.py
  sed -i 's/ord = .*$/ord = str(self.sliceArgs[2])+"th"/' jaraco/itertools.py

  cd "$srcdir"
  cp -R "tempora-$_tempora"                "py2-tempora-$_tempora"
  cp -R "jaraco.text-$_text"               "py2-jaraco.text-$_text"
  cp -R "jaraco.functools-$_functools"     "py2-jaraco.functools-$_functools"
  cp -R "jaraco.collections-$_collections" "py2-jaraco.collections-$_collections"
  cp -R "jaraco.classes-$_classes"         "py2-jaraco.classes-$_classes"
  cp -R "jaraco.stream-$_stream"           "py2-jaraco.stream-$_stream"
  cp -R "jaraco.logging-$_logging"         "py2-jaraco.logging-$_logging"
  cp -R "jaraco.itertools-$_itertools"     "py2-jaraco.itertools-$_itertools"
}

package_python-jaraco() {
  cd "$srcdir/tempora-$_tempora"
  python3 setup.py install --root="$pkgdir/" --optimize=0
  cd "$srcdir/jaraco.text-$_text"
  python3 setup.py install --root="$pkgdir/" --optimize=0
  cd "$srcdir/jaraco.functools-$_functools"
  python3 setup.py install --root="$pkgdir/" --optimize=0
  cd "$srcdir/jaraco.collections-$_collections"
  python3 setup.py install --root="$pkgdir/" --optimize=0
  cd "$srcdir/jaraco.classes-$_classes"
  python3 setup.py install --root="$pkgdir/" --optimize=0
  cd "$srcdir/jaraco.stream-$_stream"
  python3 setup.py install --root="$pkgdir/" --optimize=0
  cd "$srcdir/jaraco.logging-$_logging"
  python3 setup.py install --root="$pkgdir/" --optimize=0
  cd "$srcdir/jaraco.itertools-$_itertools"
  python3 setup.py install --root="$pkgdir/" --optimize=0

  rm "$pkgdir/usr/bin/calc-prorate"
  rmdir "$pkgdir/usr/bin"
  # None of these packages have a license file?  Fix later.
  #install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-jaraco() {
  depends=('python2-six' 'python2-pytz' 'python2-more-itertools' 'python2-backports.functools_lru_cache')
  cd "$srcdir/py2-tempora-$_tempora"
  python2 setup.py install --root="$pkgdir/" --optimize=0
  cd "$srcdir/py2-jaraco.text-$_text"
  python2 setup.py install --root="$pkgdir/" --optimize=0
  cd "$srcdir/py2-jaraco.functools-$_functools"
  python2 setup.py install --root="$pkgdir/" --optimize=0
  cd "$srcdir/py2-jaraco.collections-$_collections"
  python2 setup.py install --root="$pkgdir/" --optimize=0
  cd "$srcdir/py2-jaraco.classes-$_classes"
  python2 setup.py install --root="$pkgdir/" --optimize=0
  cd "$srcdir/py2-jaraco.stream-$_stream"
  python2 setup.py install --root="$pkgdir/" --optimize=0
  cd "$srcdir/py2-jaraco.logging-$_logging"
  python2 setup.py install --root="$pkgdir/" --optimize=0
  cd "$srcdir/py2-jaraco.itertools-$_itertools"
  python2 setup.py install --root="$pkgdir/" --optimize=0

  rm "$pkgdir/usr/bin/calc-prorate"
  rmdir "$pkgdir/usr/bin"
  #install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
