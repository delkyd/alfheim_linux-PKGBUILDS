# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libktorrent4
pkgver=1.3.1
pkgrel=3
pkgdesc="A BitTorrent protocol implementation - KDE4 version"
arch=(i686 x86_64)
url="https://ktorrent.org/"
license=(GPL2)
depends=(kdelibs qca-qt4)
makedepends=(automoc4 cmake boost doxygen)
source=("https://ftp.netbsd.org/pub/pkgsrc/distfiles/libktorrent-$pkgver.tar.bz2")
sha256sums=('2fe11ccb4bf2028c3da11e52cde890f1b3a90560e548eac89a4f8e1558b09725')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname%4}-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
