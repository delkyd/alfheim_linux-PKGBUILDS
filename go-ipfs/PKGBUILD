# $Id$
# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Contributor: Anatol Pomozov
# Contributor: kpcyrd <git@rxv.cc>
# Contributor: Frederik “Freso” S. Olesen <freso.dk@gmail.com>
# Contributor: Ian Naval <ianonavy@gmail.com>

pkgname=go-ipfs
pkgver=0.4.11
pkgrel=1

pkgdesc='A peer-to-peer hypermedia distribution protocol'
url='http://ipfs.io/'
arch=(i686 x86_64)
license=(MIT)

depends=(glibc)
optdepends=('fuse2: for mounting/advanced use')
makedepends=(go git)

source=(go-ipfs-$pkgver.tar.gz::https://github.com/ipfs/go-ipfs/archive/v$pkgver.tar.gz
        ipfs.service)
sha512sums=('4eee958247f1add6fcaec3f0ffcf922eba881503d293546bc59ac9d42eb6f104f2e012868d9369d44364a9fa5dcaea3d2d51bfb00268ba0b60f760d699815d15'
            '1a6bc7fffc3c3640cda3f33c107424edd698c62bd8509731856b8827ea18be1a1845ef0d5ca15325d48d7d871a78daacc011ff9eacea926bc35899850990c1c6')

prepare() {
  export GOPATH="$PWD"/.gopath
  mkdir -p "$GOPATH"/src/github.com/ipfs
  ln -sf "$PWD"/go-ipfs-$pkgver "$GOPATH"/src/github.com/ipfs/go-ipfs

  cd "$GOPATH"/src/github.com/ipfs/go-ipfs
  make deps
}

build() {
  export PATH="$PATH":"$PWD"/.gopath/bin
  cd "$GOPATH"/src/github.com/ipfs/go-ipfs

  make build
}

package() {
  cd go-ipfs-$pkgver

  install -Dm 755 cmd/ipfs/ipfs "$pkgdir/usr/bin/ipfs"
  install -Dm 644  ../ipfs.service "$pkgdir/usr/lib/systemd/user/ipfs.service"
  install -Dm 644 misc/completion/ipfs-completion.bash "$pkgdir/usr/share/bash-completion/completions/ipfs"
  install -Dm 644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
}
