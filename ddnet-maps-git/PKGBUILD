# Maintainer: Rafael Fontenelle <rafaelff@gnome.org>

_name=ddnet-maps
pkgname=$_name-git
pkgver=r963.g21e1ca0
pkgrel=1
pkgdesc="All released maps with server configs of DDraceNetwork"
arch=(any)
url="https://ddnet.tw"
license=('custom')
makedepends=('git')
provides=('teeworlds-ddnet-maps-git')
conflicts=('teeworlds-ddnet-maps-git')
replaces=('teeworlds-ddnet-maps-git')
backup=('usr/share/ddnet/data/autoexec_server.cfg'
        'usr/share/ddnet/data/reset.cfg'
        'usr/share/ddnet/data/storage.cfg')
source=("git+https://github.com/ddnet/$_name.git")
md5sums=('SKIP')

pkgver() {
  cd $_name
  printf "r%s.g%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd $_name
  msg2 "Removing exec premission from files"
  find types/ -type f -exec chmod 644 {} \;
}

package() {
  cd $_name
  
  _datadir="$pkgdir/usr/share/ddnet/data"
  install -d -m755 "$_datadir/types/"
  
  install -m644 autoexec_server.cfg $_datadir
  install -m644 reset.cfg           $_datadir
  install -m644 storage.cfg         $_datadir
  
    # Disable test flag
  sed '/sv_test_cmds/s/1/0/' -i "$_datadir/autoexec_server.cfg"
  
    # Append a '#' to line of the map type you DO NOT want to be installed
  cp -a types/brutal    $_datadir/types/
  cp -a types/ddmax     $_datadir/types/
  cp -a types/dummy     $_datadir/types/
  cp -a types/insane    $_datadir/types/
  cp -a types/moderate  $_datadir/types/
  cp -a types/novice    $_datadir/types/
  cp -a types/oldschool $_datadir/types/
  cp -a types/race      $_datadir/types/
  cp -a types/solo      $_datadir/types/
}
