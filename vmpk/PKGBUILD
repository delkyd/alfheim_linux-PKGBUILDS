# $Id$
# Maintainer : speps <speps at aur dot archlinux dot org>

pkgname=vmpk
pkgver=0.6.2a
pkgrel=3
pkgdesc="Virtual MIDI Piano Keyboard"
arch=('i686' 'x86_64')
url="http://vmpk.sourceforge.net/"
license=('GPL3')
depends=('drumstick' 'qt5-x11extras')
makedepends=('cmake' 'docbook-xsl' 'qt5-tools' 'libxkbcommon-x11')
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname/${pkgver::-1}/$pkgname-$pkgver.tar.bz2")
md5sums=('8d96d6b615c5d7a3f188275fcd0c08a1')

prepare() {
  cd $pkgname-$pkgver
  [ -d b ] || mkdir b
}

build() {
  cd $pkgname-$pkgver/b
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DCMAKE_BUILD_TYPE=release
  make
}

package() {
  cd $pkgname-$pkgver/b
  make DESTDIR="$pkgdir/" install
}
