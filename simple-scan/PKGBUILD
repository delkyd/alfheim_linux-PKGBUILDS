# $Id$
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Joeny Ang <ang(dot)joeny(at)gmail(dot)com>

pkgname=simple-scan
pkgver=3.26.1
pkgrel=1
pkgdesc="Simple scanning utility"
url="https://launchpad.net/simple-scan"
arch=(i686 x86_64)
license=(GPL3)
depends=(sane dconf gtk3 colord libgusb)
makedepends=(yelp-tools meson vala)
source=(https://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('1e756edd8903d526da1c7e54810277969700a0b9618aa3d105c914e4276ae70e')

prepare() {
  mkdir build
  cd $pkgname-$pkgver
}

build() {
  cd build
  arch-meson ../$pkgname-$pkgver
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
