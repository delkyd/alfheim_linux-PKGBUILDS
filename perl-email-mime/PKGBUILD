# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+aur@gmail.com>

pkgname=perl-email-mime
pkgver=1.940
pkgrel=2
pkgdesc="Easy MIME message parsing"
arch=('any')
url="http://search.cpan.org/dist/Email-MIME"
license=('GPL' 'PerlArtistic')
depends=('perl-email-mime-encodings>=1.3' 'perl-mime-types>=1.13'
	 'perl-email-simple>=2.003' 'perl-email-mime-contenttype>=1.011'
	 'perl-email-messageid')
provides=("perl-email-mime-creator=$pkgver" "perl-email-mime-modifier=$pkgver")
replaces=("perl-email-mime-creator" "perl-email-mime-modifier")
options=('!emptydirs')
source=(http://www.cpan.org/authors/id/R/RJ/RJBS/Email-MIME-$pkgver.tar.gz)
sha256sums=('39ba0ad419c60db6dec22040bc6362502b925afde942dc4df2b85866425edd5e')

build() {
  cd  "$srcdir"/Email-MIME-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package(){
  cd  "$srcdir"/Email-MIME-$pkgver
  make install DESTDIR="$pkgdir"
  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}
