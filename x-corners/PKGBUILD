# Maintainer: Michael Straube <straubem@gmx.de>

pkgname=x-corners
pkgver=1.1.1
pkgrel=2
pkgdesc='A command line tool to set up hot corners under X11'
arch=('i686' 'x86_64')
license=('MIT')
url='https://github.com/mstraube/x-corners'
depends=('libxi')
makedepends=('libxfixes')
source=("$pkgname-$pkgver.tar.gz::https://github.com/mstraube/$pkgname/archive/$pkgver.tar.gz")
sha256sums=('35cdcea53b364de2bf9e0088ba29d5900597a9f907ecc9eb23585972b1c4135a')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's|LDFLAGS =|LDFLAGS +=|' Makefile
}

build() {
  cd $pkgname-$pkgver
  make
}

package() {
  cd $pkgname-$pkgver
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
