# $Id$
# Maintainer: Daniel Micay <danielmicay@gmail.com>
pkgname=openvas-scanner
pkgver=5.0.8
pkgrel=1
pkgdesc='The OpenVAS scanning Daemon'
arch=('i686' 'x86_64')
url='http://www.openvas.org/'
license=('GPL')
depends=('openvas-libraries' 'redis')
makedepends=('cmake' 'doxygen')
groups=(openvas)
# These URLs need to be manually updated on every pkgver upgrade.
# http://wald.intevation.org/frs/?group_id=29
source=("http://wald.intevation.org/frs/download.php/2436/$pkgname-${pkgver}.tar.gz"
        "http://wald.intevation.org/frs/download.php/2437/$pkgname-${pkgver}.tar.gz.asc"
        "$pkgname.service")
sha512sums=('99bf234495351c795fd97d2af6d17ea5e42ae4c1a35051a9917629cd415d57f3ea7d6c41347c1540e1be4bec13bde7d0c163b591e144237d50db451f492af292'
            'SKIP'
            '574a9125928e672a9a22a00cab9c635897eedad0eaae550295c21d45cabd52d1d1cd376f441665d37fd1bb9fa48360039f553c300eb15dd9b83cf14df7f37de5')
validpgpkeys=(
              'C3B468D2288C68B9D526452248479FF648DB4530' # OpenVAS Transfer Integrity
)

build() {
  cd $pkgname-$pkgver
  cmake -DCMAKE_BUILD_TYPE=Release -DSBINDIR=/usr/bin \
    -DCMAKE_INSTALL_PREFIX=/usr -DSYSCONFDIR=/etc -DLOCALSTATEDIR=/var .
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir/" install
  install -Dm644 ../$pkgname.service "$pkgdir/usr/lib/systemd/system/${pkgname}.service"
}
