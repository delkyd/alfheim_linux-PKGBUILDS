# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Tobias Powalowski <t.powa@gmx.de>

pkgname=pwgen
pkgver=2.07
pkgrel=2
pkgdesc='Password generator for creating easily memorable passwords'
arch=('x86_64' 'i686')
url='https://sourceforge.net/projects/pwgen/'
license=('GPL')
depends=('glibc')
source=("https://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz"{,.asc})
sha256sums=('eb74593f58296c21c71cd07933e070492e9222b79cedf81d1a02ce09c0e11556'
            'SKIP')
validpgpkeys=('3AB057B7E78D945C8C5591FBD36F769BC11804F0')

prepare() {
  cd "$pkgname-$pkgver"

  autoconf
}

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr --mandir=/usr/share/man
  make
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
