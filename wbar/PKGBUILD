# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=wbar
pkgver=2.3.4
pkgrel=4
pkgdesc="A quick launch bar developed with speed in mind"
arch=('i686' 'x86_64')
url="https://github.com/rodolf0/wbar"
license=('GPL')
depends=('libglade' 'imlib2' 'gcc-libs')
makedepends=('intltool')
source=(http://wbar.googlecode.com/files/$pkgname-$pkgver.tgz)
sha256sums=('46a69849e4436529aefb5cfb2c94b520d05a53c237e7773b90207f67f97a93e3')

prepare() {
  sed -i 's:etc/bash_completion.d:usr/share/bash-completion/completions/:' $pkgname-$pkgver/etc/Makefile.{am,in}
}

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  ./configure --prefix=/usr --exec_prefix=/usr --sysconfdir=/etc
  make
}

package () {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
