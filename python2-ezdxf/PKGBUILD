# $Id$
# Maintainer: Grey Christoforo <first name at last name dot net>

pkgname=python2-ezdxf
_pkgname=ezdxf
pkgver=0.8.2
pkgrel=1
pkgdesc="A Python package to create/manipulate DXF drawings."
arch=('i686' 'x86_64')
url="https://github.com/mozman/ezdxf"
license=('MIT')
depends=('python2')
source=(https://github.com/mozman/ezdxf/archive/v${pkgver}.tar.gz)
md5sums=('4592c83d3eff0708f23251c272072a25')

prepare() {
  cp -r "${_pkgname}-${pkgver}" "${_pkgname}-${pkgver}-py2"
  # change python to python2
  find "${_pkgname}-${pkgver}-py2" -name '*.py' -type f -exec \
      sed -i 's|env python|env python2|' {} \;
}

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}-py2"
  python2 setup.py build
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}-py2"
  python2 setup.py install --root="${pkgdir}"
  install -Dm644 "LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
