# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Stefano Facchini <stefano.facchini@gmail.com>

pkgname=gnome-boxes
pkgver=3.24.0
pkgrel=1
pkgdesc="Simple GNOME application to access remote or virtual systems"
arch=('i686' 'x86_64')
url="https://wiki.gnome.org/Apps/Boxes"
license=('LGPL')
depends=('cdrkit' 'gtk-vnc' 'libarchive' 'libgudev' 'libosinfo' 'libsecret' 'libtracker-sparql'
         'libvirt-glib' 'mtools' 'qemu' 'spice-gtk3')
makedepends=('gobject-introspection' 'intltool' 'itstool' 'spice-protocol' 'vala')
source=(https://download.gnome.org/sources/$pkgname/${pkgver::4}/$pkgname-$pkgver.tar.xz)
sha256sums=('7495ea7184b6ef650033daf6941328b785b22e522e8142b9d190575d215dec49')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
