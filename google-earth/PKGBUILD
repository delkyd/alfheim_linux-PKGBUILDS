# Maintainer: Det <nimetonmaili g-mail>
# Contributors: 458italia, Madek, Berseker, Syr
# Check the latest version with:
# $ curl -sL https://dl.google.com/linux/earth/deb/dists/stable/main/binary-amd64/Packages | grep -Pom1 "Version: \K[^-]*"

# Attempt to fix crashes and blank Panoramio: "1" to enable.
# - http://forums.fedoraforum.org/showthread.php?p=1678303#post1678303
_attempt_fix=0

pkgname=google-earth
pkgver=7.1.8.3036
pkgrel=1
pkgdesc="Semi-legacy 3D interface to explore the globe, terrain, streets, buildings and other planets"
arch=('i686' 'x86_64')
url="https://www.google.com/earth/index.html"
license=('custom:earth')
depends=('glu' 'hicolor-icon-theme' 'ld-lsb>=3-5' 'libsm' 'libxrender' 'nss')
[[ $_attempt_fix = 1 ]] && depends+=('freeimage' 'libpng15' 'qtwebkit')
optdepends=('catalyst-utils: For AMD Catalyst'
            'nvidia-utils: For the NVIDIA driver')
options=('!emptydirs')
install=$pkgname.install
source=('googleearth.sh'
        'baifaao.cpp'
        'Google-Terms-of-Service.html::https://www.google.com/intl/ALL/policies/terms/index.html'
        'Google-Earth-Additional-Terms-of-Service.html::https://www.google.com/help/terms_maps.html'
        'Legal-Notices-for-Google-Earth-and-Google-Earth-APIs.html::https://www.google.com/help/legalnotices_maps.html'
        'Google-Privacy-Policy.html::https://www.google.com/intl/ALL/policies/privacy/index.html')
source_i686=("google-earth-stable_${pkgver}_i386.deb::https://dl.google.com/linux/earth/deb/pool/main/g/google-earth-stable/google-earth-stable_7.1.8.3036-r0_i386.deb")
source_x86_64=("google-earth-stable_${pkgver}_amd64.deb::https://dl.google.com/linux/earth/deb/pool/main/g/google-earth-stable/google-earth-stable_7.1.8.3036-r0_amd64.deb")
md5sums=('e84f5d51ea3545c131d1794f89f6464a'
         '598d579a1c3199c77850d86ba78f7b44'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP')
md5sums_i686=('bf6ccc911ce09a9ec5dc0e6ee98e92d5')
md5sums_x86_64=('77cb0eacde195c224767a77ccf54c8ef')

# For new crashy version:
#pkgver=7.3.0.3827
#source_i686=("google-earth-stable_${pkgver}_i386.deb::https://dl.google.com/earth/client/current/google-earth-stable_current_i386.deb")
#source_x86_64=("google-earth-stable_${pkgver}_amd64.deb::https://dl.google.com/earth/client/current/google-earth-stable_current_amd64.deb")
#md5sums_i686=('7f4eb5dc6a30ccb8015216adf2517e5c')
#md5sums_x86_64=('c92a95342789e2202efc42a619970034')

_instdir=/opt/google/earth/free/

# Build the baifaao.so
if [[ $_attempt_fix = 1 ]]; then
  build() {
    gcc -I /usr/include/qt4/ -O3 -fPIC --shared baifaao.cpp -o baifaao.so
  }
fi

package() {
  msg2 "Extracting the data.tar.xz..."
  bsdtar -xf data.tar.xz -C "$pkgdir/"

  msg2 "Moving stuff in place..."
  # The .desktop
  mv "$pkgdir"/$_instdir/google-earth.desktop "$pkgdir"/usr/share/applications/

  # Icons
  for i in 16 22 24 32 48 64 128 256; do
    install -Dm644 "$pkgdir"/$_instdir/product_logo_$i.png \
                   "$pkgdir"/usr/share/icons/hicolor/${i}x${i}/apps/google-earth.png
  done

  # Licenses
  for i in 'Google-Terms-of-Service.html' \
           'Google-Earth-Additional-Terms-of-Service.html' \
           'Legal-Notices-for-Google-Earth-and-Google-Earth-APIs.html' \
           'Google-Privacy-Policy.html'; do
     install -Dm644 $i "$pkgdir"/usr/share/licenses/$pkgname/$i
  done

  msg2 "Removing the Debian-intended cron job and duplicated images..."
  rm -r "$pkgdir"/etc/cron.daily/ "$pkgdir"/$_instdir/product_logo_*.png

  if [[ $_attempt_fix = 1 ]]; then
    msg2 "Attempting a fix on Panoramio and certain crashes..."
    # Install baifaao.so
    install -m755 baifaao.so "$pkgdir"/$_instdir/

    # Preload it
    install -m755 googleearth.sh "$pkgdir"/$_instdir/googleearth

    # Remove the old, bundled Qt libs
    rm "$pkgdir"/$_instdir/libQt*
  fi
}
