# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-term-table
pkgver=0.010
pkgrel=1
pkgdesc="Format a header and rows into a table"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/Term-Table"
depends=('perl-importer')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/E/EX/EXODIST/Term-Table-$pkgver.tar.gz")
sha512sums=('7d4b5c19f0efaf530f4d05258e6963be4ebae4488657c722f115cc93c8431940d318e2b228e8a8e7d9923ebee2acc4541ec98d9b78c84091faee8544a5fd76af')

build() {
  cd Term-Table-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Term-Table-$pkgver
  make test
}

package() {
  cd Term-Table-$pkgver
  make DESTDIR="$pkgdir" install
}
