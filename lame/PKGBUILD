# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor:	Ionut Biru <ibiru@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>

pkgname=lame
pkgver=3.99.5
pkgrel=4
pkgdesc="A high quality MPEG Audio Layer III (MP3) encoder"
arch=('i686' 'x86_64')
url="http://lame.sourceforge.net/"
depends=('ncurses')
makedepends=('nasm')
license=('LGPL')
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz"
        "sse.patch" CVE-2017-15018.patch)
md5sums=('84835b313d4a8b68f5349816d33e07ce'
         'ca77f3259ed398ae1c55073dacdd752f'
         'f3707ae5dbc6c84018b925ce98ce6158')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i ../sse.patch
  patch -Np1 -i ../CVE-2017-15018.patch # https://sourceforge.net/p/lame/bugs/480/
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr \
              --enable-nasm \
              --enable-shared
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
