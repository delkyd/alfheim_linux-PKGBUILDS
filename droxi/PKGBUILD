# Maintainer: Brandon Mulcahy <brandon@jangler.info>
_gemname=droxi
pkgname=$_gemname
pkgver=0.5.0
pkgrel=2
pkgdesc='An ftp-like command-line interface to Dropbox'
arch=(any)
url='https://github.com/jangler/droxi'
license=(MIT)
depends=(ruby-dropbox-sdk)
options=(!emptydirs)
source=(https://rubygems.org/downloads/$_gemname-$pkgver.gem)
noextract=($_gemname-$pkgver.gem)
sha256sums=('3a8a1d7003cba3666663875eb560b4e954c40fc8ff5989c70fc2d9deed618c36')

package() {
  local _gemdir="$(ruby -e'puts Gem.default_dir')"
  gem install --ignore-dependencies --no-user-install -i "$pkgdir/$_gemdir" -n "$pkgdir/usr/bin" $_gemname-$pkgver.gem
  rm "$pkgdir/$_gemdir/cache/$_gemname-$pkgver.gem"
  install -D -m644 "$pkgdir/$_gemdir/gems/$_gemname-$pkgver/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
