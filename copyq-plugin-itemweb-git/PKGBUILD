# Maintainer: Karol "Kenji Takahashi" Wo≈∫niak <kenji.sx>

pkgname=copyq-plugin-itemweb-git
pkgver=20151130
pkgrel=1
pkgdesc="Clipboard manager with searchable and editable history. Itemweb plugin."
url="https://github.com/hluk/CopyQ"
arch=('i686' 'x86_64')
license=('GPL3')
depends=('qt5-webkit' 'copyq-git')
makedepends=('cmake' 'git' 'qt5-tools' 'qt5-svg')
provides=('copyq-plugin-itemweb')
conflicts=('copyq-plugin-itemweb')
source=("${pkgname}::git+https://github.com/hluk/CopyQ.git")
md5sums=('SKIP')

pkgver() {
    cd ${srcdir}/${pkgname}
    git log -1 --format="%cd" --date=short | tr -d '-'
}

build() {
    mkdir -p ${srcdir}/${pkgname}/build
    cd ${srcdir}/${pkgname}/build
    cmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DWITH_QT5=TRUE \
        ${srcdir}/${pkgname}
    make itemweb
}

package() {
    cd ${srcdir}/${pkgname}/build
    install -Dm755 plugins/libitemweb.so $pkgdir/usr/lib/copyq/plugins/libitemweb.so
    rm -rf ${srdir}/${pkgname}/build
}

# vim:set ts=4 sw=4 et:
