# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Maintainer: Bruno Pagani (a.k.a. ArchangeGabriel) <archange@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=netcdf
pkgver=4.4.1.1
pkgrel=1
pkgdesc="network Common Data Form interface for array-oriented data access and corresponding library"
arch=('i686' 'x86_64')
url="https://www.unidata.ucar.edu/software/netcdf/"
depends=('hdf5' 'curl')
optdepends=('netcdf-fortran: fortran bindings' 'netcdf-cxx: c++ bindings')
options=('!makeflags')
license=('custom')
source=(${pkgname}-${pkgver}.tar.gz::"https://github.com/Unidata/netcdf-c/archive/v${pkgver}.tar.gz")
sha256sums=('7f040a0542ed3f6d27f3002b074e509614e18d6c515b2005d1537fec01b24909')

build() {
    cd ${pkgname}-c-${pkgver}
    #export CFLAGS="-march=x86-64 -mtune=generic -O0 -pipe -fstack-protector-strong"
    ./configure \
        --prefix=/usr \
        --enable-shared
    make
}

check() {
    cd ${pkgname}-c-${pkgver}
    # As of netcdf-c-4.4.1, all the tests pass except for one; a workaround is
    # to update CFLAGS so no optimization level is used (i.e., remove "-O2").
    # See https://github.com/Unidata/netcdf-c/issues/244
    make check || warning "Tests failed"
}

package() {
    cd ${pkgname}-c-${pkgver}
    make DESTDIR="${pkgdir}" install

    install -Dm644 COPYRIGHT -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}

