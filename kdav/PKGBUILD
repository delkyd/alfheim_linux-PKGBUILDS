# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kdav
pkgver=17.08.1
pkgrel=1
pkgdesc="A DAV protocol implemention with KJobs"
url='https://pim.kde.org/'
arch=(i686 x86_64)
license=(GPL LGPL FDL)
depends=(kio)
makedepends=(extra-cmake-modules)
source=("https://download.kde.org/stable/applications/${pkgver}/src/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('387de44d0fa61ed5587530431851f896f0b555e3679eea6b499f3bf19a6737d5'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
