# $Id$
# Maintainer: Alexandre Filgueira <alexfilgueira@antergos.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=cjs
pkgver=3.4.4
pkgrel=1
pkgdesc="Javascript Bindings for Cinnamon"
arch=(i686 x86_64)
url="https://github.com/linuxmint/cjs"
license=(GPL)
depends=(gtk3 gobject-introspection-runtime js38 dbus-glib)
makedepends=(gnome-common python2 gobject-introspection)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/linuxmint/cjs/archive/${pkgver}.tar.gz")
options=(!emptydirs)
sha512sums=('318a1beef6f6a827266170f0c5c1cfaa8bf48908f96e0ce625aee20ddcd648ea967f09f465ed504f21f923830017d3c4a01f62bab26a1cb84181a96131058d33')

prepare() {
  cd $pkgname-$pkgver
  # Python2 fix
  sed -i 's|#! /usr/bin/env python|#! /usr/bin/env python2|' \
  		verbump.py
}

build() {
  cd $pkgname-$pkgver

  ./autogen.sh --prefix=/usr --disable-static --libexecdir=/usr/lib

  #https://bugzilla.gnome.org/show_bug.cgi?id=656231
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  cd $pkgname-$pkgver
  # Needs a display
  make -k check || :
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
