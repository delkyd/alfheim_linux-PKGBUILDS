# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-flashback
pkgver=3.24.0
pkgrel=1
pkgdesc="GNOME Flashback session"
arch=('i686' 'x86_64')
url="https://wiki.gnome.org/Projects/GnomeFlashback"
license=('GPL')
depends=('gnome-bluetooth' 'gnome-panel' 'gnome-session' 'gnome-settings-daemon' 'gnome-themes-standard' 'libibus' 'metacity' 'nautilus')
makedepends=('intltool')
optdepends=('gnome-backgrounds: Default background'
            'gnome-control-center: System settings'
            'gnome-screensaver: Lock screen'
            'network-manager-applet: Network management')
source=(https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz
        0001-menus-Don-t-exclude-any-desktop-files.patch)
sha256sums=('ebe0a5ce1f634a42332f872b5b4ebb504c2d5d7409b0822525359d62e54f7126'
            '80f5f5d9e0593661920624c3d7b2aa47d1fa1dfe1dcdf9f5bfb83be59eb3c465')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../0001-menus-Don-t-exclude-any-desktop-files.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
