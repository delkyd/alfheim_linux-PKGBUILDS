# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=c2hs
pkgver=0.28.2
pkgrel=6
pkgdesc="C->Haskell FFI tool that gives some cross-language type safety"
url="https://github.com/haskell/c2hs"
license=("GPL2")
arch=('i686' 'x86_64')
depends=('ghc-libs' "haskell-dlist" "haskell-language-c")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('f4171478cafe9f4906735763c2e0bc1ca0a9f56b50e9d59aac07520434920f9e')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-executable-dynamic --prefix=/usr \
        --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname" \
            -f-regression -fbase3
    runhaskell Setup build
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    runhaskell Setup copy --destdir="${pkgdir}"
}
