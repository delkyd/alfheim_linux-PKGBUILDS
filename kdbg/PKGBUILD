# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Eric Belanger <belanger@astro.umontreal.ca>
# Contributor: Jason Taylor <jftaylor21@gmail.com>
# Contributor: Georg Grabler <ggrabler@chello.at>

pkgname=kdbg
pkgver=2.9.1
pkgrel=1
pkgdesc="A gdb GUI for KDE"
arch=('i686' 'x86_64')
url="http://www.kdbg.org"
license=('GPL')
depends=('kdebase-runtime' 'gdb')
makedepends=('automoc4' 'cmake' 'optipng')
source=("http://downloads.sourceforge.net/project/kdbg/Source%20Code/$pkgver/kdbg-$pkgver.tar.gz")
sha256sums=('fd1966b9b6cafb1d9ccafe7fbb7057bcf7102a9f18d726aaa2c3375c9470b3b3')

build() {
  mkdir -p build
  cd build
  cmake ../${pkgname}-${pkgver} -DCMAKE_INSTALL_PREFIX=/usr
  make
  find -name '*.png' -exec optipng -quiet -force -fix {} \;
}

package() {
  cd "$srcdir"/build
  make DESTDIR="$pkgdir" install
}
